//@execute (1,2,3)=17; (4,5,9)=56; (-3,2,-2)=-26
class pa_m11a {
	def m(a: (int, int, int)) -> int {
		return 2 * a.0 - 3 * a.1 + 7 * a.2;
	}
}
component pa_m11 {
	var failed: int;
	def main(a: int, b: int, c: int) -> int {
		var o = pa_m11a.new();
		var m = pa_m11a.m(o, _, _, _);
		return test3(m, a, b, c);
	}
	def test3<T, R>(f: (T, T, T) -> R, p1: T, p2: T, p3: T) -> R {
		var x = f(p1, p2, p3);

		check(f(p1, _,  _), (p2, p3), x);
		check(f(_,  p2, _), (p1, p3), x);
		check(f(_,  _,  p3), (p1, p2), x);

		if (f(p1, _,  _)(p2, p3) != x) failed++;
		if (f(_,  p2, _)(p1, p3) != x) failed++;
		if (f(_,  _,  p3)(p1, p2) != x) failed++;

		test2(f(p1, _,  _), p2, p3, x);
		test2(f(_,  p2, _), p1, p3, x);
		test2(f(_,  _,  p3), p1, p2, x);

		return if(failed == 0, x);
	}
	def test2<T, R>(f: (T, T) -> R, p1: T, p2: T, x: R) {
		check(f(p1, _), p2, x);
		check(f(_, p2), p1, x);

		if (f(p1, _)(p2) != x) failed++;
		if (f(_, p2)(p1) != x) failed++;
	}
	def check<T, R>(f: T -> R, p: T, e: R) {
		if (e != f(p)) failed++;
	}
}
