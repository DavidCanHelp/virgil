//@execute 0=42; 3=42; 67=42; -876278674=42
//@heap-size=20000
type Data #unboxed {
	case X(v: int);
	case Y(v: int);
}

class Tester(v1: (int, int), v2: (int, int)) {
	// Bitmatrix outcomes for a == b cases
	def ID__	=        0b_1000_0100_0010_0001;
	def EQa_	= ID__ | 0b_0000_0001_0000_0100;
	def EQ_b	= ID__ | 0b_0010_0000_1000_0000;
	def EQab 	= EQa_ | EQ_b;

	def vals = [
		Data.X(v1.0),
		Data.Y(v1.1),
		Data.X(v2.0),
		Data.Y(v2.1)
	];

	def match_equals(a: Data, b: Data) -> bool {
		match (a) {
			X(v) => match (b) {
				X(w) => return v == w;
				Y(w) => return false;
			}
			Y(v) => match (b) {
				X(w) => return false;
				Y(w) => return v == w;
			}
		}
	}
	def match_tags(a: Data, b: Data) -> bool {
		match (a) {
			X(v) => match (b) {
				X(w) => return true;
				Y(w) => return false;
			}
			Y(v) => match (b) {
				X(w) => return false;
				Y(w) => return true;
			}
		}
	}
	def tag_equals(a: Data, b: Data) -> bool {
		return a.tag == b.tag;
	}
	def bool_matrix(f: (Data, Data) -> bool) -> u32 {
		var mask = 1u;
		var result = 0u;
		for (i in vals) {
			for (j in vals) {
				if (f(i, j)) result |= mask;
				mask = mask << 1;
			}
		}
		return result;
	}
	def eq_matrix() -> u32 {
		return bool_matrix(Data.==);
	}
	def match_matrix() -> u32 {
		return bool_matrix(match_equals);
	}
	def tag_matrix() -> u32 {
		return bool_matrix(tag_equals);
	}
	def match_tag_matrix() -> u32 {
		return bool_matrix(match_tags);
	}
	def test() -> u32 {
		var r: u32;
		r = match_tag_matrix();
		if (r != EQab) return fail(0x10, r);
		r = tag_matrix();
		if (r != EQab) return fail(0x30, r);
		r = eq_matrix();

		var expect = ID__;
		if (v1.0 == v2.0) expect |= EQa_;
		if (v1.1 == v2.1) expect |= EQ_b;

		r = match_matrix();
		if (r != expect) return fail(0x50, r);
		r = eq_matrix();
		if (r != expect) return fail(0x70, r);
		
		return 0;
	}
}

def fail(code: u32, r: u32) -> u32 {
	return (code << 16) | u32.view(r);
}

def main(a: int) -> u32 {
	var r: u32;
	
	r = Tester.new((0, 0), (0, 0)).test();
	if (r != 0) return fail(0x100, r);
	
	r = Tester.new((a, a), (a, a)).test();
	if (r != 0) return fail(0x300, r);
	
	r = Tester.new((0, 1), (2, 3)).test();
	if (r != 0) return fail(0x500, r);

	r = Tester.new((4, 5), (4, 5)).test();
	if (r != 0) return fail(0x700, r);

	r = Tester.new((11, 7), (11, 5)).test();
	if (r != 0) return fail(0x900, r);

	r = Tester.new((-12, 66666), (-13, 66666)).test();
	if (r != 0) return fail(0xb00, r);
	
	return 42;
}