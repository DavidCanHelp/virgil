// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Implementation of the "System" component for Linux, using the underlying
// Linux methods.

// XXX: not thread-safe
// XXX: use on-stack storage for small IO buffers
// XXX: what about GC moving arrays during IO operations?
// XXX: merge common code with darwin/System.v3 into a common Posix implementation
component System {
	// @thread-local @static buffer for gettimeofday
	var timeval = Array<int>.new(2);
	// @thread-local @static buffer for fstat
	var statbuf = Array<int>.new(LinuxConst.STAT_BUF_SIZE);
	// @thread-local @lazy buffer for path that don't have space for a null
	var pathbuf = Array<byte>.new(100);
	// the standard output stream
	def out = LinuxFileStream.new(LinuxConst.STDOUT);
	// the standard input stream
	def err = LinuxFileStream.new(LinuxConst.STDERR);
	// the method that can print a stack trace
	var printStack: (Pointer, Pointer) -> void;

	// change the permissions of a file
	def chmod(fileName: string, mode: int) {
		Linux.syscall(LinuxConst.SYS_chmod, (pathName(fileName), mode));
	}
	// open a file
	def fileOpen(fileName: string, read: bool) -> int {
		var mode = if(read, LinuxConst.O_RDONLY, LinuxConst.O_WRONLY | LinuxConst.O_TRUNC | LinuxConst.O_CREAT);
		var fd = Linux.syscall(LinuxConst.SYS_open, (pathName(fileName), mode, 420)).0;
		return if(fd >= 0, fd, -1);
	}
	// close a file
	def fileClose(fd: int) {
		Linux.syscall(LinuxConst.SYS_close, fd);
	}
	// read a single byte from a file
	def fileRead(fd: int) -> byte {
		var r = Linux.syscall(LinuxConst.SYS_read, (fd, Pointer.atContents(out.iobuf), 1)).0;
		return if(r > 0, out.iobuf[0]);
	}
	// write some bytes to the file
	def fileWriteK(fd: int, data: Array<byte>, offset: int, len: int) {
		boundsCheck(data, offset, len);
		var buf = Pointer.atContents(data) + offset;
		Linux.syscall(LinuxConst.SYS_write, (fd, buf, len));
	}
	// read some bytes from the file
	def fileReadK(fd: int, data: Array<byte>, offset: int, len: int) -> int {
		boundsCheck(data, offset, len);
		var buf = Pointer.atContents(data) + offset;
		var r = Linux.syscall(LinuxConst.SYS_read, (fd, buf, len));
		return if(r > 0, r);
	}
	// XXX: factor out bounds check to common code?
	private def BCE = "BoundsCheckException";
	private def EMPTY = "";
	private def boundsCheck<T>(array: Array<T>, start: int, len: int) {
		if (start < 0) System.error(BCE, EMPTY);
		if (start > array.length) System.error(BCE, EMPTY);
		var end = u32.!(start) + u32.!(len);
		if (end > u32.!(array.length)) System.error(BCE, EMPTY);
	}
	// calculate bytes remaining to be read from file
	def fileLeft(fd: int) -> int {
		// XXX: is there a cheaper way to tell the number of available bytes?
		var offset = Linux.syscall(LinuxConst.SYS_lseek, (fd, 0, LinuxConst.SEEK_CUR)).0;
		var end = Linux.syscall(LinuxConst.SYS_lseek, (fd, 0, LinuxConst.SEEK_END)).0;
		Linux.syscall(LinuxConst.SYS_lseek, (fd, offset, LinuxConst.SEEK_SET));
		return end - offset;
	}
	// load a file into a byte array
	def fileLoad(fileName: string) -> Array<byte> {
		var path = pathName(fileName);
		if (Linux.syscall(LinuxConst.SYS_stat, (path, Pointer.atContents(statbuf))).0 < 0) return null;
		var fd = Linux.syscall(LinuxConst.SYS_open, (path, LinuxConst.O_RDONLY, 0)).0;
		if (fd < 0) return null;
		var buf = Array<byte>.new(statbuf[LinuxConst.STAT_WOFF_ST_SIZE]);
		if (Linux.syscall(LinuxConst.SYS_read, (fd, Pointer.atContents(buf), buf.length)).0 < 0) return null;
		Linux.syscall(LinuxConst.SYS_close, fd);
		return buf;
	}
	// print a character to standard out
	def putc(ch: byte) {
		out.putc(ch);
	}
	// print an integer (in decimal) to standard out
	def puti(i: int) {
		out.puti(i);
	}
	// print a string (as bytes) to standard out
	def puts(str: string) {
		out.puts(str);
	}
	// prints a newline character to standard out
	def ln() {
		out.ln();
	}
	// output an error, stacktrace, and exit
	def error(ex: string, msg: string) {
		err.putc('!').puts(ex).puts(": ").puts(msg).putc('\n');
		if (printStack != null) printStack(CiRuntime.callerIp() + -1, CiRuntime.callerSp());
		Linux.syscall(LinuxConst.SYS_exit, 254);
	}
	// get ticks in milliseconds
	def ticksMs() -> int {
		Linux.syscall(LinuxConst.SYS_gettimeofday, (Pointer.atContents(timeval), Pointer.NULL));
		return timeval[0] * 1000 + timeval[1] / 1000;
	}
	// get ticks in microseconds
	def ticksUs() -> int {
		Linux.syscall(LinuxConst.SYS_gettimeofday, (Pointer.atContents(timeval), Pointer.NULL));
		return timeval[0] * 1000000 + timeval[1];
	}
	// get ticks in nanoseconds
	def ticksNs() -> int {
		Linux.syscall(LinuxConst.SYS_gettimeofday, (Pointer.atContents(timeval), Pointer.NULL));
		return timeval[1] * 1000;
	}
	// get a zero-terminated pathname, allocating if necessary
	private def pathName(fileName: string) -> Pointer {
		if ((fileName.length & 3) == 0) {
			// no extra space on the end of the array for the null byte
			if (pathbuf.length <= fileName.length) pathbuf = Array.new(fileName.length + 5);
			for (i < fileName.length) pathbuf[i] = fileName[i];
			pathbuf[fileName.length] = '\x00';
			return Pointer.atContents(pathbuf);
		}
		return Pointer.atContents(fileName);
	}
}
// a closure for a file descriptor, e.g. to make STDERR easier to use
class LinuxFileStream {
	def fd: int;
	def iobuf = Array<byte>.new(11); // XXX: use a shared global IO buffer?
	new(fd) { }
	// print an integer to this stream
	def puti(i: int) -> this {
		if (i == 0) {
                	putc('0');
                        return;
                }
		var negative = true;
		if (i > 0) {
			negative = false;
			i = 0 - i;
		}
		var pos = 11;
		while (i != 0) { // XXX: use pointer loop instead?
			var digit = byte.!('0' - i % 10);
			iobuf[--pos] = digit;
			i = i / 10;
		}
		if (negative) iobuf[--pos] = '-';
		Linux.syscall(LinuxConst.SYS_write, (fd, Pointer.atContents(iobuf) + pos, 11 - pos));
	}
	// print a string to this stream
	def puts(str: string) -> this {
		Linux.syscall(LinuxConst.SYS_write, (fd, Pointer.atContents(str), str.length));
	}
	// print a single character to this stream
	def putc(ch: byte) -> this {
		iobuf[0] = ch;
		Linux.syscall(LinuxConst.SYS_write, (fd, Pointer.atContents(iobuf), 1));
	}
	// print a pointer e.g. (0xFF00AB02) to this stream
	def putp(p: Pointer) -> this {
		// XXX: print 64-bit pointers
		var val = p - Pointer.NULL;
		iobuf[0] = '0';
		iobuf[1] = 'x';
		hexChar(2, val >>> 28);
		hexChar(3, val >>> 24);
		hexChar(4, val >>> 20);
		hexChar(5, val >>> 16);
		hexChar(6, val >>> 12);
		hexChar(7, val >>> 8);
		hexChar(8, val >>> 4);
		hexChar(9, val);
		Linux.syscall(LinuxConst.SYS_write, (fd, Pointer.atContents(iobuf), 10));
	}
	private def hexChar(index: int, val: int) {
		var v = val & 0xF;
		iobuf[index] = byte.!(if(v > 9, 'A' + v - 10, '0' + v));
	}
	// print a zero-terminated string to this stream
	def putz(p: Pointer) -> this {
		for (len = 0; true; len++) {
			if ((p + len).load<byte>() == '\x00') {
				Linux.syscall(LinuxConst.SYS_write, (fd, p, len));
                                break;
			}
		}
	}
	def ln() -> this {
		putc('\n');
	}
}
