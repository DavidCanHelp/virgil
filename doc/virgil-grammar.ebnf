Virgil ::=  ToplevelDecl* EOF
ToplevelDecl ::=  ClassDecl | ComponentDecl | VariantDecl | EnumDecl | ExportDecl | VarMember | DefMember

ClassDecl ::= "class" IdentParam ( "(" VarParamDecls? ")" )? ( "extends" TypeRef TupleExpr? )? "{" Member* "}"
ComponentDecl ::= "import"? "component" IDENTIFIER "{" Member* "}"
VariantDecl ::=  "type" IdentParam ( "(" ParamDecls? ")" )? "{" VariantMember* "}"
EnumDecl ::=  "enum" IDENTIFIER ( "(" ParamDecls? ")" )? "{" EnumCases? "}"

Member ::=  DefMember | NewMember | VarMember
VariantMember ::= DefMethod | VariantCase
VarMember ::=  "private"? "var" VarDecls ";"
DefMember ::=  "private"? "def" (("var"? VarDecls ";") | IndexMethod | Method)
NewMember ::=  "new" "(" NewParamDecls? ")" ( ":"? "super" TupleExpr )? BlockStmt
DefMethod ::= "private"? "def" (IndexMethod | Method)
VariantCase ::=  "case" IDENTIFIER ( "(" ParamDecls? ")" )? ( ";" | "{" DefMethod* "}")
EnumCase ::=  IDENTIFIER ( "(" ( Expr ( "," Expr )* )? ")" )?
EnumCases ::= EnumCase ( "," EnumCase )*

VarParamDecl ::=  "var"? IDENTIFIER ":" TypeRef
VarParamDecls ::= VarParamDecl ("," VarParamDecl)*
ParamDecl ::=  IDENTIFIER ":" TypeRef
ParamDecls ::= ParamDecl ("," ParamDecl)*
NewParamDecl ::= IDENTIFIER (":" TypeRef)?
NewParamDecls ::= NewParamDecl ("," NewParamDecl)*
IdentParam ::= IDENTIFIER | (IDENTIFIERP TypeArgs ">")
TypeRef ::=  ( "(" TypeArgs? ")" | IdentParam ( "." IdentParam )* ) ( "->" TypeRef )*
TypeArgs ::= TypeRef ("," TypeRef)*

VarDecl ::= IDENTIFIER ((":" TypeRef) | ("=" Expr) | (":" TypeRef "=" Expr))
VarDecls ::= VarDecl ( "," VarDecl )*
IndexMethod ::= IdentParam "[" VarParamDecls? "]" ( "=" ParamDecl | "->" TypeRef ) MethodBody
Method ::=  IdentParam "(" VarParamDecls? ")" ( "->" ( "this" | TypeRef ) )? MethodBody

MethodBody ::= ";" | BlockStmt
ExportDecl ::=  "export" ( DefMethod | ( STRING | Ident ) ( "=" SymbolParam )? ";" )
Symbol ::= IDENTIFIER ("." IDENTIFIER)*
SymbolParam ::= IdentParam ("." IdentParam)*

BlockStmt ::= "{" Stmt* "}"
Stmt ::=  BlockStmt | EmptyStmt | IfStmt | WhileStmt | MatchStmt | VarStmt | DefStmt | BreakStmt | ContinueStmt | ReturnStmt | ForStmt | ExprStmt
EmptyStmt ::=  ";"
IfStmt ::=  "if" "(" Expr ")" Stmt ( "else" Stmt )?
WhileStmt ::=  "while" "(" Expr ")" Stmt
MatchStmt ::=  "match" "(" Expr ")" "{" ( MatchCase MatchCase* )? "}" ( "else" Stmt )?
MatchCase ::=  ("_" | (MatchPattern ( "," MatchPattern )*)) "=>" Stmt
MatchPattern ::=  IdTypePattern | SymbolPattern | Const
IdTypePattern ::=  IDENTIFIER ":" TypeRef
SymbolPattern ::= Symbol ( "(" ( IDENTIFIER ( "," IDENTIFIER )* )? ")" )?
VarStmt ::=  "var" IDENTIFIER VarDecls ";"
DefStmt ::=  "def" IDENTIFIER VarDecls ";"
BreakStmt ::=  "break" ";"
ContinueStmt ::=  "continue" ";"
ReturnStmt ::=  "return" Expr? ";"
ForStmt ::=  "for" "(" VarDecl ( "<" Expr | "in" Expr | ";" Expr ";" Expr ) ")" Stmt
ExprStmt ::=  Expr ";"

Expr ::= SubExpr (Assign Expr)?
SubExpr ::= InExpr (Infix InExpr)?
InExpr ::= Term TermSuffix*
TermSuffix ::= (MemberSuffix | ApplySuffix | IndexSuffix | IncOrDec)
MemberSuffix ::=  "." ( IdentParam | INTEGER | Operator )
ApplySuffix ::= "(" Exprs? ")"
IndexSuffix ::= "[" Exprs "]"
Term ::= (IncOrDec | "-")? (ParamExpr | Literal | ArrayExpr | TupleExpr | IfExpr)
TupleExpr ::=  "(" Exprs? ")"
ArrayExpr ::=  "[" Exprs? "]"
ParamExpr ::=  "_"
IfExpr ::= "if" "(" Expr "," Expr ( "," Expr )? ")"
Literal ::= Const | "this"
Const ::= CHAR | STRING | INTEGER | FLOAT | "true" | "false" | "null"

IncOrDec ::=  "++" | "--"
Operator ::= Infix | CastOrQuery | "-" | "~" | "[]" | "[]="
CastOrQuery ::= ("!" | "?") ( "<" TypeArgs ">" )?
Assign ::= "=" | "<<=" | ">>=" | "|=" | "&=" | "<<<=" ">>>=" | "+=" | "-=" | "*=" | "/=" | "%=" | "^="
Infix ::=  "==" | "!=" | "||" | "&&" | "<" | "<=" | ">" | ">=" | "|" | "&" | "<<" | "<<<" | ">>" | ">>>" | "+" | "-" | "*" | "/" | "%" | "^"

IDENTIFIER ::= [a-zA-Z] [a-zA-Z0-9_]*
IDENTIFIERP ::= [a-zA-Z] [a-zA-Z0-9_]* "<"
CHAR ::= "'" (HEXCHAR | PRINTABLE | ESCAPE)  "'"
INTEGER ::= [0] | ([-]? ([1-9][0-9]* | "0x" [a-fA-F0-9]+)) [uU]? [lL]?
FLOAT ::= [-]? ([0] | ([1-9][0-9]*)) ('.' [0-9]*)? ([eE] [+-]? ([0] | [1-9][0-9]*))? [fFdD]?
STRING ::= """ ( HEXCHAR | PRINTABLE | ESCAPE )* """
HEXCHAR ::= [x][0-9A-Fa-F][0-9A-Fa-f]
PRINTABLE ::= [A-Za-z0-9`~!@#$%^&*()-_=+\[{\]};:,<.>/?]
ESCAPE ::= [\\][rnbt'"]
