def TEST = UnitTest.new("SsaMarker", runAllTests);
def p0 = SsaParam.new(0, Int.TYPE);
def p1 = SsaParam.new(1, Int.TYPE);
def g = SsaGraph.new([p0, p1], Int.TYPE);

def runAllTests() {
	for (n in [SsaInternalMarker.new, SsaExternalMarker.new]) {
		testOne(n);
		testTwo(n);
		testAlloc(n);
	}
}

def testOne(n: () -> SsaMarker) {
	var m = n().reset(g);

	def get = m.getMark;
	def set = m.setMark;

	TEST.eq(true, get(p0) < 0);
	TEST.eq(true, get(p1) < 0);

	set(p0, 0);
	TEST.eq(0, get(p0));
	TEST.eq(true, get(p1) < 0);

	set(p0, 2);
	TEST.eq(2, get(p0));
	TEST.eq(true, get(p1) < 0);

	set(p1, 33);
	TEST.eq(2, get(p0));
	TEST.eq(33, get(p1));

	TEST.eq(34, m.countMarks());
}

def testTwo(n: () -> SsaMarker) {
	var m1 = n().reset(g);

	m1.setMark(p0, 1);
	m1.setMark(p1, 2);

	TEST.eq(1, m1.getMark(p0));
	TEST.eq(2, m1.getMark(p1));

	var m2 = n().reset(g);

	TEST.eq(true, m2.getMark(p0) < 0);
	TEST.eq(true, m2.getMark(p1) < 0);

	TEST.eq(1, m1.getMark(p0));
	TEST.eq(2, m1.getMark(p1));
}

def testAlloc(n: () -> SsaMarker) {
	var m = n().reset(g);

	var m0 = m.allocMark(p0);
	var m1 = m.allocMark(p1);

	TEST.eq(0, m0);
	TEST.eq(1, m1);

	TEST.eq(0, m.getMark(p0));
	TEST.eq(1, m.getMark(p1));
}
