// Copyright 2010 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A component that contains a number of utility methods for manipulating
// strings, including hashing, comparing, etc.
component Strings {
	def hexMap = "0123456789ABCDEF";

	def hash(str: string) -> int {
		var hashval = str.length;
		for (c in str) hashval = hashval * 31 + c;
		return hashval;
	}
	def equal(arr1: string, arr2: string) -> bool {
		if (arr1 == arr2) return true;
		if (arr1.length != arr2.length) return false;
		for (i = 0; i < arr1.length; i++) {
			if (arr1(i) != arr2(i)) return false;
		}
		return true;
	}
	def newMap<V>() -> PartialMap<string, V> {
		return HashMap.new(hash, equal);
	}
	// render an integer value as decimal into an array at the given position.
	// (there must be sufficient space at the end of the array)
	def renderDecimal(buf: Array<byte>, pos: int, val: int) -> int {
		if (val < 0) {
			// handle negative integers
			if (val == -2147483648) {
				// special case
				Arrays.copyInto("-2147483648", buf, pos);
				return pos + 11;
			}
			buf(pos++) = '-';
			val = 0 - val;
		}
		if (val < 10) {
			// most common case: small one-byte integer
			buf(pos) = hexMap(val);
			return pos + 1;
		} else if (val < 100) {
			// second most common case: two-byte integer
			buf(pos + 1) = hexMap(val % 10);
			buf(pos) = hexMap(val / 10);
			return pos + 2;
		} else {
			// convert decimal integer
			var nonZero = false;
			for (radix = 1000000000; radix > 0; radix = radix / 10) {
				var digit = val / radix;
				val = val % radix;
				if (digit != 0) nonZero = true;
				if (nonZero) buf(pos++) = hexMap(digit);
			}
			return pos;
		}
	}
	// render an integer as hexadecimal into the specified array
	def renderHex8(buf: Array<byte>, pos: int, val: int) -> int {
		var end = pos + 7;
		for (i = 0; i < 8; i++) {
			buf(end - i) = hexMap(val & 0xf);
			val = val #>> 4;
		}
		return end + 1;
	}
	def render(render: StringBuffer -> StringBuffer) -> string {
		return render(StringBuffer.new()).toString();
	}
	def toBuffer(str: string) -> StringBuffer {
		return StringBuffer.new().puts(str);
	}
	def format1<A>(fmt: string, a: A) -> string {
		return StringBuffer.new().format1(fmt, a).toString();
	}
	def format2<A, B>(fmt: string, a: A, b: B) -> string {
		return StringBuffer.new().format2(fmt, a, b).toString();
	}
	def format3<A, B, C>(fmt: string, a: A, b: B, c: C) -> string {
		return StringBuffer.new().format3(fmt, a, b, c).toString();
	}
	def makeFileName(dir: string, fileName: string, ext: string) -> string {
		var buf = StringBuffer.new();
		if (dir != null) {
			buf.puts(dir);
			if (dir(dir.length - 1) != '/') buf.putc('/');
		}
		buf.puts(fileName);
		if (ext != null) buf.putc('.').puts(ext);
		return buf.toString();
	}
	def concat(a: string, b: string) -> string {
		var amax = a.length;
		var r = Array<byte>.new(amax + b.length);
		for (i = 0; i < amax; i++) r(i) = a(i);
		var bmax = b.length;
		for (i = 0; i < bmax; i++) r(i + amax) = b(i);
		return r;
	}
}
// A StringBuffer is a utility for building strings. It contains a number
// of methods for appending characters, integers, strings, or renderable
// functions (i.e. functions of type StringBuffer -> StringBuffer). It uses
// parameterized methods to allow calling formatX() methods with any type
// of arguments.
// XXX: add extract() and grow() methods for more efficiency
class StringBuffer {
	var buffer = Array<byte>.new(20); // TUNABLE: initial size of string buffer
	var length: int;

	// XXX: (deprecated) append a hexadecimal byte
	def appendHexByte(ch: byte) -> StringBuffer {
		acquire(2);
		var l = length;
		buffer(l) = Strings.hexMap(ch #>> 4);
		buffer(l + 1) = Strings.hexMap(ch & 0xF);
		length = l + 2;
		return this;
	}
	// append a boolean
	def appendBool(val: bool) -> StringBuffer {
		if (val) return puts("true");
		else return puts("false");
	}
	// append a char, escaping special characters
	def appendEscapedChar(ch: byte) -> StringBuffer {
		match (ch) {
			'\'': puts("\\\'");
			'\"': puts("\\\"");
			'\t': puts("\\t");
			'\r': puts("\\r");
			'\n': puts("\\n");
			'\\': puts("\\\\");
		} else if (ch <= '~' && ch >= ' ') {
			putc(ch);
		} else {
			// append a hex representation
			putc('\\');
			putc('x');
			putc(Strings.hexMap(ch #>> 4));
			putc(Strings.hexMap(ch & 0xf));
		}
		return this;
	}
	// append a byte
	def putc(ch: byte) -> StringBuffer {
		acquire(1);
		buffer(length++) = ch;
		return this;
	}
	// append a string
	def puts(str: string) -> StringBuffer {
		var max = str.length;
		acquire(max);
		var pos = length;
		for (i = 0; i < max; i++) buffer(pos + i) = str(i);
		length = pos + max;
		return this;
	}
	// append an integer in decimal
	def puti(val: int) -> StringBuffer {
		// check for common case of small, one-byte integer
		if (val >= 0 && val <= 9) return putc(Strings.hexMap(val));
		acquire(11);
		length = Strings.renderDecimal(buffer, length, val);
		return this;
	}
	// append an integer in 0x00000000 format
	def putx(val: int) -> StringBuffer {
		acquire(10);
		buffer(length) = '0';
		buffer(length + 1) = 'x';
		Strings.renderHex8(buffer, length + 2, val);
		length = length + 10;
		return this;
	}
	// append a comma-separated list
	def appendCommaList<T>(list: List<T>, append: (T, StringBuffer) -> StringBuffer) -> StringBuffer {
		if (list == null) return this;
		append(list.head, this);
		for (l = list.tail; l != null; l = l.tail) {
			append(l.head, puts(", "));
		}
		return this;
	}
	// append a comma-separated list of array elements
	def appendArray<T>(array: Array<T>) -> StringBuffer {
		for (i = 0; i < array.length; i++) {
			if (i > 0) puts(", ");
			put1(array(i));
		}
		return this;
	}
	// append a single item of type string|int|byte|bool|(StringBuffer -> StringBuffer)
	def put1<A>(a: A) -> StringBuffer {
		if (string.?(a)) return puts(string.!(a));
		if (int.?(a)) return puti(int.!(a));
		if (byte.?(a)) return putc(byte.!(a));
		if (bool.?(a)) return appendBool(bool.!(a));
		if (Poly.is<A, StringBuffer -> StringBuffer>(a)) {
			return Poly.as<A, StringBuffer -> StringBuffer>(a)(this);
		}
		return this;
	}
	def format1<A>(fmt: string, a: A) -> StringBuffer {
		return format3(fmt, a, (), ());
	}
	def format2<A, B>(fmt: string, a: A, b: B) -> StringBuffer {
		return format3(fmt, a, b, ());
	}
	def format3<A, B, C>(fmt: string, a: A, b: B, c: C) -> StringBuffer {
		var i = 0, max = fmt.length - 1, start = 0;
		while (i < max) {
			if (fmt(i) == '%') {
				appendRange(fmt, start, i);
				start = i + 2;
				var nc = fmt(i + 1);
				if (nc == '1') { put1(a); i = start; continue; }
				if (nc == '2') { put1(b); i = start; continue; }
				if (nc == '3') { put1(c); i = start; continue; }
				if (nc == '%') { start--; continue; }
			}
			i++;
		}
		appendRange(fmt, start, fmt.length);
		return this;
	}
	private def appendRange(str: string, start: int, end: int) {
		acquire(end - start);
		var pos = length;
		for (i = start; i < end; i++) buffer(pos++) = str(i);
		length = pos;
	}
	def end() -> StringBuffer {
		return this;
	}
	// append a line terminator
	def ln() -> StringBuffer {
		putc('\n');
		return this;
	}
	// append a space
	def sp() -> StringBuffer {
		putc(' ');
		return this;
	}
	// reset the size of this buffer, reusing internal storage
	def reset() -> StringBuffer {
		length = 0;
		return this;
	}
	// pad the buffer to the specified size with the pad character
	def pad(ch: byte, nlen: int) -> StringBuffer {
		var diff = nlen - length;
		if (diff <= 0) return this;
		acquire(diff);
		for (i = length; i < nlen; i++) buffer(i) = ch;
		length = nlen;
		return this;
	}
	// convert this buffer to a string
	def toString() -> string {
		return Arrays.range(buffer, 0, length);
	}
	// acquire at least n bytes of internal storage at the end of the array
	private def acquire(n: int) {
		var nlen = length + n, olen = buffer.length;
		if (nlen > olen) buffer = Arrays.copy(buffer, Array<byte>.new(nlen + olen));
	}
}
