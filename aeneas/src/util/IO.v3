// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

class Stream {
	def outK(data: Array<byte>, start: int, len: int);
}

class FileStream extends Stream {
	def file: int;
	new(file) { }
	def outK(data: Array<byte>, offset: int, length: int) {
		System.fileWriteK(file, data, offset, length);
	}
	def close() {
		System.fileClose(file);
	}
}

// A utility to buffer writes of bytes to a flushing function, with the assumption
// that the flushing function is more efficient with larger writes.
class Buffer {
	private def ffunc: (Array<byte>, int, int) -> void;
	private var buffer: DataWriter;
	private var total: int;

	// create a new buffer with the given buffer size which flushes to the given function
	new(bufSize: int, ffunc) {
		buffer = DataWriter.new().grow(bufSize);
		buffer.refill = refill;
	}
	// refill the buffer with the specified amount of space
	def refill(buffer: DataWriter, nlength: int) -> DataWriter {
		var size = nlength - buffer.pos;
		flush();  // write existing data
		if (size > buffer.data.length) buffer.reset(Array<byte>.new(size), 0, 0);
		return buffer;
	}
	// flush any remaining data to the flush function.
	def flush() {
		if (buffer.pos > 0) {
			total = total + buffer.pos;
			ffunc(buffer.data, 0, buffer.pos);
			buffer.at(0);
		}
	}
	// get the buffer which can output to this buffer
	def getBuffer() -> DataWriter { return buffer; }
	// get the total number of bytes output to this buffer
	def getTotal() -> int { return total + buffer.pos; }
}
