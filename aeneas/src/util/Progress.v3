// Copyright 2010 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A utility to print progress indicators for tests (e.g. oXoooXXooo ...)
class ProgressPrinter(total: int, verbose: int) {
	var passed: int;
	var current: string;
	var failed: int;
	var expectedFailed: int;
	var failures: List<(string, string)>;

	def begin(testCase: string) {
		current = testCase;
		if (verbose == 2) {
			Terminal.put(testCase);
			Terminal.put("...");
		}
	}
	def pass() {
		passed++;
		if (verbose > 0) {
			output(Terminal.CTRL_GREEN, 'o', "ok");
		}
	}
	def expectedFail(report: string) {
		passed++;
		expectedFailed++;
		if (verbose == 1) {
			output(Terminal.CTRL_YELLOW, 'E', "expected");
		} else if (verbose == 2) {
			output(Terminal.CTRL_YELLOW, 'E', "expected");
			Terminal.put(" -> ");
			Terminal.putln(report);
		}
	}
	def unexpectedPass() {
		failed++;
		if (verbose == 1) {
			output(Terminal.CTRL_RED, 'P', "passed");
			failures = List.new((current, "unexpected pass"), failures);
		} else if (verbose == 2) {
			output(Terminal.CTRL_RED, 'P', "passed");
			Terminal.put(" -> unexpected pass");
		}
	}
	def fail(report: string) {
		failed++;
		if (verbose == 1) {
			output(Terminal.CTRL_RED, 'X', "failed");
			failures = List.new((current, report), failures);
		} else if (verbose == 2) {
			output(Terminal.CTRL_RED, 'X', "failed");
			Terminal.put(" -> ");
			Terminal.putln(report);
		}
	}
	def space() {
		var done = passed + failed;
		if (done % 10 == 0 || done == total) {
			Terminal.sp();
		}
		if (done % 50 == 0 || done == total) {
			Terminal.put(Terminal.CTRL_DEFAULT);
			Terminal.puti(done);
			Terminal.put(" of ");
			Terminal.puti(total);
			Terminal.ln();
		}
	}
	def output(ctrl: string, ch: byte, str: string) {
		if (verbose == 1) {
			Terminal.put(ctrl);
			Terminal.putc(ch);
			space();
		} else if (verbose == 2) {
			Terminal.put(ctrl);
			Terminal.put(str);
			Terminal.put(Terminal.CTRL_DEFAULT);
			Terminal.ln();
		}
	}
	def report() {
		Terminal.put2("%1 of %2 passed", passed, total);
		if (expectedFailed > 0) Terminal.put1(" (%1 expected failures)", expectedFailed);
		Terminal.ln();
		if (verbose == 1) Lists.apply(failures, printFailure);
	}
	def printFailure(a: string, b: string) {
		Terminal.put(Terminal.CTRL_RED);
		Terminal.put(a);
		Terminal.put(Terminal.CTRL_DEFAULT);
		Terminal.put(": ");
		Terminal.putln(b);
	}
}
