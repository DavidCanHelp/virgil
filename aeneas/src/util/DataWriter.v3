// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Utility for writing data items into an array of bytes, offering routines
// to write bytes, integers, LEBs, etc. A data writer has an internal storage
// that by default collects all bytes written, like a vector, but this behavior
// can be overridden by changing the {refill} method, to e.g., stream the
// already-written bytes to I/O.
class DataWriter {
	def var data: Array<byte>;		// array containing data
	def var pos: int;			// current position
	private var max: int;			// the maximum position ever accessed
	var refill = DataWriter.growI2X;	// refill behavior

	// write a byte
	def putb(data: int) -> this {
		acquire(1)[pos++] = byte.view(data);
	}
	// write two bytes
	def putbb(b0: int, b1: int) -> this {
		var a = acquire(2), p = pos;
		a[p + 0] = byte.view(b0);
		a[p + 1] = byte.view(b1);
		pos = p + 2;
	}
	// write three bytes
	def putbbb(b0: int, b1: int, b2: int) -> this {
		var a = acquire(3), p = pos;
		a[p + 0] = byte.view(b0);
		a[p + 1] = byte.view(b1);
		a[p + 2] = byte.view(b2);
		pos = p + 3;
	}
	// write a 16-bit little-endian integer
	def put_b16(data: int) -> this {
		var a = acquire(2), p = pos;
		a[p + 0] = byte.view(data);
		a[p + 1] = byte.view(data >> 8);
		pos = p + 2;
	}
	// write a 32-bit little-endian integer
	def put_b32(data: int) -> this {
		var a = acquire(4), p = pos;
		a[p + 0] = byte.view(data);
		a[p + 1] = byte.view(data >> 8);
		a[p + 2] = byte.view(data >> 16);
		a[p + 3] = byte.view(data >> 24);
		pos = p + 4;
	}
	// write a 64-bit little-endian integer
	def put_b64(data: long) -> this {
		put_b32(int.view(data));
		put_b32(int.view(data >> 32));
	}
	// write a 16-bit big-endian integer
	def put_b16be(data: int) -> this {
		var a = acquire(2), p = pos;
		a[p + 0] = byte.view(data >> 8);
		a[p + 1] = byte.view(data);
		pos = p + 2;
	}
	// write a 32-bit big-endian integer
	def put_b32be(data: int) -> this {
		var a = acquire(4), p = pos;
		a[p + 0] = byte.view(data >> 24);
		a[p + 1] = byte.view(data >> 16);
		a[p + 2] = byte.view(data >> 8);
		a[p + 3] = byte.view(data);
		pos = p + 4;
	}
	// write a 64-bit big-endian integer
	def put_b64be(data: long) -> this {
		put_b32be(int.view(data >> 32));
		put_b32be(int.view(data));
	}
	// write a signed 32-bit LEB
	def put_sleb32(data: int) -> this {
		if (data >= 0) {
			while (data >= 0x40) {
				putb(0x80 | (data & 0x7F));
				data = data >> 7;
			}
		} else {
			while ((data >> 6) != -1) {
				putb(0x80 | (data & 0x7F));
				data = data >> 7;
			}
		}
		putb(data & 0x7F);
	}
	// write a signed 64-bit LEB
	def put_sleb64(data: long) -> this {
		if (data >= 0) {
			while (data >= 0x40) {
				putb(0x80 | int.view(data & 0x7F));
				data = data >> 7;
			}
		} else {
			while ((data >> 6) != -1) {
				putb(0x80 | int.view(data & 0x7F));
				data = data >> 7;
			}
		}
		putb(int.view(data & 0x7F));
	}
	// write an unsigned 32-bit LEB
	def put_uleb32(data: u32) -> this {
		while (data >= 0x80) {
			putb(int.view(0x80u | (data & 0x7F)));
			data = data >> 7;
		}
		putb(int.view(data));
	}
	// write all the bytes of the given array to this writer
	def puta(data: Array<byte>) -> this {
		if (data == null) return;
		var len = data.length;
		var a = acquire(len), p = pos;
		for (i < len) a[p + i] = data[i];
		pos = p + len;
	}
	// write the bytes from the given range to this writer
	def putk(data: Array<byte>, start: int, end: int) -> this {
		if (data == null) return;
		var len = end - start, a = acquire(len), p = pos;
		for (i < len) a[p + i] = data[start + i];
		pos = p + len;
	}
	// write {length} zero bytes
	def zeroN(length: int) -> this {
		var a = acquire(length), end = pos + length;
		for (p = pos; p < end; p++) a[p] = 0;
		pos = end;
	}
	// skip {length} bytes
	def skipN(length: int) -> this {
		acquire(length);
		pos = pos + length;
	}
	// skip 5 bytes for a 32-bit LEB that will be written later
	def skip_leb32() -> int {
		var oldpos = pos;
		skipN(5);
		return oldpos;
	}
	// overwrite a 32-bit LEB at the given {offset}
	def overwrite_uleb32(offset: int, val: int) -> this {
		var oldpos = pos;
		at(offset);
		for (i < 4) {
			putb(val | 0x80);
			val >>= 7;
		}
		putb(val);
		pos = oldpos;
	}
	// set the current position to {npos}
	def at(npos: int) -> this {
		if (pos > max) max = pos; // remember the maximum pos
		pos = npos;
	}
	// align the current position to a multiple of {size} bytes
	def align(size: int) -> this {
		var rem = pos & (size - 1);
		if (rem > 0) {
			acquire(size - rem);
			pos = pos + size - rem;
		}
	}
	// return the maximum position ever accessed
	def end() -> int {
		if (pos > max) max = pos;
		return max;
	}
	// set the position to be the end
	def atEnd() -> this {
		if (pos > max) max = pos;
		else pos = max;
	}
	// acquire {size} bytes of internal storage
	def acquire(size: int) -> Array<byte> {
		var nsize = pos + size;
		if (data == null || nsize > data.length) refill(this, nsize);
		return data;
	}
	// grow the internal storage to be at least 2X bigger
	private def growI2X(nlength: int) -> this {
		if (data == null) {
			data = Array<byte>.new(10 + nlength);
		} else if (nlength > data.length) {
			data = Arrays.grow(data, data.length * 2 + nlength);
		}
	}
	// grow the internal storage of this data writer to the new length
	def grow(nlength: int) -> this {
		if (data == null) data = Array<byte>.new(nlength);
		else if (nlength > data.length) data = Arrays.grow(data, nlength);
	}
	// copy this data into a new, appropriately-sized array
	def copy() -> Array<byte> {
		if (data == null) return [];
		return Arrays.copy(data, Array<byte>.new(pos));
	}
	// extracts all data from this writer, leaving it empty
	// (more efficient than copy() if the array is sized exactly)
	def extract() -> Array<byte> {
		if (data == null) return [];
		var result = data;
		if (pos != result.length) result = Arrays.copy(result, Array<byte>.new(pos));
		data = null;
		pos = 0;
		return result;
	}
	// clear all bytes to 0 and reset the position
	def clear() -> this {
		pos = 0;
		if (data == null) return;
		var x = data;
		for (i < x.length) x[i] = 0;
	}
	// reset the current data, position, and max
	def reset(ndata: Array<byte>, npos: int, nmax: int) -> this {
		data = ndata;
		pos = npos;
		max = nmax;
	}
	def render(baseAddr: int, startPos: int, size: int, buf: StringBuilder) {
		var line = 16;
		for (i = 0; i < size; i = i + line) {
			buf.puts("  ").putx(baseAddr + i);
			buf.puts(": ");
			var start = startPos + i;
			var end = start + line;
			if (end > max) end = max;
			for (i = start; i < end; i++) {
				buf.putxb(data[i]);
				buf.sp();
			}
			buf.ln();
		}
	}
}
