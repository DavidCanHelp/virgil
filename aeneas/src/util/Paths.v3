// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Utility methods for paths.
component Paths {
	// Assembly a pathname from the directory, file, and extension.
	def assemble(dir: string, fileName: string, ext: string) -> string {
		var buf = StringBuilder.new();
		if (dir != null) {
			buf.puts(dir);
			if (dir[dir.length - 1] != '/') buf.putc('/');
		}
		buf.puts(fileName);
		if (ext != null) buf.putc('.').puts(ext);
		return buf.toString();
	}
	// Get the base name (directory) from a path.
	def baseName(fileName: string) -> string {
		var start = 0, end = fileName.length;
		// skip any path separators
		for (i < end) {
			if (fileName[i] == '/') start = i + 1; // TODO: UNIX specific path parsing
		}
		// skip file extension
		for (j = end - 1; j >= start; j--) {
			if (fileName[j] == '.') { end = j; break; }
		}
		return Arrays.range(fileName, start, end);
	}
}
