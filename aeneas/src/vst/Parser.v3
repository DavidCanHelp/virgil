// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Stateless parser logic for all of Virgil-III syntax
component Parser {
	def KWMAP_SIZE = 100; // size of perfect hashmap for keywords
	def KWMAP_HASH = 37;  // hash parameter of perfect hashmap for keywords

	def NONE      = '\x00';
	def COMMA     = ',';
	def END       = '\x00';
	def kwMap     = Array<string>.new(KWMAP_SIZE);

	def opEqual = V3Op.newEqual(Void.TYPE);
	def opNotEqual = V3Op.newNotEqual(Void.TYPE);

	def PRIVATE_VAR = VarDefParser.new(true, false);
	def PRIVATE_DEF = VarDefParser.new(true, true);
	def PUBLIC_VAR  = VarDefParser.new(false, false);
	def PUBLIC_DEF  = VarDefParser.new(false, true);

	new() {
		keyword("break");
		keyword("case");
		keyword("class");
		keyword("component");
		keyword("continue");
		keyword("def");
		keyword("else");
		keyword("extends");
		keyword("for");
		keyword("if");
		keyword("in");
		keyword("match");
		keyword("new");
		keyword("private");
		keyword("return");
		keyword("super");
		keyword("type");
		keyword("var");
		keyword("while");
	}
	def keyword(kw: string) {
		var index = kwHash(kw) % kwMap.length;
		if (kwMap(index) != null) {
			// could not construct a perfect hash map with the given parameters
			// TODO: automatically search for new perfect hash parameters
			System.error("ParserInternalError", "Could not construct perfect hashmap");
		}
		kwMap(index) = kw;
	}
	def kwHash(s: string) -> int {
		var hash = s.length;
		return s(0) * (KWMAP_HASH + hash) + s(hash - 1);
	}
	def parseFile(fileName: string, input: Array<byte>, ERROR: ErrorGen, typeCache: TypeCache) -> VstFile {
		var file = VstFile.new(fileName, input);
		var p = ParserState.new(file, ERROR, skipToNextToken, typeCache);
		file.input = input;
		file.lineEnds = p.lineEnds;
		var decls: List<Decl>;
		// parse class, component, var, and def declarations
		var list: List<Decl>;
		while (p.curByte != END) {
			var nlist = parseCompound(p, list);
			if (nlist == list) break;
			list = nlist;
		}
		file.decls = Lists.reverse(list);
		return file;
	}
	def parseCompound(p: ParserState, prev: List<Decl>) -> List<Decl> {
		match (p.curByte) {
			'c': {
				if (optKeyword(p, "class") != null) {
					var id = parseIdent(p, parseTypeParam);
					var params = if(p.curByte == '(', Parser.parseList(0, p, '(', Parser.COMMA, ')', Parser.parseParamWithType));
					var parent = if(optKeyword(p, "extends") != null, parseTypeRef(p));
					var superclause: SuperClause;
					if (p.curByte == '(') {
						var args = parseTupleExpr(p);
						superclause = SuperClause.new(args.point(), args);
					}
					var decl = VstClass.new(id.name, id.list(), params, parent, superclause, parseMembers(p));
					return List.new(decl, prev);
				}
				if (optKeyword(p, "component") != null) {
					var id = parseIdentVoid(p).name;
					var decl = VstComponent.new(id, parseMembers(p));
					return List.new(decl, prev);
				}
			}
			'v': if (optKeyword(p, "var") != null) {
				var vars = PUBLIC_VAR.parseVar(p, null);
				for (l = Lists.reverse(vars); l != null; l = l.tail) {
					prev = List.new(l.head, prev); // add each to list in orderw
				}
				return prev;
			}
			'd': if (optKeyword(p, "def") != null) {
				var vars = PUBLIC_DEF.parseDef(p, null);
				for (l = Lists.reverse(vars); l != null; l = l.tail) {
					prev = List.new(l.head, prev); // add each to list in orderw
				}
				return prev;
			}
			't': if (optKeyword(p, "type") != null) {
				var id = parseIdent(p, parseTypeParam);
				var decl = VstVariant.new(id.name, id.list(), null, parseTypeCases(p));
				return List.new(decl, prev);
			}
		}
		p.error("expected class, component, variable, or method declaration"); 
		return prev;
	}
	def parseTypeCases(p: ParserState) -> List<VstMember> {
		p.req1('{');
		var list: List<VstMember>;
		while (true) {
			if (p.curByte == '}') { p.advance1(); break; }
			var nlist = parseTypeCase(p, list);
			if (nlist == list) { p.opt1('}'); break; }
			list = nlist;
		}
		return Lists.reverse(list);
	}
	def parseTypeCase(p: ParserState, prev: List<VstMember>) -> List<VstMember> {
		match (p.curByte) {
			'd': if (optKeyword(p, "def") != null) {
				return PUBLIC_DEF.parseDef(p, prev);
			}
			'c': if (optKeyword(p, "case") != null){ 
				var id = parseIdentVoid(p);
				var params = if(p.curByte == '(', Parser.parseList(0, p, '(', Parser.COMMA, ')', Parser.parseParamWithType));
				var members: List<VstMember>;
				if (p.curByte == ';') p.advance1();
				else members = parseMembers(p);
				var decl = VstVariantCase.new(id.name, params, members);
				return List.new(decl, prev);
			}
		}
		p.error("invalid start of type case declaration");
		return null;
	}
	def parseMembers(p: ParserState) -> List<VstMember> {
		p.req1('{');
		var list: List<VstMember>;
		while (true) {
			if (p.curByte == '}') { p.advance1(); break; }
			var nlist = parseMember(p, list);
			if (nlist == list) { p.opt1('}'); break; }
			list = nlist;
		}
		return Lists.reverse(list);
	}
	def parseMember(p: ParserState, prev: List<VstMember>) -> List<VstMember> {
		var isPrivate = optKeyword(p, "private") != null;
		match (p.curByte) {
			'd': if (optKeyword(p, "def") != null) {
				var parser = if(isPrivate, PRIVATE_DEF, PUBLIC_DEF);
				return parser.parseDef(p, prev);
			}
			'n': { 
				var start = optKeyword(p, "new");
				if (start != null) return List.new(parseNew(p, start), prev);
			}
			'v': if (optKeyword(p, "var") != null) {
				var parser = if(isPrivate, PRIVATE_VAR, PUBLIC_VAR);
				return parser.parseVar(p, prev);
			}
		}
		p.error("invalid start of member declaration");
		return null;
	}
	def parseNew(p: ParserState, start: FilePoint) -> VstNew {
		var params = parseList(0, p, '(', COMMA, ')', parseParam);
		var superclause: SuperClause;
		if (p.curByte == ':') {
			p.advance1();
			superclause = SuperClause.new(reqKeyword(p, "super"), parseTupleExpr(p));
		} else if (p.curByte == 's') {
			superclause = SuperClause.new(reqKeyword(p, "super"), parseTupleExpr(p));
		}
		var body = parseBlockStmt(p);
		var token = Token.new(p.fileName, "new", start.beginLine, start.beginColumn);
		return VstNew.new(token, params, superclause, body);
	}
	def parseParam(p: ParserState) -> ParamDecl {
		var id = parseIdentVoid(p).name;
		var tref = if(p.curByte == ':', (p.advance1(), parseTypeRef(p)).last);
		return ParamDecl.new(id, tref);
	}
	def parseParamWithType(p: ParserState) -> ParamDecl {
		var id = parseIdentVoid(p).name;
		p.req1(':');
		return ParamDecl.new(id, parseTypeRef(p));
	}
	def parseTypeRef(p: ParserState) -> TypeRef {
		var tref: TypeRef;
		if (p.curByte == '(') {
			var list = parseList(0, p, '(', COMMA, ')', parseTypeRef);
			tref = TypeRef.new(null, list.list, Tuple.TYPECON, null);
		} else {
			var id = parseIdent(p, parseTypeRef);
			tref = TypeRef.new(id.name, id.list(), null, null);
		}
		var arrow = p.optN("->");
		while (arrow >= 0) { // type suffixes ( '->' Type )*
			var rtype = parseTypeRef(p);
			tref = TypeRef.new(null, Lists.cons2(tref, rtype), Function.DELEGATE, null);
			arrow = p.optN("->");
		}
		return tref;
	}
	def parseTypeParam(p: ParserState) -> TypeParam {
		var id = parseIdentVoid(p).name;
		return TypeUtil.newTypeParam(id, p.typeCache);
	}
	def parseStmt(p: ParserState) -> Stmt {
		match (p.curByte) {
			'{': return parseBlockStmt(p);
			';': return parseEmptyStmt(p);
			'i': return parseIfStmt(p);
			'w': return parseWhileStmt(p);
			'm': return parseMatchStmt(p);
			'v': {
				var start = optKeyword(p, "var");
				if (start != null) return parseVarStmt(p, start);
			}
			'd': {
				var start = optKeyword(p, "def");
				if (start != null) return parseDefStmt(p, start);
			}
			'b': return parseBreakStmt(p);
			'c': return parseContinueStmt(p);
			'r': return parseReturnStmt(p);
			'f': return parseForStmt(p);
		}
		return parseExprStmt(p);
	}
	def parseBlockStmt(p: ParserState) -> BlockStmt {
		return BlockStmt.new(parseList(0, p, '{', NONE, '}', parseStmt));
	}
	def parseEmptyStmt(p: ParserState) -> Stmt {
		var src = p.point(), x = p.advance1();
		return EmptyStmt.new(src);
	}
	def parseControlExpr(p: ParserState) -> Expr {
		return (p.req1('('), parseExpr(p), p.req1(')')).1;
	}
	def parseIfStmt(p: ParserState) -> Stmt {
		var start = optKeyword(p, "if");
		if (start != null) {
			var cond = parseControlExpr(p);
			var tblock = parseStmt(p);
			var fblock = if(optKeyword(p, "else") != null, parseStmt(p));
			return IfStmt.new(start, cond, tblock, fblock);
		}
		return parseExprStmt(p);
	}
	def parseWhileStmt(p: ParserState) -> Stmt {
		var start = optKeyword(p, "while");
		if (start != null) {
			var cond = parseControlExpr(p);
			var tblock = parseStmt(p);
			return WhileStmt.new(start, cond, tblock);
		}
		return parseExprStmt(p);
	}
	def parseForStmt(p: ParserState) -> Stmt {
		var start = optKeyword(p, "for");
		if (start != null) {
			p.req1('(');
			var decl = PRIVATE_VAR.parseLocal(p);
			if (p.curByte == '<') {
				var token = p.token(1);
				var limit = parseExpr(p);
				p.req1(')');
				var tblock = parseStmt(p);
				decl.isReadOnly = true;
				return ForLessStmt.new(start, decl, token, limit, tblock);
			} else if (optKeyword(p, "in") != null) {
				// for (vardecl in expr) { ... }
				var expr = parseExpr(p);
				p.req1(')');
				var tblock = parseStmt(p);
				decl.isReadOnly = true;
				return ForeachStmt.new(start, decl, expr, tblock);
			} else {
				// for (vardecl; cond; update) { ... }
				p.req1(';');
				var cond = parseExpr(p);
				p.req1(';');
				var update = parseExpr(p);
				p.req1(')');
				var tblock = parseStmt(p);
				return ForStmt.new(start, decl, cond, update, tblock);
			}
		}
		return parseExprStmt(p);
	}
	def parseMatchStmt(p: ParserState) -> Stmt {
		var start = optKeyword(p, "match");
		if (start != null) {
			var key = parseControlExpr(p), defcase: MatchCase;
			var list = parseList(0, p, '{', NONE, '}', parseMatchCase);
			if (optKeyword(p, "else") != null) defcase = MatchCase.new(null, parseStmt(p));
			return MatchStmt.new(start, key, list, defcase);
		}
		return parseExprStmt(p);
	}
	def parseMatchCase(p: ParserState) -> MatchCase {
		var list = parseList(1, p, NONE, COMMA, ':', parseMatchPattern), stmt = parseStmt(p);
		return MatchCase.new(list, stmt);
	}
	def parseMatchPattern(p: ParserState) -> MatchPattern {
		var expr: VarExpr;
		var ch = p.curByte;
		match (ch) {
			'\'': expr = parseByteLiteral(p);
			// XXX: add string literals
		} else {
			if (Char.isIdentStart(ch)) {
				expr = parseVarExpr(p);
				while (p.curByte == '.') {
					var pos = p.point();
					p.advance1();
					if (Char.isIdentStart(p.curByte)) {
						expr = VarExpr.new(expr, pos, parseIdent(p, parseTypeRef));
					} else {
						p.error("identifier expected");
					}
				}
			} else if (ch == '-') {
				expr = parseNumber(p);
			} else if (Char.isDecimal(ch)) {
				expr = parseNumber(p);
			} else {
				p.error("match pattern expected");
			}
		}
		var params = if(p.curByte == '(', Parser.parseList(0, p, '(', Parser.COMMA, ')', parseMatchParam));
		return MatchPattern.new(expr, params);
	}
	def parseMatchParam(p: ParserState) -> VarDecl {
		var id = parseIdentVoid(p);
		var v = VarDecl.new(id.name, null, null);
		v.isReadOnly = true;
		return v;
	}
	def parseVarStmt(p: ParserState, start: FilePoint) -> Stmt {
		var id = parseIdentVoid(p);
		var vars = Lists.reverse(PUBLIC_VAR.parseVars(p, id));
		return LocalStmt.new(start, vars);
	}
	def parseDefStmt(p: ParserState, start: FilePoint) -> Stmt {
		var id = parseIdentVoid(p);
		var vars = Lists.reverse(PUBLIC_DEF.parseVars(p, id));
		return LocalStmt.new(start, vars);
	}
	def parseBreakStmt(p: ParserState) -> Stmt {
		var start = optKeyword(p, "break");
		if (start != null) return BreakStmt.new(term1(p, start, ';'));
		return parseExprStmt(p);
	}
	def parseContinueStmt(p: ParserState) -> Stmt {
		var start = optKeyword(p, "continue");
		if (start != null) return ContinueStmt.new(term1(p, start, ';'));
		return parseExprStmt(p);
	}
	def parseReturnStmt(p: ParserState) -> Stmt {
		var start = optKeyword(p, "return");
		if (start != null) {
			if (p.curByte == ';') return ReturnStmt.new(term1(p, start, ';'), null);
			var expr = parseExpr(p);
			return ReturnStmt.new(term1(p, start, ';'), expr);
		}
		return parseExprStmt(p);
	}
	def parseExprStmt(p: ParserState) -> Stmt {
		var expr = parseExpr(p);
		if (expr == null) return null;
		return ExprStmt.new(term1(p, expr.range(), ';'), expr);
	}
	def parseExpr(p: ParserState) -> Expr {
		var subExpr = parseSubExpr(p);
		if (p.curByte == '=' && p.peek(1) != '=') {
			var pos = p.token(1);
			var rhs = parseExpr(p);
			return AssignExpr.new(subExpr, pos, rhs);
		}
		return addBinOpSuffixes(p, subExpr);
	}
	def parseSubExpr(p: ParserState) -> Expr {
		return addTermSuffixes(p, parseTerm(p));
	}
	def parseTerm(p: ParserState) -> Expr {
		var ch = p.curByte;
		match (ch) {
			'\'': return parseByteLiteral(p);
			'\"': return parseStringLiteral(p);
			'(':  return parseTupleExpr(p);
			'[':  return parseArrayLiteral(p);
			'!':  return parseNotExpr(p);
			'_':  return parseParamExpr(p);
			'-':  {
				if (p.peek(1) == '-') return AutoExpr.new(p.token(2), parseSubExpr(p), V3Infix.Sub, false);
				return parseNumber(p);
			}
			'+':  {
				if (p.peek(1) == '+') return AutoExpr.new(p.token(2), parseSubExpr(p), V3Infix.Add, false);
			}
			'i': {
				if (ch == 'i') {
					var start = optKeyword(p, "if");
					if (start != null) {
						var args = parseList(1, p, '(', COMMA, ')', parseExpr);
						return IfExpr.new(start, args);
					}
				}
			}
		}
		if (Char.isIdentStart(ch)) return parseVarExpr(p);
		else if (Char.isDecimal(ch)) return parseNumber(p);
		p.error("invalid start of expression");
		return null;
	}
	def parseParamExpr(p: ParserState) -> Expr {
		// only "_" is allowed, not "__" or "_id"
		if (Char.isIdentMiddle(p.peek(1))) {
			p.error("identifier expected");
			return null;
		}
		return ParamExpr.new(p.token(1));
	}
	def parseNotExpr(p: ParserState) -> Expr {
		var point = p.point();
		var start = p.req1('!');
		return NotExpr.new(point, parseSubExpr(p));
	}
	def parseByteLiteral(p: ParserState) -> VarExpr {
		var parsed = Byte.parseByte(p.input, p.curPos);
		if (parsed.passed) {
			var token = p.token(parsed.length);
			return newValueExpr(token, Byte.box(parsed.val), Byte.TYPE);
		}
		p.error("invalid character literal");
		return null;
	}
	def parseStringLiteral(p: ParserState) -> StringExpr {
		var parsed = Byte.parseString(p.input, p.curPos);
		if (parsed.passed) {
			var token = p.token(parsed.length);
			return StringExpr.new(token, parsed.val);
		}
		p.error("invalid string literal");
		return null;
	}
	def parseTupleExpr(p: ParserState) -> TupleExpr {
		return TupleExpr.new(parseList(0, p, '(', COMMA, ')', parseExpr));
	}
	def parseArrayLiteral(p: ParserState) -> ArrayExpr {
		return ArrayExpr.new(parseList(0, p, '[', COMMA, ']', parseExpr));
	}
	def parseNumber(p: ParserState) -> VarExpr {
		var np = NumberParser.new(p.input, p.curPos);
		if (np.parse()) {
			var token = p.token(np.pos - np.start);
			return newValueExpr(token, np.val, np.vtype);
		} else {
			p.errorAtOffset(np.error_pos - np.start, np.error_msg);
			p.advance(np.pos - np.start);
			return null;
		}
	}
	def parseVarExpr(p: ParserState) -> VarExpr {
		var id = parseIdent(p, parseTypeRef);
		return VarExpr.new(null, null, id);
	}
	def addTermSuffixes(p: ParserState, expr: Expr) -> Expr {
		while (true) {
			match (p.curByte) {
				'.': { // expr.
					var pos = p.point();
					p.advance1();
					var ch = p.curByte;
					if (Char.isIdentStart(ch)) {
						expr = VarExpr.new(expr, pos, parseIdentUnchecked(p, parseTypeRef));
					} else if (Char.isDecimal(ch)) {
						var end = p.plus(0, Char.isDecimal);
						var token = p.token(end - p.curPos);
						expr = VarExpr.new(expr, pos, VstIdent<TypeRef>.new(token, null));
					} else {
						expr = addMemberOpSuffix(p, pos, expr);
					}
				}
				'(': { // expr(
					expr = AppExpr.new(expr, parseTupleExpr(p));
				}
				'+': if (p.peek(1) == '+') { // expr++
					return AutoExpr.new(p.token(2), expr, V3Infix.Add, true);
				} else break;
				'-': if (p.peek(1) == '-') { // expr--
					return AutoExpr.new(p.token(2), expr, V3Infix.Sub, true);
				} else break;
			} else {
				break;
			}
		}
		return expr;
	}
	def addMemberOpSuffix(p: ParserState, pos: FilePoint, expr: Expr) -> Expr {
		if (p.curByte == '!' || p.curByte == '?') {
			// parse a type cast or type query
			if (p.peek(1) != '=') {
				var hasParams = p.peek(1) == '<', t = p.token(1);
				var typeParams = if(hasParams, parseList(0, p, '<', COMMA, '>', parseTypeRef));
				return VarExpr.new(expr, pos, VstIdent.new(t, typeParams));
			}
		}
		var op = parseInfix(p);
		if (op == null) {
			p.error("member expected");
			return expr;
		}
		return VarExpr.new(expr, pos, VstIdent<TypeRef>.new(op.token, null));
	}
	def addBinOpSuffixes(p: ParserState, expr: Expr) -> Expr {
		var op = parseInfix(p);
		if (op == null) return expr;
		var exprStack = Stack<Expr>.new();
		var operStack = Stack<InfixOp>.new();
		exprStack.push(expr);
		while (op != null) {
			if (operStack.top != null && operStack.peek().prec >= op.prec) { // LR reduction
				reduce(p, exprStack, operStack, op.prec);
			}
			operStack.push(op);
			exprStack.push(parseSubExpr(p));
			op = parseInfix(p);
		}
		reduce(p, exprStack, operStack, -1);
		return exprStack.pop();
	}
	def reduce(p: ParserState, exprStack: Stack<Expr>, operStack: Stack<InfixOp>, prec: int) {
		while (!operStack.empty() && operStack.peek().prec >= prec) {
			var right = exprStack.pop();
			var left = exprStack.pop();
			var binop = operStack.pop();
			exprStack.push(BinOpExpr.new(left, binop, right));
		}
	}
	def parseList<T>(min: int, p: ParserState, begDelim: byte, sepByte: byte, endDelim: byte, f: ParserState -> T) -> VstList<T> {
		var start = p.point();
		if (p.curByte != begDelim) {
			if (begDelim != NONE) {
				p.error1("\'%1\' expected", begDelim);
				return VstList<T>.new(p.point().rangeOf(0), null);
			}
		} else {
			p.advance1();
		}
		if (min == 0 && p.curByte == endDelim) {
			// zero length list
			return VstList<T>.new(eatEnd(p, start), null);
		}
		var list: List<T>;
		var range: FileRange;
		while (true) {
			var lastPos = p.curPos;
			var item = f(p);
			if (p.curPos == lastPos) {
				// didn't parse anything
				p.opt1(endDelim);
				return VstList.new(p.end(start), Lists.reverse(list));
			}
			list = List<T>.new(item, list);
			if (p.curByte == endDelim) {
				// end of list
				range = eatEnd(p, start);
				break;
			}
			if (sepByte == NONE) continue;
			if (p.curByte == sepByte) {
				// separator character
				p.advance1();
			} else {
				var msg = Strings.format2("\'%1\' or \'%2\' expected", sepByte, endDelim);
				p.error(msg);
				range = p.end(start);
				break;
			}
		}
		return VstList.new(range, Lists.reverse(list));
	}
	def eatEnd(p: ParserState, start: FilePoint) -> FileRange {
		var endLine = p.curLine;
		var endColumn = p.curCol + 1;
		p.advance1();
		return FileRange.new(p.fileName, start.beginLine, start.beginColumn, endLine, endColumn);
	}
	def parseIdentVoid(p: ParserState) -> VstIdent<void> {
		return parseIdentCommon(p, null, true);
	}
	def parseIdent<T>(p: ParserState, parse: ParserState -> T) -> VstIdent<T> {
		return parseIdentCommon(p, parse, true);
	}
	def parseIdentUnchecked<T>(p: ParserState, parse: ParserState -> T) -> VstIdent<T> {
		return parseIdentCommon(p, parse, false);
	}
	// parse a parameterized identifier with the supplied parameter parsing function
	def parseIdentCommon<T>(p: ParserState, parse: ParserState -> T, kwCheck: bool) -> VstIdent<T> {
		if (Char.isIdentStart(p.curByte)) {
			var d = p.input;
			for (q = p.curPos + 1; q < d.length; q++) {
				var c = d(q);
				if (c == '<') { // parse parameters, if allowed
					if (parse == null) {
						p.error("identifier cannot be parameterized here");
						return extractIdent(p, q, kwCheck);
					}
					var start = p.curPos;
					var token = p.token(q - start);
					var list = parseList(1, p, '<', COMMA, '>', parse);
					return VstIdent<T>.new(token, list);
				}
				// if the character is part of the middle continue
				if (!Char.isIdentMiddle(c)) return extractIdent(p, q, kwCheck);
			}
			// end of input
			return extractIdent(p, d.length, kwCheck);
		}
		p.error("identifier expected");
		return extractIdent(p, p.curPos, false);
	}
	// extract an unparameterized identifier and advance the parser
	def extractIdent<T>(p: ParserState, end: int, kwCheck: bool) -> VstIdent<T> {
		var token = p.token(end - p.curPos);
		var id = token.image;
		if (kwCheck && id.length >= 2) {
			var kw = kwMap(kwHash(id) % kwMap.length);
			if (kw != null && Strings.equal(kw, id)) {
				p.errorAt(token.range(), Strings.format1("keyword \"%1\" cannot be used as identifier", id));
			}
		}
		return VstIdent<T>.new(token, null);
	}
	def boundOp(p: ParserState, infix: byte, op: Operator) -> InfixOp {
		var i = InfixOp.new(p.token(V3Infix.names(infix).length), infix, V3Infix.prec(infix));
		i.op = op;
		return i;
	}
	// parse an infix or member operator
	def parseInfix(p: ParserState) -> InfixOp {
		var op = '\x00';
		match (p.curByte) {
			'=' : op = if(p.peek(1) == '=', V3Infix.Equal);
			'!' : op = if(p.peek(1) == '=', V3Infix.NotEqual);
			'|' : {
				if (p.peek(1) == '|') return boundOp(p, V3Infix.OrOr, V3Op.opBoolOr);
				else op = V3Infix.Or;
			}
			'&' : {
				if (p.peek(1) == '&') return boundOp(p, V3Infix.AndAnd, V3Op.opBoolAnd);
				else op = V3Infix.And;
			}
			'<' : op = if(p.peek(1) == '=', V3Infix.Lteq, V3Infix.Lt);
			'>' : op = if(p.peek(1) == '=', V3Infix.Gteq, V3Infix.Gt);
			'+' : op = V3Infix.Add;
			'-' : op = V3Infix.Sub;
			'*' : op = V3Infix.Mul;
			'/' : op = V3Infix.Div;
			'%' : op = V3Infix.Mod;
			'^' : op = V3Infix.Xor;
			'#' : {
				var d = p.input, q = p.curPos;
				if (d.length - q > 2) {
					if (d(q + 1) == '>' && d(q + 2) == '>') op = V3Infix.Shr;
					if (d(q + 1) == '<' && d(q + 2) == '<') op = V3Infix.Shl;
				}
			}
		}
		if (op == '\x00') return null;
		return InfixOp.new(p.token(V3Infix.names(op).length), op, V3Infix.prec(op));
	}
	def reqKeyword(p: ParserState, kw: string) -> FilePoint {
		var start = optKeyword(p, kw);
		if (start == null) p.error1("\"%1\" expected", kw);
		return start;
	}
	def optKeyword(p: ParserState, kw: string) -> FilePoint {
		if (p.curByte != kw(0)) return null;
		var i = p.curPos, d = p.input, max = i + kw.length;
		if (max > d.length) return null;
		for (j < kw.length) {
			if (d(i + j) != kw(j)) return null;
		}
		if (max < d.length && Char.isIdentMiddle(d(max))) return null;
		var pt = FilePoint.new(p.fileName, p.curLine, p.curCol);
		p.advance(kw.length);
		return pt;
	}
	def term1(p: ParserState, start: FilePoint, ch: byte) -> FileRange {
		var endLine = p.curLine;
		var endColumn = p.curCol;
		if (p.curByte == ch) {
			p.advance1();
			endColumn++;
		} else {
			p.error1("\"%1\" expected", ch);
		}
		return FileRange.new(p.fileName, start.beginLine, start.beginColumn, endLine, endColumn);
	}
	def skipToNextToken(p: ParserState) {
		// skip forward over whitespace and comments
		var curPos = p.curPos, curLine = p.curLine, curCol = p.curCol;
		var input = p.input, max = input.length;
		while (curPos < max) {
			var ch = input(curPos);
			match (ch) {
				'\t': { curCol = 1 + (((curCol + 8) #>> 3) #<< 3); }
				' ' : { curCol++; }
				'\n': {
					p.lineEnds.add(curPos);
					curCol = 1;
					curLine++;
				}
				'/' : {
					if (curPos >= max - 1 || input(curPos + 1) != '/') break;
					// skip line comment
					curPos = curPos + 2;
					while (curPos < max) {
						if (input(curPos) == '\n') {
							p.lineEnds.add(curPos);
							curCol = 1;
							curLine++;
							break;
						}
						curPos++;
					}
				} 
			} else {
				if (ch < ' ' || ch > '~') {
					// illegal character!
					p.setPos(curPos, curLine, curCol);
					p.error1("invalid character valued %1", int.!(ch));
					curCol++;
				} else {
					// non-whitespace character, break
					break;
				}
			}
			curPos++;
		}
		// reached the end or a non-whitespace character
		p.setPos(curPos, curLine, curCol);
	}
	def skipNone(p: ParserState) {
		// don't skip any characters
	}
	def newValueExpr(token: Token, val: Val, vtype: Type) -> VarExpr {
		var expr = VarExpr.new(null, token, VstIdent<TypeRef>.new(token, null));
		expr.binding = ValueBinding.new(token, val, vtype);
		return expr;
	}
}
// a small closure for parsing var/def declarations
class VarDefParser(isPrivate: bool, isReadOnly: bool) {
	def parseLocal(p: ParserState) -> VarDecl {
		var id = Parser.parseIdentVoid(p).name;
		var tref = if(p.curByte == ':', Parser.parseTypeRef(p.advance1()));
		var init = if(p.curByte == '=', Parser.parseExpr(p.advance1()));
		var decl = VarDecl.new(id, tref, init);
		decl.isReadOnly = isReadOnly;
		return decl;
	}
	def parseVars(p: ParserState, id: VstIdent<void>) -> List<VarDecl> {
		var list: List<VarDecl>;
		while (true) {
			var lastPos = p.curPos;
			var tref = if(p.curByte == ':', Parser.parseTypeRef(p.advance1()));
			var init = if(p.curByte == '=', Parser.parseExpr(p.advance1()));
			var decl = VarDecl.new(id.name, tref, init);
			decl.isReadOnly = isReadOnly;
			list = List.new(decl, list);
			if (p.curByte == ',') {
				p.advance1();
				id = Parser.parseIdentVoid(p);
			} else if (p.curByte == ';') {
				p.advance1();
				break;
			} else {
				p.error("\',\' or \';\' expected");
				break;
			}
		}
		return list;
	}
	def parseDef(p: ParserState, prev: List<VstMember>) -> List<VstMember> {
		var id = Parser.parseIdent(p, Parser.parseTypeParam);
		if (p.curByte == '(' || id.params != null) return parseMethodSuffix(p, id, prev);
		var n = VstIdent<void>.new(id.name, null);
		return parseFieldSuffix(p, n, prev);
	}
	def parseVar(p: ParserState, prev: List<VstMember>) -> List<VstMember> {
		var id = Parser.parseIdentVoid(p);
		return parseFieldSuffix(p, id, prev);
	}
	private def parseFieldSuffix(p: ParserState, id: VstIdent<void>, prev: List<VstMember>) -> List<VstMember> {
		var vars = parseVars(p, id);
		for (l = Lists.reverse(vars); l != null; l = l.tail) {
			var v = l.head;
			prev = List.new(VstField.new(isPrivate, isReadOnly, v.token, v.tref, v.init), prev);
		}
		return prev;
	}
	private def parseMethodSuffix(p: ParserState, id: VstIdent<TypeParam>, prev: List<VstMember>) -> List<VstMember> {
		var params = Parser.parseList(0, p, '(', Parser.COMMA, ')', Parser.parseParamWithType);
		var rtype = if(p.optN("->") >= 0, Parser.parseTypeRef(p), TypeRef.new(null, null, null, Void.TYPE));
		var body = if(p.curByte == ';', EmptyStmt.new(p.token(1)), Parser.parseBlockStmt(p));
		var m = VstMethod.new(isPrivate, id.name, id.list(), params, rtype, body);
		return List.new(m, prev);
	}
}
def NEGATIVE = 0x0001;
def DOUBLE   = 0x0002;
def LONG     = 0x0004;
def HUGE     = 0x0008;
def HEX      = 0x0010;
def BINARY   = 0x0020;
def OCTAL    = 0x0040;
def U_SUFFIX = 0x0080;
def L_SUFFIX = 0x0100;
def F_SUFFIX = 0x0200;
def D_SUFFIX = 0x0400;
def FAIL     = 0x0800;
def CHAR_ZERO = u32.!('0');
def U32_NINE = u32.!(9);
def U32_MAX_DIV_10 = u32.!(429496729);
def U32_MAX_MOD_10 = u32.!(6);
def U64_UPPER_MAX_DIV_10 = u32.!(0x19999999); // (18,446,744,073,709,551,616 / 10) >> 32
def U64_LOWER_MAX_DIV_10 = u32.!(0x99999999); // (18,446,744,073,709,551,616 / 10) & 0xFFFFFFFF
def U64_LOWER_MAX_MOD_10 = u32.!(6); // (18,446,744,073,709,551,616 % 10)
def NONE: (bool, Type, Val) = (false, null, null);
class NumberParser(array: Array<byte>, start: int) {
	var flags: int;
	var pos = start;
	var curByte: byte;
	var error_msg: string;
	var error_pos: int;
	var vtype: Type;
	var val: Val;
	new() {
		if (start < array.length) curByte = array(start);
	}
	def parse() -> bool {
		if (curByte == '-') {
			flags = flags | NEGATIVE;
			if (next() == '0') {
				match (next()) {
					'.', 'f', 'F', 'd', 'D': return parseDouble();
				}
				return error("fractional");
			} else if (Char.isDecimal(curByte)) {
				val = accumNegDecimal();
			} else {
				return error("decimal digit");
			}
		} else if (curByte == '0') {
			match (next()) {
				'x', 'X': {
					if (!Char.isHex(next())) error("hexadecimal digit");
					val = accumHex();
				}
				'b', 'B': {
					if (!Char.isBinary(next())) error("binary digit");
					val = accumBin();
				}
				'0', '1', '2', '3', '4', '5', '6', '7': val = parseOct();
				'8', '9': return error("octal digit");
			} else {
				val = Box.new(0);
			}
		} else if (Char.isDecimal(curByte)) {
			val = accumPosDecimal();
		} else {
			return error("decimal digit");
		}
		vtype = if ((flags & LONG) == 0, Int.TYPE, Int.getType(true, 64));
		match (curByte) {
			'.', 'e', 'E', 'f', 'F', 'd', 'D': return parseDouble();
			'u', 'U': {
				flags = flags | U_SUFFIX;
				match (next()) {
					'l', 'L': {
						flags = flags | L_SUFFIX;
						vtype = Int.getType(false, 64);
						reboxLong(false);
						next();
					}
				} else {
					vtype = Int.getType(false, 32);
					if (Box<(int, int)>.?(val)) {
						if ((flags & HEX) != 0) return error("hexadecimal too large for u32");
						if ((flags & BINARY) != 0) return error("binary literal too large for u32");
						var lval = Box<(int, int)>.!(val).val;
						if (lval.0 != 0) return error("decimal too large for u32");
						val = Box.new(lval.1);
					}
				}
			}
			'l', 'L': {
				flags = flags | L_SUFFIX;
				vtype = Int.getType(true, 64);
				reboxLong(true);
				next();
			}
		}
		return (flags & FAIL) == 0;
	}
	def reboxLong(signed: bool) {
		if (Box<int>.?(val)) {
			var v = Box<int>.!(val).val;
			val = Int64.box(if(signed && v < 0, -1), v);
		}
	}
	def accumHex() -> Val {
		flags = flags | HEX;
		var ival = Char.hexValue(curByte), p = pos;
		while (true) {
			var n = next();
			if (!Char.isHex(n)) return Box.new(ival);
			if (pos > (p + 7)) break;
			ival = (ival #<< 4) | Char.hexValue(n);
		}
		flags = flags | LONG;
		pos--;
		var lval = (0, ival);
		while (true) {
			var n = next();
			if (!Char.isHex(n)) return Int64.box(lval);
			if (pos > (p + 15)) break;
			lval = Int64.shiftin(lval, 4, Char.hexValue(n));
		}
		return huge("hexadecimal", Char.isHex);
	}
	def accumBin() -> Val {
		flags = flags | BINARY;
		var ival = Char.binValue(curByte), p = pos;
		while (true) {
			var b = Char.binValue(next());
			if (b > 1) return Box.new(ival);
			if (pos > (p + 31)) break;
			ival = (ival #<< 1) | b;
		}
		flags = flags | LONG;
		pos--;
		var lval = (0, ival);
		while (true) {
			var b = Char.binValue(next());
			if (b > 1) return Int64.box(lval);
			if (pos > (p + 63)) break;
			lval = Int64.shiftin(lval, 1, b);
		}
		return huge("binary", Char.isBinary);
	}
	def parseOct() -> Val {
		flags = flags | OCTAL;
		error("invalid octal literal");
		while (Char.isOctal(next())) ;  // eat the rest of the digits.
		return null;
	}
	def parseDouble() -> bool {
		// start over from scratch
		pos = start;
		if (start < array.length) curByte = array(start);
		return error("double literal");
	}
	def accumNegDecimal() -> Val {
		var p = pos;
		var i = accumDecimalInt();
		if (i >= 0) return Box.new(0 - i);
		if (i == Int.MIN_VALUE) return Box.new(i);
		reset(p);
		var l = accumDecimalLong();
		return Int64.box(Int64.neg(l));
	}
	def accumPosDecimal() -> Val {
		var p = pos;
		var i = accumDecimalInt();
		if (i >= 0) return Box.new(i);
		reset(p);
		var l = accumDecimalLong();
		return Int64.box(l);
	}
	def accumDecimalInt() -> int {
		var ival = curByte - CHAR_ZERO;
		while (true) {
			var d = next() - CHAR_ZERO;
			if (d > U32_NINE) return int.!(ival);  // not a decimal digit. finish
			if (ival >= U32_MAX_DIV_10) {  // overflow of u32
				if (ival > U32_MAX_DIV_10) break;
				if (d >= U32_MAX_MOD_10) break;
			}
			ival = ival * u32.!(10) + d;
		}
		return -1;
	}
	def accumDecimalLong() -> (int, int) {
		flags = flags | LONG;
		var lval = (0, int.!(curByte - CHAR_ZERO));
		while (true) {
			var d = next() - CHAR_ZERO;
			if (d > U32_NINE) return lval; // not a decimal digit. finish
			if (u32.!(lval.0) >= U64_UPPER_MAX_DIV_10) {
				if (u32.!(lval.0) > U64_UPPER_MAX_DIV_10) break;
				if (u32.!(lval.1) > U64_LOWER_MAX_DIV_10) break;
				if (d > U64_LOWER_MAX_MOD_10) break;
			} else {
				lval = Int64.addu32(Int64.mulu16(lval, u16.!(10)), d);
			}
		}
		huge("decimal", Char.isDecimal);
		return (-1, -1);
	}
	def skipDecimal(expected: bool) {
		if (expected && !Char.isDecimal(curByte)) error("expected decimal digit");
		while (Char.isDecimal(curByte)) next();  // eat the rest of the digits.
	}
	def next() -> byte {
		if (pos < (array.length - 1)) return curByte = array(++pos);
		else return curByte = '\x00';
	}
	def reset(p: int) {
		pos = p;
		if (p < array.length) curByte = array(p);
	}
	def huge(format: string, eat: byte -> bool) -> Val {
		flags = flags | HUGE;
		error(Strings.format1("%1 literal too large", format));
		while (eat(next())) ;
		return null;
	}
	def error(msg: string) -> bool {
		if (error_msg == null) {
			error_msg = msg;
			error_pos = pos;
		}
		flags = flags | FAIL;
		return false;
	}
	def passed() -> bool {
		return (flags & FAIL) == 0;
	}
	def hasSuffix() -> bool {
		return (flags & (U_SUFFIX | L_SUFFIX | D_SUFFIX | F_SUFFIX)) != 0;
	}
}
