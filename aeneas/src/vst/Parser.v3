// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Stateless parser logic for all of Virgil-III syntax
component Parser {
	def KWMAP_SIZE = 61; // size of perfect hashmap for keywords
	def KWMAP_HASH = 3;  // hash parameter of perfect hashmap for keywords

	def KC_OTHER = '\x01';
	def KC_TRUE  = '\x02';
	def KC_FALSE = '\x03';
	def KC_NULL  = '\x04';

	def NONE      = '\x00';
	def COMMA     = ',';
	def END       = '\x00';
	def kwKeys    = Array<string>.new(KWMAP_SIZE);
	def kwClass   = Array<byte>.new(KWMAP_SIZE);

	def PRIVATE_VAR = VarDefParser.new(true, false);
	def PRIVATE_DEF = VarDefParser.new(true, true);
	def PUBLIC_VAR  = VarDefParser.new(false, false);
	def PUBLIC_DEF  = VarDefParser.new(false, true);

	new() {
		keyword("break",	KC_OTHER);
		keyword("case",		KC_OTHER);
		keyword("class",	KC_OTHER);
		keyword("component",	KC_OTHER);
		keyword("continue",	KC_OTHER);
		keyword("def",		KC_OTHER);
		keyword("else",		KC_OTHER);
		keyword("enum",		KC_OTHER);
		keyword("extends",	KC_OTHER);
		keyword("false",	KC_FALSE);
		keyword("for",		KC_OTHER);
		keyword("if",		KC_OTHER);
		keyword("in",		KC_OTHER);
		keyword("match",	KC_OTHER);
		keyword("new",		KC_OTHER);
		keyword("null",		KC_NULL);
		keyword("private",	KC_OTHER);
		keyword("return",	KC_OTHER);
		keyword("super",	KC_OTHER);
		keyword("true",		KC_TRUE);
		keyword("type",		KC_OTHER);
		keyword("var",		KC_OTHER);
		keyword("while",	KC_OTHER);
	}
	def keyword(kw: string, kc: byte) {
		var index = kwIndex(kw);
		if (kwKeys(index) != null) {
			// could not construct a perfect hash map with the given parameters
			System.error("ParserInternalError", "rerun optimizePerfectHashTable()!");
		}
		kwKeys(index) = kw;
		kwClass(index) = kc;
	}
	def optimizePerfectHashTable() -> (int, int) {
		// optimize the perfect hash table size.
		for (size = 20; size < 150; size++) {
			var array = Array<bool>.new(size);
			for (mul in [3, 5, 7, 11, 13, 17]) {
				for (i < array.length) array(i) = false;
				var passed = true;
				for (s in kwKeys) {
					if (s == null) continue;
					var hash = (s.length + s(0) + mul * s(1)) % size;
					if (array(hash)) { passed = false; break; }
					array(hash) = true;
				}
				if (passed) return (mul, size);
			}
		}
		return (-1, -1);
	}
	def kwIndex(s: string) -> int {
		return (s.length + s(0) + KWMAP_HASH * s(1)) % KWMAP_SIZE;
	}
	def parseFile(fileName: string, input: Array<byte>, ERROR: ErrorGen, typeCache: TypeCache) -> VstFile {
		var file = VstFile.new(fileName, input);
		var p = ParserState.new(file, ERROR, skipToNextToken, typeCache);
		file.input = input;
		file.lineEnds = p.lineEnds;
		var decls: List<Decl>;
		// parse class, component, var, and def declarations
		var list: List<Decl>;
		while (p.curByte != END) {
			var nlist = parseCompound(p, list);
			if (nlist == list) break;
			list = nlist;
		}
		file.decls = Lists.reverse(list);
		return file;
	}
	def parseCompound(p: ParserState, prev: List<Decl>) -> List<Decl> {
		match (p.curByte) {
			'c': {
				if (optKeyword(p, "class") != null) {
					var id = parseIdent(p, parseTypeParam);
					var params = parseOptionalParams(p);
					var parent = if(optKeyword(p, "extends") != null, parseTypeRef(p));
					var superclause: SuperClause;
					if (p.curByte == '(') {
						var args = parseTupleExpr(p);
						superclause = SuperClause.new(args.point(), args);
					}
					var decl = VstClass.new(id.name, id.list(), params, parent, superclause, parseMembers(p));
					return List.new(decl, prev);
				}
				if (optKeyword(p, "component") != null) {
					var id = parseIdentVoid(p).name;
					var decl = VstComponent.new(id, parseMembers(p));
					return List.new(decl, prev);
				}
			}
			'v': if (optKeyword(p, "var") != null) {
				var vars = PUBLIC_VAR.parseVar(p, null);
				for (l = Lists.reverse(vars); l != null; l = l.tail) {
					prev = List.new(l.head, prev); // add each to list in orderw
				}
				return prev;
			}
			'd': if (optKeyword(p, "def") != null) {
				var vars = PUBLIC_DEF.parseDef(p, null);
				for (l = Lists.reverse(vars); l != null; l = l.tail) {
					prev = List.new(l.head, prev); // add each to list in orderw
				}
				return prev;
			}
			't': if (optKeyword(p, "type") != null) {
				return parseVariant(p, prev);
			}
			'e': if (optKeyword(p, "enum") != null) {
				return parseEnum(p, prev);
			}
		}
		p.error("expected class, component, variable, or method declaration"); 
		return prev;
	}
	def parseVariant(p: ParserState, prev: List<Decl>) -> List<Decl> {
		var id = parseIdent(p, parseTypeParam);
		var desugar = VariantDesugaring.new(id);
		var decl = desugar.synthesizeTopLevelClass(parseVariantCases(p, desugar));
		var list = List.new(decl, prev);
		for (c in decl.cases) {
			// Lift the desugared variant cases to the top level for verification.
			list = List.new(c.decl, list);
		}
		return list;
	}
	def parseVariantCases(p: ParserState, desugar: VariantDesugaring) -> List<VstMember> {
		var list = desugar.synthesizeMembers(p.fileName, p.curLine, p.curCol);
		p.req1('{');
		while (true) {
			if (p.curByte == '}') { p.advance1(); break; }
			var nlist = parseVariantCase(p, desugar, list);
			if (nlist == list) { p.opt1('}'); break; }
			list = nlist;
		}
		return Lists.reverse(list);
	}
	def parseVariantCase(p: ParserState, desugar: VariantDesugaring, prev: List<VstMember>) -> List<VstMember> {
		match (p.curByte) {
			'd': if (optKeyword(p, "def") != null) {
				return PUBLIC_DEF.parseDef(p, prev);
			}
			'c': if (optKeyword(p, "case") != null){ 
				var id = parseIdentVoid(p);
				var params = parseOptionalParams(p);
				var members: List<VstMember>;
				if (p.curByte == ';') p.advance1();
				else members = parseMembers(p);
				var decl = desugar.synthesizeVariantCaseMember(id, params, members);
				return List.new(decl, prev);
			}
		}
		p.error("invalid start of type case declaration");
		return null;
	}
	def parseEnum(p: ParserState, prev: List<Decl>) -> List<Decl> {
		var id = parseIdentVoid(p);
		var params = parseOptionalParams(p);
		var desugar = VstEnumDesugaring.new(id);
		var list = parseEnumCases(p, desugar);
		return List.new(desugar.synthesizeClass(list), prev);
	}
	def parseEnumCases(p: ParserState, desugar: VstEnumDesugaring) -> List<VstMember> {
		var list = desugar.synthesizeMembers(p.fileName, p.curLine, p.curCol);
		p.req1('{');
		while (true) {
			if (p.curByte == '}') { p.advance1(); break; }
			var nlist = parseEnumCase(p, desugar, list);
			if (nlist == list) { p.opt1('}'); break; }
			list = nlist;
		}
		return Lists.reverse(list);
	}
	def parseEnumCase(p: ParserState, desugar: VstEnumDesugaring, list: List<VstMember>) -> List<VstMember> {
		var id = parseIdentVoid(p);
		var args = if(p.curByte == '(', parseList(0, p, '(', COMMA, ')', parseExpr));
		p.opt1(',');
		if (id.name.image.length > 0) {
			list = List.new(desugar.synthesizeEnumCase(id), list);
		}
		return list;
	}
	def parseMembers(p: ParserState) -> List<VstMember> {
		p.req1('{');
		var list: List<VstMember>;
		while (true) {
			if (p.curByte == '}') { p.advance1(); break; }
			var nlist = parseMember(p, list);
			if (nlist == list) { p.opt1('}'); break; }
			list = nlist;
		}
		return Lists.reverse(list);
	}
	def parseMember(p: ParserState, prev: List<VstMember>) -> List<VstMember> {
		var isPrivate = optKeyword(p, "private") != null;
		match (p.curByte) {
			'd': if (optKeyword(p, "def") != null) {
				var parser = if(isPrivate, PRIVATE_DEF, PUBLIC_DEF);
				return parser.parseDef(p, prev);
			}
			'n': { 
				var start = optKeyword(p, "new");
				if (start != null) return List.new(parseNew(p, start), prev);
			}
			'v': if (optKeyword(p, "var") != null) {
				var parser = if(isPrivate, PRIVATE_VAR, PUBLIC_VAR);
				return parser.parseVar(p, prev);
			}
		}
		p.error("invalid start of member declaration");
		return null;
	}
	def parseNew(p: ParserState, start: FilePoint) -> VstNew {
		var params = parseList(0, p, '(', COMMA, ')', parseParam);
		var superclause: SuperClause;
		if (p.curByte == ':') {
			p.advance1();
			superclause = SuperClause.new(reqKeyword(p, "super"), parseTupleExpr(p));
		} else if (p.curByte == 's') {
			superclause = SuperClause.new(reqKeyword(p, "super"), parseTupleExpr(p));
		}
		var body = parseBlockStmt(p);
		var token = Token.new(p.fileName, "new", start.beginLine, start.beginColumn);
		return VstNew.new(token, params, superclause, body);
	}
	def parseParam(p: ParserState) -> ParamDecl {
		var id = parseIdentVoid(p).name;
		var tref = if(p.curByte == ':', (p.advance1(), parseTypeRef(p)).last);
		return ParamDecl.new(id, tref);
	}
	def parseOptionalParams(p: ParserState) -> VstList<ParamDecl> {
		return if(p.curByte == '(', Parser.parseList(0, p, '(', Parser.COMMA, ')', Parser.parseParamWithType));
	}
	def parseParamWithType(p: ParserState) -> ParamDecl {
		var id = parseIdentVoid(p).name;
		p.req1(':');
		return ParamDecl.new(id, parseTypeRef(p));
	}
	def parseTypeRef(p: ParserState) -> TypeRef {
		var tref: TypeRef;
		if (p.curByte == '(') {
			var list = parseList(0, p, '(', COMMA, ')', parseTypeRef);
			tref = TypeRef.new(null, null, list.list, Tuple.TYPECON, null);
		} else {
			var id = parseIdent(p, parseTypeRef);
			tref = TypeRef.new(null, id.name, id.list(), null, null);
			while (p.opt1('.') > 0) {
				var id = parseIdent(p, parseTypeRef);
				tref = TypeRef.new(tref, id.name, id.list(), null, null);
			}
		}
		var arrow = p.optN("->");
		while (arrow >= 0) { // type suffixes ( '->' Type )*
			var rtype = parseTypeRef(p);
			tref = TypeRef.new(null, null, Lists.cons2(tref, rtype), Function.DELEGATE, null);
			arrow = p.optN("->");
		}
		return tref;
	}
	def parseTypeParam(p: ParserState) -> TypeParam {
		var id = parseIdentVoid(p).name;
		return TypeUtil.newTypeParam(id, p.typeCache);
	}
	def parseStmt(p: ParserState) -> Stmt {
		match (p.curByte) {
			'{': return parseBlockStmt(p);
			';': return parseEmptyStmt(p);
			'i': return parseIfStmt(p);
			'w': return parseWhileStmt(p);
			'm': return parseMatchStmt(p);
			'v': {
				var start = optKeyword(p, "var");
				if (start != null) return parseVarStmt(p, start);
			}
			'd': {
				var start = optKeyword(p, "def");
				if (start != null) return parseDefStmt(p, start);
			}
			'b': return parseBreakStmt(p);
			'c': return parseContinueStmt(p);
			'r': return parseReturnStmt(p);
			'f': return parseForStmt(p);
		}
		return parseExprStmt(p);
	}
	def parseBlockStmt(p: ParserState) -> BlockStmt {
		return BlockStmt.new(parseList(0, p, '{', NONE, '}', parseStmt));
	}
	def parseEmptyStmt(p: ParserState) -> Stmt {
		var src = p.point(), x = p.advance1();
		return EmptyStmt.new(src);
	}
	def parseControlExpr(p: ParserState) -> Expr {
		return (p.req1('('), parseExpr(p), p.req1(')')).1;
	}
	def parseIfStmt(p: ParserState) -> Stmt {
		var start = optKeyword(p, "if");
		if (start != null) {
			var cond = parseControlExpr(p);
			var tblock = parseStmt(p);
			var fblock = if(optKeyword(p, "else") != null, parseStmt(p));
			return IfStmt.new(start, cond, tblock, fblock);
		}
		return parseExprStmt(p);
	}
	def parseWhileStmt(p: ParserState) -> Stmt {
		var start = optKeyword(p, "while");
		if (start != null) {
			var cond = parseControlExpr(p);
			var tblock = parseStmt(p);
			return WhileStmt.new(start, cond, tblock);
		}
		return parseExprStmt(p);
	}
	def parseForStmt(p: ParserState) -> Stmt {
		var start = optKeyword(p, "for");
		if (start != null) {
			p.req1('(');
			var decl = PRIVATE_VAR.parseLocal(p);
			if (p.curByte == '<') {
				var token = p.token(1);
				var limit = parseExpr(p);
				p.req1(')');
				var tblock = parseStmt(p);
				decl.isReadOnly = true;
				return ForLessStmt.new(start, decl, token, limit, tblock);
			} else if (optKeyword(p, "in") != null) {
				// for (vardecl in expr) { ... }
				var expr = parseExpr(p);
				p.req1(')');
				var tblock = parseStmt(p);
				decl.isReadOnly = true;
				return ForeachStmt.new(start, decl, expr, tblock);
			} else {
				// for (vardecl; cond; update) { ... }
				p.req1(';');
				var cond = parseExpr(p);
				p.req1(';');
				var update = parseExpr(p);
				p.req1(')');
				var tblock = parseStmt(p);
				return ForStmt.new(start, decl, cond, update, tblock);
			}
		}
		return parseExprStmt(p);
	}
	def parseMatchStmt(p: ParserState) -> Stmt {
		var start = optKeyword(p, "match");
		if (start != null) {
			var key = parseControlExpr(p);
			var list = parseList(0, p, '{', NONE, '}', parseMatchCase);
			var stmt = MatchStmt.new(start, key, list);
			var default = optKeyword(p, "else");
			if (default != null) stmt.defcase = MatchCase.new(default, null, parseStmt(p));
			return stmt;
		}
		return parseExprStmt(p);
	}
	def parseMatchCase(p: ParserState) -> MatchCase {
		if (p.curByte == '_') {
			if (Char.isIdentMiddle(p.peek(1))) {
				p.error("default match case expected");
				return null;
			}
			var default = p.token(1);
			p.req1(':');
			var stmt = parseStmt(p);
			return MatchCase.new(default, null, stmt);
		}
		var list = parseList(1, p, NONE, COMMA, ':', parseMatchPattern), stmt = parseStmt(p);
		return MatchCase.new(null, list, stmt);
	}
	def parseMatchPattern(p: ParserState) -> MatchPattern {
		var expr: Expr;
		var ch = p.curByte;
		match (ch) {
			'\'': expr = parseByteLiteral(p);
			// XXX: add string literals
		} else {
			if (Char.isIdentStart(ch)) {
				expr = parseVarExpr(p);
				while (p.curByte == '.') {
					var pos = p.point();
					p.advance1();
					if (Char.isIdentStart(p.curByte)) {
						expr = VarExpr.new(expr, pos, parseIdent(p, parseTypeRef));
					} else {
						p.error("identifier expected");
					}
				}
			} else if (ch == '-') {
				expr = parseNumber(p);
			} else if (Char.isDecimal(ch)) {
				expr = parseNumber(p);
			} else {
				p.error("match pattern expected");
			}
		}
		var params = if(p.curByte == '(', Parser.parseList(0, p, '(', Parser.COMMA, ')', parseMatchParam));
		return MatchPattern.new(expr, params);
	}
	def parseMatchParam(p: ParserState) -> VarDecl {
		var id = parseIdentVoid(p);
		var v = VarDecl.new(id.name, null, null);
		v.isReadOnly = true;
		return v;
	}
	def parseVarStmt(p: ParserState, start: FilePoint) -> Stmt {
		var id = parseIdentVoid(p);
		var vars = Lists.reverse(PUBLIC_VAR.parseVars(p, id));
		return LocalStmt.new(start, vars);
	}
	def parseDefStmt(p: ParserState, start: FilePoint) -> Stmt {
		var id = parseIdentVoid(p);
		var vars = Lists.reverse(PUBLIC_DEF.parseVars(p, id));
		return LocalStmt.new(start, vars);
	}
	def parseBreakStmt(p: ParserState) -> Stmt {
		var start = optKeyword(p, "break");
		if (start != null) return BreakStmt.new(term1(p, start, ';'));
		return parseExprStmt(p);
	}
	def parseContinueStmt(p: ParserState) -> Stmt {
		var start = optKeyword(p, "continue");
		if (start != null) return ContinueStmt.new(term1(p, start, ';'));
		return parseExprStmt(p);
	}
	def parseReturnStmt(p: ParserState) -> Stmt {
		var start = optKeyword(p, "return");
		if (start != null) {
			if (p.curByte == ';') return ReturnStmt.new(term1(p, start, ';'), null);
			var expr = parseExpr(p);
			return ReturnStmt.new(term1(p, start, ';'), expr);
		}
		return parseExprStmt(p);
	}
	def parseExprStmt(p: ParserState) -> Stmt {
		var expr = parseExpr(p);
		if (expr == null) return null;
		return ExprStmt.new(term1(p, expr.range(), ';'), expr);
	}
	def parseExpr(p: ParserState) -> Expr {
		var subExpr = parseSubExpr(p);
		if (p.curByte == '=' && p.peek(1) != '=') {
			var pos = p.token(1);
			var rhs = parseExpr(p);
			return AssignExpr.new(subExpr, null, pos, rhs);
		}
		return addBinOpSuffixes(p, subExpr);
	}
	def parseSubExpr(p: ParserState) -> Expr {
		return addTermSuffixes(p, parseTerm(p));
	}
	def parseTerm(p: ParserState) -> Expr {
		var ch = p.curByte;
		match (ch) {
			'\'': return parseByteLiteral(p);
			'\"': return parseStringLiteral(p);
			'(':  return parseTupleExpr(p);
			'[':  return parseArrayLiteral(p);
			'!':  return parseNotExpr(p);
			'_':  return parseParamExpr(p);
			'-':  {
				if (p.peek(1) == '-') return AutoExpr.new(p.token(2), parseSubExpr(p), V3Infix.Sub, false);
				return parseNumber(p);
			}
			'+':  {
				if (p.peek(1) == '+') return AutoExpr.new(p.token(2), parseSubExpr(p), V3Infix.Add, false);
			}
			'i': {
				if (ch == 'i') {
					var start = optKeyword(p, "if");
					if (start != null) {
						var args = parseList(1, p, '(', COMMA, ')', parseExpr);
						return IfExpr.new(start, args);
					}
				}
			}
		}
		if (Char.isIdentStart(ch)) return parseVarExpr(p);
		else if (Char.isDecimal(ch)) return parseNumber(p);
		p.error("invalid start of expression");
		return null;
	}
	def parseParamExpr(p: ParserState) -> Expr {
		// only "_" is allowed, not "__" or "_id"
		if (Char.isIdentMiddle(p.peek(1))) {
			p.error("identifier expected");
			return null;
		}
		return ParamExpr.new(p.token(1));
	}
	def parseNotExpr(p: ParserState) -> Expr {
		var point = p.point();
		var start = p.req1('!');
		return NotExpr.new(point, parseSubExpr(p));
	}
	def parseByteLiteral(p: ParserState) -> Literal {
		var parsed = Byte.parseByte(p.input, p.curPos);
		if (parsed.passed) {
			var token = p.token(parsed.length);
			return Literal.new(token, Byte.box(parsed.val), Byte.TYPE);
		}
		p.error("invalid character literal");
		return null;
	}
	def parseStringLiteral(p: ParserState) -> StringExpr {
		var parsed = Byte.parseString(p.input, p.curPos);
		if (parsed.passed) {
			var token = p.token(parsed.length);
			return StringExpr.new(token, parsed.val);
		}
		p.error("invalid string literal");
		return null;
	}
	def parseTupleExpr(p: ParserState) -> TupleExpr {
		return TupleExpr.new(parseList(0, p, '(', COMMA, ')', parseExpr));
	}
	def parseArrayLiteral(p: ParserState) -> ArrayExpr {
		return ArrayExpr.new(parseList(0, p, '[', COMMA, ']', parseExpr));
	}
	def parseNumber(p: ParserState) -> Literal {
		var np = NumberParser.new(p.input, p.curPos);
		if (np.parse()) {
			var token = p.token(np.pos - np.start);
			if (np.is_binary) {
				return BinLiteral.new(token, np.val, np.vtype, np.l_suffix, np.u_suffix, np.width);
			} else if (np.is_hex) {
				return HexLiteral.new(token, np.val, np.vtype, np.l_suffix, np.u_suffix, np.width);
			} else {
				return DecLiteral.new(token, np.val, np.vtype, np.l_suffix, np.u_suffix, np.is_negative);
			}
		} else {
			p.errorAtOffset(np.error_pos - np.start, np.error_msg);
			p.advance(np.pos - np.start);
			return null;
		}
	}
	def newLiteralFromIdent(p: ParserState, id: VstIdent<TypeRef>, val: Val, t: Type) -> Literal {
		if (id.params != null) p.error1("value \'%1\' cannot be parameterized", id.name.image);
		return Literal.new(id.name, val, t);
	}
	def parseVarExpr(p: ParserState) -> Expr {
		var id = parseIdentCommon(p, parseTypeRef);
		var binding: Binding;
		match (id.kwClass) {
			KC_TRUE:  return newLiteralFromIdent(p, id, Bool.TRUE, Bool.TYPE);
			KC_FALSE: return newLiteralFromIdent(p, id, Bool.FALSE, Bool.TYPE);
			KC_NULL:  return newLiteralFromIdent(p, id, Values.BOTTOM, Null.TYPE);
			KC_OTHER: kwError(p, id.name);
		}
		var ve = VarExpr.new(null, null, id);
		ve.binding = binding;
		return ve;
	}
	def addTermSuffixes(p: ParserState, expr: Expr) -> Expr {
		while (true) {
			match (p.curByte) {
				'.': { // expr.
					var pos = p.point();
					p.advance1();
					expr = addMemberSuffix(p, pos, expr);
				}
				'(': { // expr(
					expr = AppExpr.new(expr, parseTupleExpr(p));
				}
				'+': if (p.peek(1) == '+') { // expr++
					return AutoExpr.new(p.token(2), expr, V3Infix.Add, true);
				} else break;
				'-': if (p.peek(1) == '-') { // expr--
					return AutoExpr.new(p.token(2), expr, V3Infix.Sub, true);
				} else break;
			} else {
				break;
			}
		}
		return expr;
	}
	def addMemberSuffix(p: ParserState, pos: FilePoint, expr: Expr) -> Expr {
		var ch = p.curByte;
		if (Char.isIdentStart(ch)) {
			// expr.ident
			return VarExpr.new(expr, pos, parseIdentUnchecked(p, parseTypeRef));
		}
		if (Char.isDecimal(ch)) {
			// expr.decimal
			var end = p.plus(0, Char.isDecimal);
			var token = p.token(end - p.curPos);
			return VarExpr.new(expr, pos, VstIdent<TypeRef>.new(token, 0, null));
		}
		if (ch == '!' || ch == '?') {
			// expr.! or expr.?
			if (p.peek(1) != '=') {
				var hasParams = p.peek(1) == '<', t = p.token(1);
				var typeParams = if(hasParams, parseList(0, p, '<', COMMA, '>', parseTypeRef));
				return VarExpr.new(expr, pos, VstIdent.new(t, 0, typeParams));
			}
		}
		var op = parseInfix(p);
		if (op == null) {
			p.error("member expected");
			return expr;
		}
		return VarExpr.new(expr, pos, VstIdent<TypeRef>.new(op.token, 0, null));
	}
	def addBinOpSuffixes(p: ParserState, expr: Expr) -> Expr {
		var op = parseInfix(p);
		if (op == null) return expr;
		var exprStack = Stack<Expr>.new();
		var operStack = Stack<InfixOp>.new();
		exprStack.push(expr);
		while (op != null) {
			if (operStack.top != null && operStack.peek().prec >= op.prec) { // LR reduction
				reduce(p, exprStack, operStack, op.prec);
			}
			operStack.push(op);
			exprStack.push(parseSubExpr(p));
			op = parseInfix(p);
		}
		reduce(p, exprStack, operStack, -1);
		return exprStack.pop();
	}
	def reduce(p: ParserState, exprStack: Stack<Expr>, operStack: Stack<InfixOp>, prec: int) {
		while (!operStack.empty() && operStack.peek().prec >= prec) {
			var right = exprStack.pop(), left = exprStack.pop();
			var binop = operStack.pop();
			var expr = if (binop.assign, AssignExpr.new(left, binop, binop.token, right), BinOpExpr.new(left, binop, right));
			exprStack.push(expr);
		}
	}
	def parseList<T>(min: int, p: ParserState, begDelim: byte, sepByte: byte, endDelim: byte, f: ParserState -> T) -> VstList<T> {
		var start = p.point();
		if (p.curByte != begDelim) {
			if (begDelim != NONE) {
				p.error1("\'%1\' expected", begDelim);
				return VstList<T>.new(p.point().rangeOf(0), null);
			}
		} else {
			p.advance1();
		}
		if (min == 0 && p.curByte == endDelim) {
			// zero length list
			return VstList<T>.new(eatEnd(p, start), null);
		}
		var list: List<T>;
		var range: FileRange;
		while (true) {
			var lastPos = p.curPos;
			var item = f(p);
			if (p.curPos == lastPos) {
				// didn't parse anything
				p.opt1(endDelim);
				return VstList.new(p.end(start), Lists.reverse(list));
			}
			list = List<T>.new(item, list);
			if (p.curByte == endDelim) {
				// end of list
				range = eatEnd(p, start);
				break;
			}
			if (sepByte == NONE) continue;
			if (p.curByte == sepByte) {
				// separator character
				p.advance1();
			} else {
				var msg = Strings.format2("\'%1\' or \'%2\' expected", sepByte, endDelim);
				p.error(msg);
				range = p.end(start);
				break;
			}
		}
		return VstList.new(range, Lists.reverse(list));
	}
	def eatEnd(p: ParserState, start: FilePoint) -> FileRange {
		var endLine = p.curLine;
		var endColumn = p.curCol + 1;
		p.advance1();
		return FileRange.new(p.fileName, start.beginLine, start.beginColumn, endLine, endColumn);
	}
	def kwError(p: ParserState, id: Token) {
		p.errorAt(id.range(), Strings.format1("keyword \"%1\" cannot be used as identifier", id.image));
	}
	def parseIdentVoid(p: ParserState) -> VstIdent<void> {
		var id = parseIdentCommon<void>(p, null);
		if (id.kwClass != 0) kwError(p, id.name);
		return id;
	}
	def parseIdent<T>(p: ParserState, parse: ParserState -> T) -> VstIdent<T> {
		var id = parseIdentCommon(p, parse);
		if (id.kwClass != 0) kwError(p, id.name);
		return id;
	}
	def parseIdentUnchecked<T>(p: ParserState, parse: ParserState -> T) -> VstIdent<T> {
		return parseIdentCommon(p, parse);
	}
	// parse a parameterized identifier with the supplied parameter parsing function
	def parseIdentCommon<T>(p: ParserState, parse: ParserState -> T) -> VstIdent<T> {
		if (Char.isIdentStart(p.curByte)) {
			var d = p.input;
			for (q = p.curPos + 1; q < d.length; q++) {
				var c = d(q);
				if (c == '<') { // parse parameters, if allowed
					if (parse == null) {
						p.error("identifier cannot be parameterized here");
						return extractIdent(p, q);
					}
					var id = extractIdent<T>(p, q);
					var list = parseList(1, p, '<', COMMA, '>', parse);
					return VstIdent<T>.new(id.name, id.kwClass, list);
				}
				// if the character is part of the middle continue
				if (!Char.isIdentMiddle(c)) return extractIdent(p, q);
			}
			// end of input
			return extractIdent(p, d.length);
		}
		p.error("identifier expected");
		return extractIdent(p, p.curPos);
	}
	// extract an unparameterized identifier and advance the parser
	def extractIdent<T>(p: ParserState, end: int) -> VstIdent<T> {
		var token = p.token(end - p.curPos), kc = '\x00';
		var id = token.image;
		if (id.length >= 2) {
			var index = kwIndex(id), kw = kwKeys(index);
			if (kw != null && Strings.equal(kw, id)) kc = kwClass(index);
		}
		return VstIdent<T>.new(token, kc, null);
	}
	def boundOp(p: ParserState, infix: byte, op: Operator) -> InfixOp {
		var i = InfixOp.new(p.token(V3Infix.names(infix).length), infix, V3Infix.prec(infix), false);
		i.op = op;
		return i;
	}
	// parse an infix or member operator
	def parseInfix(p: ParserState) -> InfixOp {
		var op = '\x00';
		match (p.curByte) {
			'=' : op = if(p.peek(1) == '=', V3Infix.Equal);
			'!' : op = if(p.peek(1) == '=', V3Infix.NotEqual);
			'|' : {
				if (p.peek(1) == '|') return boundOp(p, V3Infix.OrOr, V3Op.opBoolOr);
				else op = V3Infix.Or;
			}
			'&' : {
				if (p.peek(1) == '&') return boundOp(p, V3Infix.AndAnd, V3Op.opBoolAnd);
				else op = V3Infix.And;
			}
			'<' : {
				var nch = p.peek(1);
				if (nch == '=') op = V3Infix.Lteq;
				else if (nch == '<') op = V3Infix.QShl;
				else op = V3Infix.Lt;
			}
			'>' : {
				var nch = p.peek(1);
				if (nch == '=') op = V3Infix.Gteq;
				else if (nch == '>') {
					var nnch = p.peek(2);
					op = if (nnch == '>', V3Infix.QShr, V3Infix.QSar);
				}
				else op = V3Infix.Gt;
			}
			'+' : op = V3Infix.Add;
			'-' : op = V3Infix.Sub;
			'*' : op = V3Infix.Mul;
			'/' : op = V3Infix.Div;
			'%' : op = V3Infix.Mod;
			'^' : op = V3Infix.Xor;
			'#' : {
				var d = p.input, q = p.curPos;
				if (d.length - q > 2) {
					if (d(q + 1) == '>' && d(q + 2) == '>') op = V3Infix.Shr;
					if (d(q + 1) == '<' && d(q + 2) == '<') op = V3Infix.Shl;
				}
			}
		}
		if (op == '\x00') return null;
		var len = V3Infix.names(op).length;
		var assign = false;
		var prec = V3Infix.prec(op);
		if (p.peek(len) == '=') {
			len++;
			assign = true;
			prec = 0;
			if (!V3Infix.isLegalCompound(op)) p.error("illegal compound assignment");
		}
		return InfixOp.new(p.token(len), op, prec, assign);
	}
	def reqKeyword(p: ParserState, kw: string) -> FilePoint {
		var start = optKeyword(p, kw);
		if (start == null) p.error1("\"%1\" expected", kw);
		return start;
	}
	def optKeyword(p: ParserState, kw: string) -> FilePoint {
		if (p.curByte != kw(0)) return null;
		var i = p.curPos, d = p.input, max = i + kw.length;
		if (max > d.length) return null;
		for (j < kw.length) {
			if (d(i + j) != kw(j)) return null;
		}
		if (max < d.length && Char.isIdentMiddle(d(max))) return null;
		var pt = FilePoint.new(p.fileName, p.curLine, p.curCol);
		p.advance(kw.length);
		return pt;
	}
	def term1(p: ParserState, start: FilePoint, ch: byte) -> FileRange {
		var endLine = p.curLine;
		var endColumn = p.curCol;
		if (p.curByte == ch) {
			p.advance1();
			endColumn++;
		} else {
			p.error1("\"%1\" expected", ch);
		}
		return FileRange.new(p.fileName, start.beginLine, start.beginColumn, endLine, endColumn);
	}
	def skipToNextToken(p: ParserState) {
		// skip forward over whitespace and comments
		var curPos = p.curPos, curLine = p.curLine, curCol = p.curCol;
		var input = p.input, max = input.length;
		while (curPos < max) {
			var ch = input(curPos);
			match (ch) {
				'\t': { curCol = 1 + (((curCol + 8) #>> 3) #<< 3); }
				' ' : { curCol++; }
				'\n': {
					p.lineEnds.add(curPos);
					curCol = 1;
					curLine++;
				}
				'/' : {
					if (curPos >= max - 1 || input(curPos + 1) != '/') break;
					// skip line comment
					curPos = curPos + 2;
					while (curPos < max) {
						if (input(curPos) == '\n') {
							p.lineEnds.add(curPos);
							curCol = 1;
							curLine++;
							break;
						}
						curPos++;
					}
				} 
			} else {
				if (ch < ' ' || ch > '~') {
					// illegal character!
					p.setPos(curPos, curLine, curCol);
					p.error1("invalid character valued %1", int.!(ch));
					curCol++;
				} else {
					// non-whitespace character, break
					break;
				}
			}
			curPos++;
		}
		// reached the end or a non-whitespace character
		p.setPos(curPos, curLine, curCol);
	}
	def skipNone(p: ParserState) {
		// don't skip any characters
	}
}
// a small closure for parsing var/def declarations
class VarDefParser(isPrivate: bool, isReadOnly: bool) {
	def parseLocal(p: ParserState) -> VarDecl {
		var id = Parser.parseIdentVoid(p).name;
		var tref = if(p.curByte == ':', Parser.parseTypeRef(p.advance1()));
		var init = if(p.curByte == '=', Parser.parseExpr(p.advance1()));
		var decl = VarDecl.new(id, tref, init);
		decl.isReadOnly = isReadOnly;
		return decl;
	}
	def parseVars(p: ParserState, id: VstIdent<void>) -> List<VarDecl> {
		var list: List<VarDecl>;
		while (true) {
			var lastPos = p.curPos;
			var tref = if(p.curByte == ':', Parser.parseTypeRef(p.advance1()));
			var init = if(p.curByte == '=', Parser.parseExpr(p.advance1()));
			var decl = VarDecl.new(id.name, tref, init);
			decl.isReadOnly = isReadOnly;
			list = List.new(decl, list);
			if (p.curByte == ',') {
				p.advance1();
				id = Parser.parseIdentVoid(p);
			} else if (p.curByte == ';') {
				p.advance1();
				break;
			} else {
				p.error("\',\' or \';\' expected");
				break;
			}
		}
		return list;
	}
	def parseDef(p: ParserState, prev: List<VstMember>) -> List<VstMember> {
		var id = Parser.parseIdent(p, Parser.parseTypeParam);
		if (p.curByte == '(' || id.params != null) return parseMethodSuffix(p, id, prev);
		var n = VstIdent<void>.new(id.name, id.kwClass, null);
		return parseFieldSuffix(p, n, prev);
	}
	def parseVar(p: ParserState, prev: List<VstMember>) -> List<VstMember> {
		var id = Parser.parseIdentVoid(p);
		return parseFieldSuffix(p, id, prev);
	}
	private def parseFieldSuffix(p: ParserState, id: VstIdent<void>, prev: List<VstMember>) -> List<VstMember> {
		var vars = parseVars(p, id);
		for (l = Lists.reverse(vars); l != null; l = l.tail) {
			var v = l.head;
			prev = List.new(VstField.new(isPrivate, isReadOnly, v.token, v.tref, v.init), prev);
		}
		return prev;
	}
	private def parseMethodSuffix(p: ParserState, id: VstIdent<TypeParam>, prev: List<VstMember>) -> List<VstMember> {
		var params = Parser.parseList(0, p, '(', Parser.COMMA, ')', Parser.parseParamWithType);
		var rtype = if(p.optN("->") >= 0, Parser.parseTypeRef(p), TypeRef.new(null, null, null, null, Void.TYPE));
		var body = if(p.curByte == ';', EmptyStmt.new(p.token(1)), Parser.parseBlockStmt(p));
		var m = VstMethod.new(isPrivate, id.name, id.list(), params, rtype, body);
		return List.new(m, prev);
	}
}
