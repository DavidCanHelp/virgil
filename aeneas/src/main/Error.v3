// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// an error, such as a user error, a parsing or typechecking error, etc
class Error {
	def range: FileRange;
	def sourceLine: string;
	def error: string;
	def description: string;
	new(range, sourceLine, error, description) { }
	// convert this error to a string
	def toString() -> string {
		var buffer = StringBuffer.new();
		if (range != null) {
			if (range.beginLine == 0) buffer.format1("[%1]", range.fileName);
			else buffer.format3("[%1 @ %2:%3] ", range.fileName, range.beginLine, range.beginColumn);
		}
		if (error != null) buffer.format2("%1: %2", error, description);
		else buffer.puts(description);
		return buffer.toString();
	}
	// report this error to the console, including color highlighting
	def report() {
		if (range != null) {
			Terminal.putc('[');
			Terminal.blue(range.fileName, 0);
			if (range.beginLine > 0) {
				Terminal.put(" @ ");
				Terminal.put(Terminal.CTRL_CYAN);
				Terminal.put2("%1:%2", range.beginLine, range.beginColumn);
				Terminal.put(Terminal.CTRL_DEFAULT);
			}
			Terminal.put("] ");
		}
		if (error == null) Terminal.red("Error", 0);
		else Terminal.red(error, 0);
		Terminal.put(": ");
		Terminal.put(description);
		Terminal.ln();
		if (sourceLine != null && range != null) {
			Terminal.put(sourceLine);
			Terminal.ln();
			var i = 1;
			while (i++ < range.beginColumn) Terminal.putc(' ');
			Terminal.putc('^');
			if (range.endLine == range.beginLine) {
				while (i++ < range.endColumn) Terminal.putc('^');
			}
			Terminal.ln();
		}
	}
}

class ErrorGen {
	var errors: List<Error>;
	var numErrors: int;
	var maxErrors: int;
	var notTooMany: bool = true;
	var noErrors: bool = true;
	var errorType: Type;
	var unresolvedTypeCache = Strings.newMap<TypeCon>();
	var typeCache: TypeCache;

	new(maxErrors: int) {
		this.maxErrors = maxErrors;
	}
	def report() {
		Lists.apply(Lists.reverse(errors), Error.report);
		errors = null;
	}
	def firstError() -> string {
		if (errors != null) return Lists.reverse(errors).head.toString();
		return "";
	}
	def getErrorTypeCon(name: string) -> TypeCon {
		var typeCon = unresolvedTypeCache.get(name);
		if (typeCon == null) {
			if (typeCache == null) typeCache = TypeCache.new();
			typeCon = TypeCon.new(name, V3Kind.VOID, -1, typeCache);
			unresolvedTypeCache.set(name, typeCon);
		}
		return typeCon;
	}
	def getErrorType() -> Type {
		if (errorType != null) return errorType;
		return errorType = getErrorTypeCon("?").create0();
	}
	def assert1<T>(b: bool, msg: string, t: T) {
		if (!b) fail(Strings.format1(msg, t));
	}
	def assert(b: bool, msg: string) {
		if (!b) fail(msg);
	}
	def fail(msg: string) {
		if (Aeneas.FATAL.get()) return V3.fail(msg);
		addError(null, null, V3Exception.InternalError, msg);
	}
	def OutputError(fileName: string) {
		addError(null, null, "Cannot create output file", fileName);
	}
	def ExceptionInInitialization(comp: VstComponent, except: Exception) {
		var msg = V3.renderResult(except, StringBuffer.new()).toString();
		addError(comp.token.range(), null, "ExceptionInInitialization", msg);
	}
	def FileNotFound(fname: string) {
		addError(null, null, "File not found", fname);
	}
	def MainNotFound() {
		addError(null, null, null, "Main not found");
	}
	def addError(range: FileRange, sourceLine: string, error: string, msg: string) {
		errors = List.new(Error.new(range, sourceLine, error, msg), errors);
		incErrors();
	}
	def unexpectedType<T>(t: Type, v: T) -> T {
		var msg = Strings.format1("unexpected type %1", t.render);
		if (Aeneas.FATAL.get()) return V3.fail(msg);
		addError(null, null, null, msg);
		return v;
	}
	def incErrors() {
		numErrors++;
		noErrors = false;
		if (numErrors >= maxErrors) notTooMany = false;
	}
}
