// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Utility methods for working with ints, including parsing and rendering,
// as well as the representation of the "int" type in the compiler
component Int {
	def MAX_WIDTH = 64;
	private def cache = Array<Int_TypeCon>.new(2 * MAX_WIDTH + 1);
	def TYPECON   = getTypeCon(true, 32);
	def TYPE      = TYPECON.create0();
	def TYPE_LIST = List.new(TYPE, null);
	def ONE       = Box.new(1);
	def TWO       = Box.new(2);
	def FOUR      = Box.new(4);
	def MINUS_1   = Box.new(-1);
	def MAX_VALUE = 2147483647;
	def MAX_VALUE_DIV_10 = 214748364;
	def MAX_VALUE_MOD_10 = 7;
	def MIN_VALUE = -2147483648;
	def MIN_VALUE_DIV_10 = -214748364;
	def MIN_VALUE_MOD_10 = 8;

	def getType(signed: bool, width: int) -> Type {
		return getTypeCon(signed, width).create0();
	}
	def getTypeCon(signed: bool, width: int) -> Int_TypeCon {
		if (width <= 0 || width > MAX_WIDTH) return null;
		var index = if (signed, width, width + MAX_WIDTH);
		var typeCon = cache(index);
		if (typeCon == null) typeCon = cache(index) = Int_TypeCon.new(byte.!(width), signed);
		return typeCon;
	}
	def lookupFsi(name: string, signed: bool) -> Int_TypeCon {
		if (name.length < 2 || name.length > 3) return null;
		var width = name(1) - int.!('0');
		if (width < 1 || width > 9) return null;
		if (name.length == 3) {
			var low = name(2) - int.!('0');
			if (low < 0 || low > 9) return null;
			width = width * 10 + low;
		}
		return getTypeCon(signed, width);
	}
	def unbox(val: Val) -> int {
		if (val == null) return 0;
		return Box<int>.!(val).val;
	}
	def box(val: int) -> Box<int> {
		return Box.new(val);
	}
	def newMap<V>() -> HashMap<int, V> {
		return HashMap<int, V>.new(int.!<int>, int.==);
	}
	def parseDecimal(a: Array<byte>) -> Box<int> {
		if (a.length == 0) return null;
		var ch = a(0), r: Parsed<int>;
		if (ch == '-') r = parseNegDecimal(a, 0);
		else if (ch < '0' || ch > '9') return null;
		else r = parsePosDecimal(a, 0);
		if (r.passed && r.length == a.length) return box(r.val);
		return null;
	}
	// note: assumes pos < a.length
	def parsePosDecimal(a: Array<byte>, pos: int) -> Parsed<int> {
		var v0 = a(pos), passed = true;
		if (v0 == '0') {
			var p1 = pos + 1;
			if (p1 == a.length || a(p1) - '0' > 9) return Parsed.new(true, 1, 0);
			passed = false;
		} else if (v0 < '0' || v0 > '9') {
			return error(0);
		}
		var max = a.length, accum = v0 - int.!('0'), i = pos + 1;
		while (i < max) {
			var d: byte = a(i) - '0'; // XXX: use u32 to avoid zero extension
			if (d > 9) break;
			if (accum >= MAX_VALUE_DIV_10) {  // check for overflow
				if (accum > MAX_VALUE_DIV_10) passed = false;
				else if (d > MAX_VALUE_MOD_10) passed = false;
			}
			accum = accum * 10 + d;
			i++;
		}
		return Parsed.new(passed, i - pos, accum);
	}
	// note: assumes a(pos) == '-'
	def parseNegDecimal(a: Array<byte>, pos: int) -> Parsed<int> {
		var p1 = pos + 1, passed = true;
		if (p1 >= a.length) return error(1);
		var v1 = a(p1);
		if (v1 == '0') passed = false;
		else if (v1 < '1' || v1 > '9') return error(1);

		var max = a.length, accum = int.!('0') - v1, i = pos + 2;
		while (i < max) {
			var d: byte = a(i) - '0'; // XXX: use u32 to avoid zero extension
			if (d > 9) break;
			if (accum <= MIN_VALUE_DIV_10) {  // check for overflow
				if (accum < MIN_VALUE_DIV_10) passed = false;
				else if (d > MIN_VALUE_MOD_10) passed = false;
			}
			accum = accum * 10 - d;
			i++;
		}
		return Parsed.new(passed, i - pos, accum);
	}
	// note: assumes a(0) == '0' && (a(1) == 'b' || a(1) == 'B')
	def parseBin(a: Array<byte>, pos: int) -> Parsed<int> {
		var i = pos + 2, max = a.length;
		if (i >= max) return error(2);
		var accum = 0;
		while (i < max) {
			var ch = a(i);
			if (ch == '1') accum = accum + accum + 1;
			else if (ch == '0') accum = accum + accum;
			else return bin(i - pos, accum);
			i++;
		}
		return bin(i - pos, accum);
	}
	// note: assumes a(0) == '0' && (a(1) == 'x' || a(1) == 'X')
	def parseHex(a: Array<byte>, pos: int) -> Parsed<int> {
		var i = pos + 2, max = a.length;
		if (i >= max) return error(2);
		var accum = 0;
		while (i < max) {
			var ch = a(i);
			if (!Char.isHex(ch)) return hex(i - pos, accum);
			accum = (accum * 16) | Char.hexValue(ch);
			i++;
		}
		return hex(i - pos, accum);
	}
	private def bin(len: int, val: int) -> Parsed<int> {
		return Parsed.new(len >= 3 && len <= 34, len, val);
	}
	private def hex(len: int, val: int) -> Parsed<int> {
		return Parsed.new(len >= 3 && len <= 10, len, val);
	}
	private def error(len: int) -> Parsed<int> {
		return Parsed.new(false, len, 0);
	}
	def min(a: int, b: int) -> int {
		return if(a < b, a, b);
	}
}
// convert a width and a signed-ness into the name of the integer type
def intName(width: byte, signed: bool) -> string {
	if (width == 16 && signed) return "short"; // XXX ?
	if (width == 32 && signed) return "int";
	if (width == 64 && signed) return "long";
	if (width == 8 && !signed) return "byte";
	if (width < 10) return [if(signed, 'i', 'u'), byte.!('0' + width)];
	return [if(signed, 'i', 'u'), byte.!('0' + (width / 10)), byte.!('0' + (width % 10))];
}
// Type constructor for integral types, including a width and a signed-ness attribute.
class Int_TypeCon extends Member_TypeCon {
	def width: byte;  // width in bits (1-64)
	def signed: bool; // true if signed, false if unsigned
	var cache: Array<OperatorOf<V3OpSyntax>>; // cache of operators
	var binopTypes: Array<Type>;
	var max: Val;
	var min: Val;

	new(width, signed) super(intName(width, signed), V3Kind.INT, 0, TypeUtil.globalCache) {
		if (width <= 32) {
			min = if(signed, box(-1 #<< (width - 1)));
			max = box(-1 ^ (0xFFFFFFFF #<< (width - if(signed, 1))));
		} else {
			min = if(signed, Int64.box(Int64.shlint((-1, -1), (width - 1))));
			max = Int64.box(Int64.xor((-1, -1), Int64.shlint((-1, -1), (width - if(signed, 1)))));
		}
		create0();
	}
	def box(v: int) -> Val {
		return Box.new(v);
	}
	def represent(v: Val, t: Int_TypeCon) -> (bool, Val) {
		if (v == null) return (true, null);
		if (width <= 32) {
			if (!Box<int>.?(v)) return (false, null);
			var i = Int.unbox(v);
			if (width < 32) return (i >= Int.unbox(min) && i <= Int.unbox(max), v);
			return (signed == t.signed || i >= 0, v);
		}
		if (width <= 64) {
			def u = Int64.unboxVal, l = u(v, t.signed);
			if (width < 64) return (Int64.gteq(l, u(min, signed)) && Int64.lteq(l, u(max, signed)), v);
			return (signed == t.signed || l.0 >= 0, Int64.box(l));
		}
		return (false, null);
	}
	def byteSize() -> int {
		if (width <= 8) return 1;
		if (width <= 16) return 2;
		if (width <= 32) return 4;
		return 8;
	}
	def lookupMember(t: Type, name: string, receiver: bool) -> Member {
		if (receiver) return null;
		if (Strings.equal(name, "min")) return Members.valMember(singleton, min, false);
		if (Strings.equal(name, "max")) return Members.valMember(singleton, max, false);
		return null;
	}
	def lookupInfix(infix: byte) -> OperatorOf<V3OpSyntax> {
		if (cache == null) cache = Array.new(V3Infix.NumOps);
		var op = cache(infix);
		if (op != null) return op;
		var opcode: byte, resultType = singleton;
		match (infix) {
			V3Infix.Add:  { opcode = V3Opcode.IntAdd; }
			V3Infix.Sub:  { opcode = V3Opcode.IntSub; }
			V3Infix.Mul:  { opcode = V3Opcode.IntMul; }
			V3Infix.Div:  { opcode = V3Opcode.IntDiv; }
			V3Infix.Mod:  { opcode = V3Opcode.IntMod; }
			V3Infix.And:  { opcode = V3Opcode.IntAnd; }
			V3Infix.Or:   { opcode = V3Opcode.IntOr;  }
			V3Infix.Xor:  { opcode = V3Opcode.IntXor; }
			V3Infix.Shl:  { opcode = V3Opcode.IntShl; }
			V3Infix.Shr:  { opcode = V3Opcode.IntShr; }
			V3Infix.Lt:   { opcode = V3Opcode.IntLt;   resultType = Bool.TYPE; }
			V3Infix.Gt:   { opcode = V3Opcode.IntGt;   resultType = Bool.TYPE; }
			V3Infix.Lteq: { opcode = V3Opcode.IntLteq; resultType = Bool.TYPE; }
			V3Infix.Gteq: { opcode = V3Opcode.IntGteq; resultType = Bool.TYPE; }
		}
		if (opcode == '\x00') return null; // not found
		if (binopTypes == null) binopTypes = [singleton, singleton];
		if (width <= 32) {
			var syntax = V3OpSyntax.new(opcode, V3Infix.names(infix), opSsa);
			var eval = if(signed, eval_i32, eval_u32);
			return cache(infix) = OperatorOf.new(opcode, TypeUtil.NO_TYPES, binopTypes, resultType, syntax, eval);
		} else {
			var syntax = V3OpSyntax.new(opcode, V3Infix.names(infix), opSsa64);
			var eval = if(signed, eval_i64, eval_u64);
			return cache(infix) = OperatorOf.new(opcode, TypeUtil.NO_TYPES, binopTypes, resultType, syntax, eval);
		}
	}
	def isU32() -> bool {
		return width == 32 && !signed;
	}
	def isU64() -> bool {
		return width == 64 && !signed;
	}
	// return inverse of an operator {op}, if it exists, {null} otherwise
	def inverse(op: Operator) -> Operator {
		match (op.opcode) {
			V3Opcode.IntLt:   return lookupInfix(V3Infix.Gteq);
			V3Opcode.IntGt:   return lookupInfix(V3Infix.Lteq);
			V3Opcode.IntLteq: return lookupInfix(V3Infix.Gt);
			V3Opcode.IntGteq: return lookupInfix(V3Infix.Lt);
		}
		return null;
	}
	// return a commuted operator {op'} where {x op y == y op' x},
	// if it exists, {null} otherwise
	def commute(op: Operator) -> Operator {
		match (op.opcode) {
			V3Opcode.IntAdd,
			V3Opcode.IntMul,
			V3Opcode.IntAnd,
			V3Opcode.IntOr,
			V3Opcode.IntXor: return op;
			V3Opcode.IntLt:   return lookupInfix(V3Infix.Gt);
			V3Opcode.IntGt:   return lookupInfix(V3Infix.Lt);
			V3Opcode.IntLteq: return lookupInfix(V3Infix.Gteq);
			V3Opcode.IntGteq: return lookupInfix(V3Infix.Lteq);
		}
		return null;
	}
	def opSsa64(b: V3SsaBuilder, op: Operator, x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return b.add(op, [x, y], 0);
	}
	def opSsa(b: V3SsaBuilder, op: Operator, x: SsaInstr, y: SsaInstr) -> SsaInstr {
		if (SsaValue.?(x)) {
			// try folding x op y
			if (SsaValue.?(y)) return b.fold2(op, SsaValue.!(x).val, SsaValue.!(y).val);
			var commuted = commute(op);
			if (commuted != null) return opSsaN(b, commuted, y, x); // put constants on right
		}
		return opSsaN(b, op, x, y);
	}
	def opSsaN(b: V3SsaBuilder, op: Operator, x: SsaInstr, y: SsaInstr) -> SsaInstr {
		var facts = if(signed, 0, Facts.V_NON_NEGATIVE);
		if (SsaValue.?(y) && width <= 32) {  // TODO: strength reduce 64 bit ops
			var yv = y.unbox<int>();
			// strength reduce x op K
			match (op.opcode) {
				V3Opcode.IntAdd: if (yv == 0) return x; // x + 0 == x
				V3Opcode.IntSub: if (yv == 0) return x; // x - 0 == x
				V3Opcode.IntMul: {
					if (yv == 0) return y; // x * 0 == 0
					if (yv == 1) return x; // x * 1 == 1
					if (yv == 2) return opSsaN(b, lookupInfix(V3Infix.Add), x, x);
					if ((yv & (yv - 1)) == 0) {
						// x * 2^k == x << k
						return opSsaN(b, lookupInfix(V3Infix.Shl), x, b.graph.intConst(IntOp.log(yv)));
					}
				}
				V3Opcode.IntDiv: {
					if (yv == 0) return b.addThrow(b.source, V3Exception.DivideByZero); // fold x / 0
					if (yv == 1) return x; // x / 1 == x
					if (yv == -1) return opSsaN(b, lookupInfix(V3Infix.Sub), b.graph.zeroConst(), x); // x / -1 == 0 - x
					if ((yv & (yv - 1)) == 0 && x.checkFact(Facts.V_NON_NEGATIVE)) {
						// x / 2^k == x >> k
						return opSsaN(b, lookupInfix(V3Infix.Shr), x, b.graph.intConst(IntOp.log(yv)));
					}
					facts = facts | Facts.O_SAFE_DIVIDE;
				}
				V3Opcode.IntMod: {
					if (yv == 0) return b.addThrow(b.source, V3Exception.DivideByZero); // fold x % 0
					if (yv == 1 || yv == -1) return b.graph.zeroConst(); // x % +/-1 == 0
					if ((yv & (yv - 1)) == 0 && x.checkFact(Facts.V_NON_NEGATIVE)) {
						// x % 2^k == x & (2^k - 1)
						return opSsaN(b, lookupInfix(V3Infix.And), x, b.graph.intConst(yv - 1));
					}
					facts = facts | Facts.O_SAFE_DIVIDE;
				}
				V3Opcode.IntAnd: {
					if (yv == 0) return y; // x & 0 == 0
					if (yv == -1) return x; // x & -1 == x
					if (yv >= 0) facts = facts | Facts.V_NON_NEGATIVE;
				}
				V3Opcode.IntOr: {
					if (yv == 0) return x; // x | 0 == x
					if (yv == -1) return y; // x | -1 == -1
					if (yv < 0) facts = facts | Facts.V_BELOW_ZERO;
				}
				V3Opcode.IntXor: {
					if (yv == 0) return y; // x ^ 0 == x
					if (yv >= 0) facts = facts | Facts.V_NON_NEGATIVE;
				}
				V3Opcode.IntShl, V3Opcode.IntShr: {
					if (yv == 0) return x;
					if (yv < 0 || yv >= width) return b.graph.zeroConst();
					facts = facts | Facts.O_NO_SHIFT_CHECK;
				}
			}
		}
		// propagate facts and perform other strength reductions for x op y.
		match (op.opcode) {
			V3Opcode.IntSub: if (x == y) return b.graph.zeroConst();
			V3Opcode.IntDiv: {
				if (x == y && x.checkFact(Facts.V_NON_ZERO)) return b.graph.intConst(1);
				if (y.checkFact(Facts.V_NON_ZERO)) facts = facts | Facts.O_SAFE_DIVIDE;
				facts = facts | (x.facts & y.facts & Facts.V_NON_NEGATIVE); // nonneg(x) /% nonneg(y) => nonneg
			}
			V3Opcode.IntMod: {
				if (y.checkFact(Facts.V_NON_ZERO)) facts = facts | Facts.O_SAFE_DIVIDE;
			}
			V3Opcode.IntAnd: {
				if (x == y) return x;
				facts = facts | (x.facts | y.facts) & Facts.V_NON_NEGATIVE;
			}
			V3Opcode.IntOr: {
				if (x == y) return x;
				facts = facts | (x.facts | y.facts) & Facts.V_NON_ZERO;
				facts = facts | (x.facts & y.facts) & Facts.V_NON_NEGATIVE;
			}
			V3Opcode.IntXor: if (x == y) return b.graph.zeroConst();
			V3Opcode.IntShr: {
				facts = facts | (x.facts & Facts.V_NON_NEGATIVE);
			}
		}
		return b.add(op, [x, y], facts);
	}
	def eval_i32(v: V3OpSyntax, a: Arguments) -> Result {
		var x = unbox_i32(a.getArg(0)), y = unbox_i32(a.getArg(1));
		var r: int;
		match (v.opcode) {
			V3Opcode.IntAdd:  r = (x + y);
			V3Opcode.IntSub:  r = (x - y);
			V3Opcode.IntMul:  r = (x * y);
			V3Opcode.IntDiv:  { if (y == 0) return a.throw(V3Exception.DivideByZero, null); r = (x / y); }
			V3Opcode.IntMod:  { if (y == 0) return a.throw(V3Exception.DivideByZero, null); r = (x % y); }
			V3Opcode.IntAnd:  r = (x & y);
			V3Opcode.IntOr:   r = (x | y);
			V3Opcode.IntXor:  r = (x ^ y);
			V3Opcode.IntShl:  r = (x #<< y);
			V3Opcode.IntShr:  r = (x #>> y);
			V3Opcode.IntLt:   return Bool.box(x < y);
			V3Opcode.IntGt:   return Bool.box(x > y);
			V3Opcode.IntLteq: return Bool.box(x <= y);
			V3Opcode.IntGteq: return Bool.box(x >= y);
		}
		return box_i32(r);
	}
	def eval_u32(v: V3OpSyntax, a: Arguments) -> Result {
		var x = unbox_u32(a.getArg(0)), y = unbox_u32(a.getArg(1));
		var r: u32;
		match (v.opcode) {
			V3Opcode.IntAdd:  r = (x + y);
			V3Opcode.IntSub:  r = (x - y);
			V3Opcode.IntMul:  r = (x * y);
			V3Opcode.IntDiv:  { if(y == u32.!(0)) return a.throw(V3Exception.DivideByZero, null); r = (x / y); }
			V3Opcode.IntMod:  { if(y == u32.!(0)) return a.throw(V3Exception.DivideByZero, null); r = (x % y); }
			V3Opcode.IntAnd:  r = (x & y);
			V3Opcode.IntOr:   r = (x | y);
			V3Opcode.IntXor:  r = (x ^ y);
			V3Opcode.IntShl:  r = (x #<< y);
			V3Opcode.IntShr:  r = (x #>> y);
			V3Opcode.IntLt:   return Bool.box(x < y);
			V3Opcode.IntGt:   return Bool.box(x > y);
			V3Opcode.IntLteq: return Bool.box(x <= y);
			V3Opcode.IntGteq: return Bool.box(x >= y);
		}
		return box_u32(r);
	}
	def eval_i64(v: V3OpSyntax, a: Arguments) -> Result {
		var x = unbox_i64(a.getArg(0)), y = unbox_i64(a.getArg(1));
		var r: (int, int);
		match (v.opcode) {
			V3Opcode.IntAdd:  r = Int64.add(x, y);
			V3Opcode.IntSub:  r = Int64.sub(x, y);
			V3Opcode.IntMul,
			V3Opcode.IntDiv,
			V3Opcode.IntMod:  return a.throw(V3Exception.Unimplemented, null);
			V3Opcode.IntAnd:  r = Int64.and(x, y);
			V3Opcode.IntOr:   r = Int64.or(x, y);
			V3Opcode.IntXor:  r = Int64.xor(x, y);
			V3Opcode.IntShl:  r = Int64.shl(x, y);
			V3Opcode.IntShr:  r = Int64.shr(x, y);
			V3Opcode.IntLt,
			V3Opcode.IntGt:   return a.throw(V3Exception.Unimplemented, null);
			V3Opcode.IntLteq: return Bool.box(Int64.lteq(x, y));
			V3Opcode.IntGteq: return Bool.box(Int64.gteq(x, y));
		}
		return box_i64(r);
	}
	def eval_u64(v: V3OpSyntax, a: Arguments) -> Result {
		return a.throw(V3Exception.Unimplemented, null);
	}
	def unbox_u32(v: Val) -> u32 {
		if (v == null) return u32.!(0);
		return u32.!(Box<int>.!(v).val);
	}
	def unbox_i32(v: Val) -> i32 {
		if (v == null) return 0;
		return Box<int>.!(v).val;
	}
	def unbox_i64(v: Val) -> (int, int) {
		if (v == null) return (0, 0);
		return Box<(int, int)>.!(v).val;
	}
	def box_u32(u: u32) -> Val {
		return box_i32(int.!(u));
	}
	def box_i32(v: int) -> Val {
		if (v == 0) return null;
		v = if(width < 32, trunc_32(v), v);
		return if(v != 0, Box.new(v));
	}
	def box_i64(v: (int, int)) -> Val {
		if (v == (0, 0)) return null;
		v = if(width < 64, trunc_64(v), v);
		return if(v != (0, 0), Int64.box(v));
	}
	def trunc_32(v: int) -> int {
		var x = 0xFFFFFFFF #<< width;
		v = v ^ (v & x); // mask off higher bits
		if (signed && (v & (x #>> 1)) != 0) v = v | x; // sign extend
		return v;
	}
	def trunc_64(l: (int, int)) -> (int, int) {
		var v = l.0;
		var x = 0xFFFFFFFF #<< (width - 32);
		v = v ^ (v & x); // mask off higher bits
		if (signed && (v & (x #>> 1)) != 0) v = v | x; // sign extend
		return (v, l.1);
	}
}
// All functionality relating to integer operations.
component IntOp {
	// computes the log of a power of two integer {v}
	def log(v: int) -> int {
		// XXX: faster integer logarithm
		for (i < 32) {
			if ((1 #<< i) == v) return i;
		}
		return 32;
	}
	def get(op: Operator, infix: byte) -> Operator {
		return V3.asInt(op.paramTypes(0)).lookupInfix(infix);
	}
	def evalConversion(t1: Type, t2: Type, val: Val) -> TypeCast {
		if (t1 == t2) return TypeSystem.TRUE;
		var i1 = V3.asInt(t1), i2 = V3.asInt(t2);
		if (i1.width < i2.width) {
			if (i1.width <= 32 && i2.width > 32) {
				var v = Int64.unboxVal(val, i1.signed);
				return TypeCast.new(CastOp.VALUE, Int64.box(v));
			}
			return TypeSystem.TRUE;
		}
		return evalTruncate(i2, i1.signed, val);
	}
	def evalTruncate(t: Int_TypeCon, signed: bool, val: Val) -> TypeCast {
		if (t.width <= 32) {
			if (Box<int>.?(val)) {
				var b = Int.unbox(val), v = t.trunc_32(b);
				return if(v == b, TypeSystem.TRUE, TypeCast.new(CastOp.VALUE, Box.new(v)));
			} else {
				var v = t.trunc_32(Int64.unbox(val).1);
				return TypeCast.new(CastOp.VALUE, Box.new(v));
			}
		} else if (t.width < 64) {
			var v = t.trunc_64(Int64.unboxVal(val, signed));
			return TypeCast.new(CastOp.VALUE, Int64.box(v));
		}
		return TypeSystem.TRUE;
	}
}
