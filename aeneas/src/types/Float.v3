// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

component Float {
	def FLOAT32_TYPECON = Float_TypeCon.new(8, 23, "float");
	def FLOAT64_TYPECON = Float_TypeCon.new(11, 52, "double");
	def FLOAT32 = FLOAT32_TYPECON.create0();
	def FLOAT64 = FLOAT64_TYPECON.create0();

	def VIEW_TYPE_PARAM_LIST_32 = List.new(TypeUtil.newTypeParamWithConstraint(TypeUtil.BUILTIN_TOKEN, TypeUtil.globalCache,
		true, checkFloatViewTypeArg(32, _, _)), null);
	def VIEW_TYPE_PARAM_LIST_64 = List.new(TypeUtil.newTypeParamWithConstraint(TypeUtil.BUILTIN_TOKEN, TypeUtil.globalCache,
		true, checkFloatViewTypeArg(64, _, _)), null);

	def F32_ZERO = Float32Val.new(0);
	def F64_ZERO = Float64Val.new(0);
	def F32_ONE = Float32Val.new(0x3f800000);
	def F64_ONE = Float64Val.new(0x3ff0000000000000);
	def F32_NAN = Float32Val.new(0x7fc00000);
	def F64_NAN = Float64Val.new(0x7ff8000000000000);
	def F32_MINUS_ZERO = Float32Val.new(0x80000000u);
	def F64_MINUS_ZERO = Float64Val.new(0x8000000000000000ul);
	def F32_MINUS_INFINITY = Float32Val.new(0xff800000u);
	def F64_MINUS_INFINITY = Float64Val.new(0xfff0000000000000ul);
	def F32_INFINITY = Float32Val.new(0x7f800000u);
	def F64_INFINITY = Float64Val.new(0x7ff0000000000000ul);

	def f32(sign: int, exp: int, mantissa: u32) -> Float32Val {
		var bits = if(sign != 0, 0x80000000u);
		if (exp < -150) return Float32Val.new(bits);
		if (exp <= -127) {
			// subnormal; make implicit 1 explicit and shift right
			mantissa = 0x80000000u | mantissa >> 1;
			var shift = -127 - exp;
			mantissa = (u32.!(roundOff(mantissa, 9 + shift)) << 9) + (mantissa >> u5.view(shift));
			exp = -127;
		} else if (exp >= 128) {
			// saturate large exponents to infinity
			return if (sign != 0, F32_MINUS_INFINITY, F32_INFINITY);
		}
		bits |= (u32.!(u8.!(exp + 127)) << 23);
		bits |= mantissa >> 9;
		return Float32Val.new(bits);
	}
	def f64(sign: int, exp: int, mantissa: u64) -> Float64Val {
		var bits = if(sign != 0, 0x8000000000000000ul);
		if (exp < -1076) return Float64Val.new(bits);
		if (exp <= -1023) {
			// subnormal; make implicit 1 explicit and shift right
			mantissa = 0x8000000000000000ul | mantissa >> 1;
			var shift = -1023 - exp;
			mantissa = (u64.!(roundOff(mantissa, 12 + shift)) << 12) + (mantissa >> u6.view(shift));
			exp = -1023;
		} else if (exp >= 1024) {
			// saturate large exponents to infinity
			return if (sign != 0, F64_MINUS_INFINITY, F64_INFINITY);
		}
		bits |= (u64.!(u11.!(exp + 1023)) << 52);
		bits |= mantissa >> 12;
		return Float64Val.new(bits);
	}
	def roundOff(val: u64, bits: int) -> u1 {
		var b = val << u6.view(64 - bits);
		var max = 0x8000000000000000uL;
		if (b < max) return 0;
		if (b > max) return 1;
		if ((val << u6.view(63 - bits)) >= max) return 1;
		return 0;
	}
}
// Type constructor for floating point types. Generalized to have a configurable
// exponent and fraction width (in bits) and a sign bit.
class Float_TypeCon extends Member_TypeCon {
	def exp_width: byte;
	def fraction_width: byte;
	def total_width = byte.!(1 + exp_width + fraction_width);
	def is64 = total_width == 64;
	var cache: FloatOpCache;

	new(exp_width, fraction_width, name: string)
		super(name, V3Kind.FLOAT, 0, TypeUtil.globalCache) {
		cache = FloatOpCache.new(this, create0());
	}

	def lookupMember(t: Type, name: string, receiver: bool) -> LookupResult {
		var op: Operator;
		if (Strings.equal(name, "sign")) op = cache.opSign;
		else if (Strings.equal(name, "exponent")) op = cache.opExponent;
		else if (Strings.equal(name, "fraction")) op = cache.opFraction;
		if (op != null) {
			if (receiver) return LookupResult.Apply(op, null, Fact.O_PURE);
			else return LookupResult.Inst(op, null);
		}
		if (receiver) return LookupResult.None;

		var typeParams: List<TypeParamType>;
		if (Strings.equal(name, "nan")) {
			var val = if(is64, Float.F64_NAN, Float.F32_NAN);
			return LookupResult.Const(t, val);
		} else if (Strings.equal(name, "infinity")) {
			var val = if(is64, Float.F64_INFINITY, Float.F32_INFINITY);
			return LookupResult.Const(t, val);
		} else if (Strings.equal(name, "roundi")) {
			var op = V3Op.newFloatRoundI(TypeUtil.TYPE_PARAM, t);
			return LookupResult.Inst(op, TypeUtil.ONE_TYPE_PARAM);
		} else if (Strings.equal(name, "round")) {
			var op = V3Op.newFloatRound(t);
			return LookupResult.Inst(op, null);
		} else if (total_width < 64 && Strings.equal(name, "roundd")) {
			op = V3Op.opFloatRoundD;
		} else if (Strings.equal(name, "view")) {
			if (is64) {
				typeParams = Float.VIEW_TYPE_PARAM_LIST_64;
				op = V3Op.newFloat64ViewI(typeParams.head);
			} else {
				typeParams = Float.VIEW_TYPE_PARAM_LIST_32;
				op = V3Op.newFloat32ViewI(typeParams.head);
			}
		}
		else if (Strings.equal(name, "abs")) op = cache.opAbs;
		else if (Strings.equal(name, "ceil")) op = cache.opCeil;
		else if (Strings.equal(name, "floor")) op = cache.opFloor;
		else if (Strings.equal(name, "sqrt")) op = cache.opSqrt;

		if (op != null) return LookupResult.Inst(op, typeParams);
		return LookupResult.None;
	}
	def lookupInfix(infix: V3Infix, rtype: Type) -> Operator {
		var t = this;
		if (rtype != null && rtype.typeCon.kind == V3Kind.FLOAT) {
			var that = Float_TypeCon.!(rtype.typeCon);
			if (that.total_width > this.total_width) t = that;
		}
		var op: Operator;
		match (infix) {
			Add => op = t.cache.opAdd;
			Sub => op = t.cache.opSub;
			Mul => op = t.cache.opMul;
			Div => op = t.cache.opDiv;
			EqEq => op = t.cache.opEqual;
			NotEq => op = t.cache.opNotEqual;
			Lt => op = t.cache.opLt;
			LtEq => op = t.cache.opLteq;
			Gt => op = t.cache.opGt;
			GtEq => op = t.cache.opGteq;
			_ => ;
		}
		return op;
	}
	def min(itt: IntType) -> Val {
		if (itt.signed)	return if(is64, Float.f64(-1, itt.width - 1, 0), Float.f32(-1, itt.width - 1, 0));
		return if(is64, Float.F64_ZERO, Float.F32_ZERO);
	}
	def mind(itt: IntType) -> double {
		if (itt.signed)	return double.view(Float.f64(-1, itt.width - 1, 0).bits);
		return 0;
	}
	def max(itt: IntType) -> Val {
		var width = if(itt.signed, itt.width - 1, itt.width);
		if (is64) {
			if (width == 0) return Float.F64_ZERO;
			var mantissa = u64.max << u6.view(64 - width);
			return Float.f64(0, width - 1, mantissa << 1);
		} else {
			if (width == 0) return Float.F32_ZERO;
			var mantissa = u32.max;
			if (width < 32) mantissa = mantissa << u5.view(32 - width);
			return Float.f32(0, width - 1, mantissa << 1);
		}
	}
	def maxplus1(itt: IntType) -> Val {
		if (itt.signed) return if(is64, Float.f64(0, itt.width - 1, 0), Float.f32(0, itt.width - 1, 0));
		return if(is64, Float.f64(0, itt.width, 0), Float.f32(0, itt.width, 0));
	}
	def maxplus1d(itt: IntType) -> double {
		if (itt.signed) return double.view(Float.f64(0, itt.width - 1, 0).bits);  // XXX: boxing
		return double.view(Float.f64(0, itt.width, 0).bits);
	}
	def tryPromoteS(v: long) -> Val {
		if (v == long.min) return null; // not representable
		return if (v < 0, tryPromoteU(1, u64.!(0 - v)), tryPromoteU(0, u64.!(v)));
	}
	def tryPromoteU(sign: u1, v: u64) -> Val {
		if (v == 0) {
			return if(is64, Float.F64_ZERO, Float.F32_ZERO);
		}
		var exponent = 62u + (1u << u5.view(exp_width-1));
		while ((v >> 63) == 0) {
			v = v << 1;
			exponent--;
		}
		v = v << 1; // shift off implicit 1 bit
		var shift = u6.view(64 - fraction_width);
		var fraction = (v >> shift);
		if (v != (fraction << shift)) return null; // requires rounding
		var r = u64.!(sign) << u6.view(total_width - 1);
		r |= u64.!(exponent) << u6.view(fraction_width);
		r |= fraction;
		return if(is64, Float64Val.new(r), Float32Val.new(u32.view(r)));
	}
	def exponentBias() -> int {
		return (1 << u5.view(exp_width - 1)) - 1;
	}
}
class FloatOpCache(ftc: Float_TypeCon, ft: Type) {
	def arr_t: Array<Type> = [ft];
	def typeArgs = arr_t;
	def tupleType = Tuple.newType(Lists.cons2(ft, ft));
	def unopSig = Function.sig(ft, ft);
	def binopSig = Function.sig(tupleType, ft);
	def cmpSig = Function.sig(tupleType, Bool.TYPE);

	def opAdd = newOp(Opcode.FloatAdd(ftc.is64), typeArgs, binopSig);
	def opSub = newOp(Opcode.FloatSub(ftc.is64), typeArgs, binopSig);
	def opMul = newOp(Opcode.FloatMul(ftc.is64), typeArgs, binopSig);
	def opDiv = newOp(Opcode.FloatDiv(ftc.is64), typeArgs, binopSig);

	def opSign = V3Op.newVstSugar(VstOperator.FloatSign, arr_t, arr_t, Int.getType(false, 1));
	def opExponent = V3Op.newVstSugar(VstOperator.FloatExponent, arr_t, arr_t, Int.getType(true, ftc.exp_width));
	def opFraction = V3Op.newVstSugar(VstOperator.FloatFraction, arr_t, arr_t, Int.getType(false, ftc.fraction_width));

	def opAbs = newOp(Opcode.FloatAbs(ftc.is64), typeArgs, unopSig);
	def opCeil = newOp(Opcode.FloatCeil(ftc.is64), typeArgs, unopSig);
	def opFloor = newOp(Opcode.FloatFloor(ftc.is64), typeArgs, unopSig);
	def opSqrt = newOp(Opcode.FloatSqrt(ftc.is64), typeArgs, unopSig);

	def opEqual = newOp(Opcode.FloatEq(ftc.is64), typeArgs, cmpSig);
	def opNotEqual = newOp(Opcode.FloatNe(ftc.is64), typeArgs, cmpSig);
	def opLt = newOp(Opcode.FloatLt(ftc.is64), typeArgs, cmpSig);
	def opLteq = newOp(Opcode.FloatLteq(ftc.is64), typeArgs, cmpSig);
	def opGt = V3Op.newVstSugar(VstOperator.Commute(opLt), arr_t, [ft, ft], Bool.TYPE);
	def opGteq = V3Op.newVstSugar(VstOperator.Commute(opLteq), arr_t, [ft, ft], Bool.TYPE);
}
def newOp(op: Opcode, typeArgs: Array<Type>, sig: Signature) -> Operator {
	return Operator.new(op, typeArgs, sig);
}
class Float32Val(bits: u32) extends Val {
	def equals(that: Val) -> bool {
		// TODO(float): != is always false for nans.
		return Float32Val.?(that) && Float32Val.!(that).bits == bits;
	}
}
class Float64Val(bits: u64) extends Val {
	def equals(that: Val) -> bool {
		// TODO(float): != is always false for nans.
		return Float64Val.?(that) && Float64Val.!(that).bits == bits;
	}
}
def checkFloatViewTypeArg(width: int, tv: TypeVarType, t: Type) -> Type {
	if (IntType.?(t)) {
		var it = IntType.!(t);
		if (it.width <= width) return Int.getType(it.signed, width);
	}
	if (!tv.error) {
		tv.error = true;
		tv.error_msg = Strings.format1("view requires %d-bit integral type argument", width);
	}
	return null;
}
