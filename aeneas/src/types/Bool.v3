// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Utilities for the representation of the "bool" type in the compiler.
component Bool {
	def TYPE  = Bool_TypeCon.new().create0();
	def TRUE  = Box.new(true);
	def FALSE = Box.new(false);

	def unbox(val: Result) -> bool {
		if (val == null) return false;
		return (Box<bool>.!(val)).val;
	}
	def box(val: bool) -> Box<bool> {
		return if(val, TRUE, FALSE);
	}
	def toInt(b: bool) -> int {
		return if(b, 1, 0);
	}
}
class Bool_TypeCon extends Member_TypeCon("bool", V3Kind.BOOL, 0, TypeUtil.globalCache) {
	def lookupInfix(infix: byte, rtype: Type) -> OperatorOf<V3OpSyntax> {
		if (infix == V3Infix.AndAnd) return V3Op.opBoolAnd;
		if (infix == V3Infix.OrOr) return V3Op.opBoolOr;
		return null;
	}
}
