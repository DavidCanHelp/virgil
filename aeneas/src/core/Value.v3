// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A Result represents the result of evaluating an expression, which can either
// be a Val or an Exception.
class Result { }
// A Val represents a runtime value, such as integer, object reference, tuple, etc.
class Val extends Result {
	def equals(val: Val) -> bool { return this == val; }
}
// The Box class provides a utility to simplify using first-order values 
// (e.g. the integer 0) as meta-level values (e.g. the Constant #0).
class Box<T> extends Val {
	def val: T;
	new(val) { }
	def equals(other: Val) -> bool {
		if (other == this) return true;
		if (other == Values.BOTTOM) {
			var none: T;
			return this.val == none;
		}
		return Box<T>.?(other) && this.val == Box<T>.!(other).val;
	}
}
// The source position in a method, such as for an exception stacktrace.
class Source {
	def mdecl: VstMethod;	// declared method
	def line: int;	// line number
	def column: int;	// column number
	def tail: Source;	// caller, if this method is inlined into that caller
	new(mdecl, line, column, tail) { }
	def render(buffer: StringBuffer) -> StringBuffer {
		buffer.format1("in %1", mdecl.getFullName());
		if (line >= 0) {
			return buffer.format3(" [%1 @ %2:%3]", mdecl.token.fileName, line, column);
		}
		return buffer;
	}
}
// An Exception represents an exceptional result (i.e. not a value), for example a
// safety violation such as !NullCheckException or !DivideByZeroException.
class Exception extends Result {
	def error: string;
	def msg: string;
	def trace: List<Source>;
	new(error, msg, trace) { }
}
// Utility methods for values.
component Values {
	var BOTTOM: Val = null;
	var NONE: Array<Val> = [];
	def equal(val1: Val, val2: Val) -> bool {
		if (val1 == val2) return true;
		if (val1 == null) return val2.equals(val1);
		return val1.equals(val2);
	}
}
