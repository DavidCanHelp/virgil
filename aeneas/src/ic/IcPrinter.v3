// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A utility class to print out information about an IcMethod and its code
class IcPrinter(interp: IcInterpreter) {
	var indent: int;
	var successors: bool = true;
	var buf = TerminalBuffer.new();

	def printConsts(firstConst: int, consts: Array<Val>) {
		for (i < consts.length) {
			buf.reset();
			doIndent(buf);
			buf.put1("value #%d = ", i + firstConst).putcv(consts[i], null);
			buf.outln();
		}
	}
	def printCode(iccode: Array<IcInstr>) {
		for (i < iccode.length) {
			print(i, iccode[i]);
		}
	}
	def render(b: StringBuilder, ip: int, i: IcInstr) -> StringBuilder {
		doIndent(b);
		b.putd(ip);
		b.puts(": ");
		if (ip < 100) b.sp();
		if (ip < 10) b.sp();
		match (i) {
			Move(dst, src) => {
				b.put2("move  #%d = %s", dst, renderReg(src));
			}
			Int32Binop(dst, x, y, opcode, src) => {
				b.put2("int32 #%d = %s(", dst, opcode.name);
				renderList(b, [x, y]);
				b.putc(')');
			}
			Int32Compare(dst, x, y, opcode, src) => {
				b.put2("int32 #%d = %s(", dst, opcode.name);
				renderList(b, [x, y]);
				b.putc(')');
			}
			Int32Unop(dst, x, shift, opcode, src) => {
				b.put2("int32 #%d = %s(", dst, opcode.name);
				renderList(b, [x]);
				b.putc(')');
			}
			Int64Binop(dst, x, y, lsigned, rsigned, opcode, src) => {
				b.put2("int64 #%d = %s(", dst, opcode.name);
				renderList(b, [x, y]);
				b.putc(')');
			}
			Int64Compare(dst, x, y, lsigned, rsigned, opcode, src) => {
				b.put2("int64 #%d = %s(", dst, opcode.name);
				renderList(b, [x, y]);
				b.putc(')');
			}
			Int64Unop(dst, x, shift, signed, opcode, src) => {
				b.put2("int64 #%d = %s(", dst, opcode.name);
				renderList(b, [x]);
				b.putc(')');
			}
			FastOp(dst, x, y, op, opcode, src) => {
				b.put2("fast  #%d = %q(", dst, op.render);
				renderList(b, [x, y]);
				b.putc(')');
			}
			Apply(dst, op, args, src) => {
				b.put2("apply #%d = %q(", dst, op.render);
				renderList(b, args);
				b.putc(')');
			}
			Call(dst, func, args, src) => {
				b.put2("call  #%d = %s(", dst, renderReg(func));
				renderList(b, args);
				b.putc(')');
			}
			Switch(arg, targets) => {
				b.put1("switch %s", renderReg(arg));
				if (successors) {
					b.ln();
					for (k < targets.length - 1) {
						doIndent(b).put2("        %d -> @%d\n", k, targets[k]);
					}
					doIndent(b).put1("        _ -> @%d", targets[targets.length - 1]);
				}
			}
			Branch(always, bval, arg, target) => {
				if (always) b.put1("jump  @%d", target);
				else if (bval) b.put2("bt    %s @%d", renderReg(arg), target);
				else b.put2("bf    %s @%d", renderReg(arg), target);
			}
			Ret(arg) => {
				b.put1("ret   %s", renderReg(arg));
			}
			Throw(exception, src) => {
				b.put1("throw %s", exception);
			}
			Probe(orig, probes) => {
				b.puts("[probe] ");
				render(b, ip, orig);
				return b;
			}
		}
		return b.ln();
	}
	def print(ip: int, i: IcInstr) {
		render(buf.reset(), ip, i);
		buf.outt();
	}
	def doIndent(b: StringBuilder) -> StringBuilder {
		return b.pad(' ', b.length + indent * 2);
	}
	def renderList(b: StringBuilder, a: Array<int>) {
		for (i < a.length) {
			if (i > 0) b.csp();
			b.puts(renderReg(a[i]));
		}
	}
	def renderReg(reg: int) -> string { // XXX: add TerminalBuffer as param
		var b = TerminalBuffer.new();
		if (interp != null) {
			var index = interp.rp + reg;
			if (u32.view(index) >= u32.view(interp.regs.length)) b.put1("#%d=!!", reg);
			else b.put1("#%d=", reg).putcv(interp.regs[index], null);
		}
		else b.putc('#').putd(reg);
		return b.toString();
	}
}

// Tracer. Use as a last resort, usually to debug the IcInterpreter
class IcTracer {
	var p: IcPrinter;
	var lastFrame: IcFrame;
	var depth: int;

	new(i: IcInterpreter) {
		p = IcPrinter.new(i);
		i.addProbeBefore(before);
		i.addProbeAfter(after);
	}
	def before(i: IcInterpreter, probeIp: int) {
		lastFrame = i.topFrame;
		p.indent = depth;
		p.print(i.ip, i.code[i.ip]);
	}
	def after(i: IcInterpreter, probeIp: int) {
		var top = i.topFrame;
		if (top != lastFrame) {
			// a call or return must have occurred
			if (top != null) {
				if (top.next == lastFrame) { depth = depth + 1;}
				else { depth = depth - 1; }
			}
		}
	}
}
// Prints a dynamic call tree as a program executes.
class IcCallTracer(matcher: VstMatcher, traceParent: bool) {
	def buf = TerminalBuffer.new();
	var depth: int;
	var frame: IcFrame;
	// instrument the code of a method.
	def onGen(memberRef: IrSpec, icm: IcMethod) {
		if (!matcher.matches(memberRef.asMethod().source)) return;
		for (i < icm.iccode.length) {
			var j = icm.iccode[i];
			if (IcInstr.Ret.?(j)) icm.addProbeBefore(i, pmOnRet);
		}
		icm.addProbeBefore(0, pmOnEntry);
	}
	// Print the method and args on entry.
	def pmOnEntry(i: IcInterpreter, probeIp: int) {
		var topFrame = i.topFrame;
		if (topFrame != frame) {
			// this method has just been entered (i.e. not a loop back to start)
			buf.reset();
			for (i < depth) buf.puts("  ");
			if (traceParent && topFrame.next != null) {
				buf.green();
				topFrame.next.icm.memberRef.member.renderLong(buf);
				buf.end();
				buf.puts("(");
				for (j < topFrame.next.icm.numParams) {
					if (j > 0) buf.csp();
					var v = i.regs[topFrame.retRp + j];
					buf.putcv(v, null);
				}
				buf.puts(")\n  => ");
			}
			buf.green();
			topFrame.icm.memberRef.member.renderLong(buf);
			buf.end();
			buf.puts("(");
			for (j < topFrame.icm.numParams) {
				if (j > 0) buf.csp();
				var v = i.regs[i.rp + j];
				buf.putcv(v, null);
			}
			buf.puts(")").outln();
			frame = topFrame;
			depth++;
		}
	}
	// Pop the current frame on return.
	def pmOnRet(i: IcInterpreter, probeIp: int) {
		if(frame == null) return;
		frame = frame.next;
		depth--;
	}
}
