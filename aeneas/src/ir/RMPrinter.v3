// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Utility for printing results from RMA.
component RmPrinter {
	def print(rma: RmAnalyzer) {
		rma.typeMap.apply(RmPrinter.printRmaType);
		rma.specializer.polyMap.apply(RmPrinter.printIrSet(rma, _, _));
		Terminal.put("methods {\n");
		Lists.apply(rma.liveMethods, RmPrinter.printLiveMethod);
		Terminal.put("}\n");
	}
	def printRmaType(utype: Type, typeInfo: RmaType) {
		if (V3.isClass(utype)) Terminal.put("class ");
		else if (V3.isComponent(utype)) Terminal.put("component ");
		else Terminal.put("type ");
		Terminal.putb(typeInfo.oldType.render(StringBuffer.new()));
		if (typeInfo.instances != null) Terminal.put(" [heap]");
		if (typeInfo.isAllocated) Terminal.put(" [alloc]");
		Terminal.put(" {\n");
		typeInfo.applySubInfo(printRmaTypeRef);
		typeInfo.applyFields(printUsedRmaMember, typeInfo);
		typeInfo.applyMethods(printUsedRmaMember, typeInfo);
		if (typeInfo.instances != null) typeInfo.instances.apply(printRecord);
		Terminal.put("}\n");
	}
	def printIrSet(rma: RmAnalyzer, method: IrMethod, poly: IrSet) {
		var buf = StringBuffer.new();
		buf.puts("@poly ");
		method.renderLong(buf);
		poly.enum(rma.specializer, printPolySet(buf, _));
		Terminal.putbln(buf);
		for (l = poly.polyOps; l != null; l = l.tail) {
			buf.reset();
			buf.put1('\t');
			V3Op.renderInto(l.head, buf);
			Terminal.putbln(buf);
		}
	}
	def printPolySet(buf: StringBuffer, list: List<IrSpec>) {
		buf.puts("\t[");
		for (l = list; l != null; l = l.tail) {
			for (s = l.head.getTypes().typeArgsList; s != null; s = s.tail) {
				s.head.render(buf);
			}
		}
		buf.puts("]");
	}
	def printUsedRmaMember(typeInfo: RmaType, memberInfo: RmaMember) {
		Terminal.put("    + ");
		printRmaMember(typeInfo, memberInfo);
	}
	def printRmaMember(typeInfo: RmaType, memberInfo: RmaMember) {
		var memberRef = memberInfo.origMember;
		if (memberRef.isField()) {
			if (memberRef.member.checkFact(Facts.F_VALUE)) Terminal.put("value  {");
			else Terminal.put("field  {");
		} else if (memberRef.isNew()) Terminal.put("new    {");
		else Terminal.put("method {");
		Terminal.put(Strings.render(memberRef.render));
		if (memberInfo.isRead) Terminal.put(" [read]");
		if (RmaField.?(memberInfo)) {
			var fieldInfo = RmaField.!(memberInfo);
			if (fieldInfo.isWritten) Terminal.put(" [written]");
			if (fieldInfo.isInit) Terminal.put(" [init]");
		}
		if (RmaMethod.?(memberInfo)) {
			var methodInfo = RmaMethod.!(memberInfo);
			if (methodInfo.isLive) Terminal.put(" [impl]");
		}
		Terminal.put("}\n");
	}
	def printRmaTypeRef(utype: Type, typeInfo: RmaType) {
		Terminal.put("    subt: {");
		Terminal.putb(utype.render(StringBuffer.new()));
		Terminal.put("}\n");
	}
	def printRecord(record: Record, r: Record) {
		Terminal.put("    record {");
		Terminal.puti(record.id);
		Terminal.put(": ");
		Terminal.putb(record.rtype.render(StringBuffer.new()));
		Terminal.put("}\n");
	}
	def printLiveMethod(memberInfo: RmaMethod) {
		Terminal.put("    + ");
		printRmaMember(memberInfo.container, memberInfo);
	}
}
