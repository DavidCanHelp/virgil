// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.
// module which provides the "Pointer" type
component MachModule {
	def ptr = "Pointer";
	def PTR_16: MachPointer_TypeCon = MachPointer_TypeCon.new(ptr, 16);
	def PTR_32: MachPointer_TypeCon = MachPointer_TypeCon.new(ptr, 32);
	def PTR_64: MachPointer_TypeCon = MachPointer_TypeCon.new(ptr, 64);
	def install(prog: Program, mach: MachProgram) {
		// install the builtin "Pointer" type in the program
		var size = mach.data.addressWidth;
		if (size <= 16) return prog.typeEnv.add(PTR_16);
		if (size <= 32) return prog.typeEnv.add(PTR_32);
		return prog.typeEnv.add(PTR_64);
	}
}
// Type constructor for language-level "Pointer" type, available to programs
// that target machine targets (e.g. x86-darwin)
class MachPointer_TypeCon extends Member_TypeCon {
	def instMembers = Strings.newMap<(Operator, List<TypeParamType>)>();
	def width: byte;
	def size = byte.view((width + 7u) >>> 3);
	var ptrType: Type;
	var opAdd: Operator;
	var opSub: Operator;
	var opLt: Operator;
	var opGt: Operator;
	var opLteq: Operator;
	var opGteq: Operator;
	var atContents: Operator;
	var atLength: Operator;
	var atObject: Operator;
	var atElement: Operator;

	new(name: string, width) super(name, V3Kind.POINTER, 0, TypeUtil.globalCache) {
		ptrType = create0();
		var typeParam = TypeUtil.ONE_TYPE_PARAM.head;
		var arrayType = V3Array.newType(typeParam);
		instMembers["load"] = (V3Op.newPtrLoad(ptrType, typeParam), TypeUtil.ONE_TYPE_PARAM);
		instMembers["store"] = (V3Op.newPtrStore(ptrType, typeParam), TypeUtil.ONE_TYPE_PARAM);
		instMembers["cmpswp"] = (V3Op.newPtrCmpSwp(ptrType, typeParam), TypeUtil.ONE_TYPE_PARAM);
		atContents = V3Op.newPtrAtContents(typeParam, ptrType);
		atLength = V3Op.newPtrAtLength(typeParam, ptrType);
		atObject = V3Op.newPtrAtObject(TypeUtil.REF_TYPE_PARAM.head, ptrType);
		atElement = V3Op.newPtrAtElement(typeParam, ptrType);
		var it = Int.getType(true, width);
		opAdd = V3Op.newPtrAdd(ptrType, it);
		opSub = V3Op.newPtrSub(ptrType, it);
		opLt = V3Op.newPtrLt(ptrType);
		opLteq = V3Op.newPtrLteq(ptrType);
		opGt = V3Op.newVstSugar(VstOperator.Commute(opLt), opLt.typeArgs, opLt.sig.paramTypes, Bool.TYPE);
		opGteq = V3Op.newVstSugar(VstOperator.Commute(opLteq), opLt.typeArgs, opLt.sig.paramTypes, Bool.TYPE);
	}
	def lookupInfix(infix: V3Infix, rtype: Type) -> Operator {
		match (infix) {
			_ => return null;
		}
	}
}
