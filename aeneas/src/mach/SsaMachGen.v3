// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Transforms "v3" code in SSA form to "mach" code (i.e. machine-level
// operations such as memory load/store).
class SsaMachGen extends SsaRebuilder {
	def mach: MachProgram;
	var explicitNullChecks: bool;

	new(mach, context: SsaContext) : super(context) { }

	def build() {
		context.method.ssa = genRegion();
		context.printSsa("Machine");
	}
	def genParam(oi: SsaParam, params: Sequence<SsaParam>) {
		var np = SsaParam.new(params.length, mach.machType(oi.vtype));
		params.add(np);
		map1(oi, np);
	}
	def genReturnType() -> Type {
		return mach.machType(context.region.returnType);
	}
	def genPhi(oi: SsaPhi) {
		// XXX: factor up into SsaRebuilder
		map1(oi, SsaPhi.new(mach.machType(oi.vtype), mapBlockStart(oi.block), null));
	}
	def genVal(oi: SsaValue) {
		map1(oi, newRegion.valConst(mach.machType(oi.vtype), mach.machVal(oi.val, oi.vtype)));
	}
	def genApplyOp(oi: SsaApplyOp) {
		var ni: SsaInstr;
		match(oi.op.opcode) {
			V3Opcode.Equal:			ni = apply(oi.source, V3Op.newEqual(mach.machType(oi.op.typeArgs(0))), genRefs(oi.inputs));
			V3Opcode.NotEqual: 		ni = apply(oi.source, V3Op.newNotEqual(mach.machType(oi.op.typeArgs(0))), genRefs(oi.inputs));
			V3Opcode.IntAdd,
			V3Opcode.IntSub,
			V3Opcode.IntMul,
			V3Opcode.IntDiv,
			V3Opcode.IntMod,
			V3Opcode.IntShl,		// XXX: signed IntShr only needs truncation because shifts are currently logical
			V3Opcode.IntShr:		ni = genIntOpWithTrunc(oi); // truncation necessary
			V3Opcode.IntAnd,
			V3Opcode.IntOr,
			V3Opcode.IntXor,
			V3Opcode.IntLt,
			V3Opcode.IntGt,
			V3Opcode.IntLteq,
			V3Opcode.IntGteq,
			V3Opcode.IntConvert,
			V3Opcode.BoolAnd,
			V3Opcode.BoolOr,
			V3Opcode.BoolNot: 		ni = genSimple(oi);
			V3Opcode.TypeCast: 		ni = genTypeCast(oi);
			V3Opcode.TypeQuery: 		ni = genTypeQuery(oi);
			V3Opcode.TypeSubsume: 		ni = genTypeSubsume(oi);
			V3Opcode.ArrayAlloc: 		ni = genArrayAlloc(oi);
			V3Opcode.ArrayInit: 		ni = genArrayInit(oi);
			V3Opcode.ArrayGetElem: 		ni = genArrayGetElem(oi);
			V3Opcode.ArraySetElem: 		ni = genArraySetElem(oi);
			V3Opcode.ArrayGetLength: 	ni = genArrayGetLength(oi);
			V3Opcode.ClassAlloc: 		ni = genClassAlloc(oi);
			V3Opcode.ClassGetField: 	ni = genClassGetField(oi);
			V3Opcode.ClassInitField,
			V3Opcode.ClassSetField: 	ni = genClassSetField(oi);
			V3Opcode.ClassGetMethod: 	ni = genClassGetMethod(oi);
			V3Opcode.ClassGetVirtual: 	; // should not occur
			V3Opcode.ClassGetVirtFunc: 	ni = genClassGetVirtual(oi);
			V3Opcode.ComponentInit: 	; // should not occur
			V3Opcode.ComponentGetField: 	ni = genComponentGetField(oi);
			V3Opcode.ComponentSetField: 	ni = genComponentSetField(oi);
			V3Opcode.ComponentGetMethod: 	; // should not occur
			V3Opcode.TupleCreate: 		ni = genTupleCreate(oi);
			V3Opcode.TupleGetElem: 		ni = genTupleGetElem(oi);
			V3Opcode.NullCheck: {
				ni = genNullCheck(oi);
				instrMap.set(oi, ni);
				return; // don't update SSA facts!!!
			}
			V3Opcode.BoundsCheck: 		ni = genBoundsCheck(oi, true);
			V3Opcode.CallMethod: 		ni = genCallMethod(oi);
			V3Opcode.CallClassVirtual: 	ni = genCallClassVirtual(oi);
			V3Opcode.CallDelegate:		; // should not occur
			V3Opcode.CallFunction: {
				var ftype = oi.op.typeArgs(0);
				ni = call(oi.source, true, mach.machType(ftype), null, genRefs(oi.inputs));
			}
			V3Opcode.VariantGetTag:	{
				var oobj = oi.inputs(0), nobj = genRef1(oobj);
				ni = genIfNull(oi, oi.op.resultType, nobj, null, genVariantGetTag(oi, _));
			}
			V3Opcode.ConditionalThrow:	ni = genConditionalThrow(oi);
			MachOpcode.MachSpecial: {
				// perform specialized lowering by calling the supplied function
				var loweringFunc = oi.op.attr<(SsaApplyOp, SsaMachGen) -> SsaInstr>();
				ni = loweringFunc(oi, this);
			}
		} else {
			// assume machine-level and other operators aren't lowered
			ni = curBlock.addApply(oi.source, oi.op, genRefs(oi.inputs));
		}
		if (ni == null) return context.fail(Strings.format1("unexpected opcode %1", oi.op.opcode));
		map1(oi, ni);
	}
	def genIntOpWithTrunc(oi: SsaApplyOp) -> SsaInstr {
		// TODO: convert to actual 32-bit int op
		var ni = apply(oi.source, oi.op, genRefs(oi.inputs));
		if (V3.asInt(oi.op.resultType).width < 32) { // TODO: use actual machine width
			ni = apply(null, V3Op.newIntConvert(Int.TYPE, oi.op.resultType), [ni]);
		}
		return ni;
	}
	def genSimple(oi: SsaApplyOp) -> SsaInstr {
		return apply(oi.source, oi.op, genRefs(oi.inputs));
	}
	def genTypeCast(oi: SsaApplyOp) -> SsaInstr {
		var castOp = oi.op.attr<TypeCast>();
		var ft = oi.op.typeArgs(0), tt = oi.op.typeArgs(1);
		match (castOp.approx) {
			CastOp.SUBSUME,
			CastOp.TRUE: return genRef1(oi.inputs(0));
			CastOp.FALSE: return curBlock.addThrow(oi.source, V3Exception.TypeCheck);
			CastOp.VALUE: return newRegion.valConst(tt, castOp.result);
			CastOp.CLASS_CAST: return genClassCast(ft, tt, oi);
		} else {
			// other kinds of casts should have been removed
			context.fail(Strings.format1("unexpected cast %1", castOp.name()));
			return newRegion.nop();
		}
	}
	def genClassCast(ft: Type, tt: Type, oi: SsaApplyOp) -> SsaInstr {
		var oobj = oi.inputs(0), nobj = genRef1(oobj);
		if (V3.isClass(ft) && V3.isClass(tt)) {
			if (context.compiler.DisableTypeChecks) return nobj;
			var t = mach.classIdRange(tt), low = t.0, high = t.1;
			if (low == high) {
				// no live classes can match, only null
				if (V3Op.needsNullCheck(oi, oobj.dest)) {
					var cmp = apply(null, V3Op.newNotEqual(ft), [nobj, newRegion.nullConst(ft)]);
					apply(oi.source, V3Op.newConditionalThrow(V3Exception.TypeCheck), [cmp]);
					return newRegion.nullConst(tt);
				}
				return curBlock.addThrow(oi.source, V3Exception.TypeCheck);
			}
			return genIfNull(oi, mach.machType(tt), nobj, null, genClassIdCheck(oi, low, high, _));
		}
		return curBlock.addThrow(oi.source, V3Exception.TypeCheck);
	}
	def genClassIdCheck(oi: SsaApplyOp, low: int, high: int, nobj: SsaInstr) -> SsaInstr {
		var tid = ptrLoad(mach.tagType, nobj, 0);
		if (high == low + 1) {
			var cmp = apply(null, V3Op.newNotEqual(Int.TYPE), [tid, newRegion.intConst(low)]);
			apply(oi.source, V3Op.newConditionalThrow(V3Exception.TypeCheck), [cmp]);
		} else {
			var cmp1 = curBlock.opIntLt(tid, newRegion.intConst(low));
			apply(oi.source, V3Op.newConditionalThrow(V3Exception.TypeCheck), [cmp1]);
			var cmp2 = curBlock.opIntGteq(tid, newRegion.intConst(high));
			apply(oi.source, V3Op.newConditionalThrow(V3Exception.TypeCheck), [cmp2]);
		} 
		return nobj;
	}
	def genTypeQuery(oi: SsaApplyOp) -> SsaInstr {
		var ft = oi.op.typeArgs(0), tt = oi.op.typeArgs(1);
		if (V3.isClass(ft) && V3.isClass(tt)) {
			var oobj = oi.inputs(0), nobj = genRef1(oobj);	
			var t = mach.classIdRange(tt), low = t.0, high = t.1;
			if (low == high) {
				// no live classes can match, and null is not an instance of
				return newRegion.falseConst();
			}
			return genIfNull(oi, Bool.TYPE, nobj, null, genClassIdQuery(oi, low, high, _));
		}
		return newRegion.trueConst();
	}
	def genClassIdQuery(oi: SsaApplyOp, low: int, high: int, nobj: SsaInstr) -> SsaInstr {
		var tid = ptrLoad(mach.tagType, nobj, 0);
		if (high == low + 1) {
			return apply(null, V3Op.newEqual(Int.TYPE), [tid, newRegion.intConst(low)]);
		} else {
			var cmp1 = curBlock.opIntGteq(tid, newRegion.intConst(low));
			var cmp2 = curBlock.opIntLt(tid, newRegion.intConst(high));
			return apply(null, V3Op.opBoolAnd, [cmp1, cmp2]);
		}
	}
	def genTypeSubsume(oi: SsaApplyOp) -> SsaInstr {
		return genRef1(oi.inputs(0)); // always a no-op at the machine level
	}
	def genArrayAlloc(oi: SsaApplyOp) -> SsaInstr {
		var olen = oi.inputs(0), arrayType = oi.op.typeArgs(0);
		var hsize = mach.getArrayElemOffset(arrayType), scale = mach.getArrayElemScale(arrayType);
		if (SsaValue.?(olen.dest)) {
			// length is known statically
			var len = Int.unbox(SsaValue.!(olen.dest).val);
			if (len < 0) return curBlock.addThrow(oi.source, V3Exception.LengthCheck);
			return genArrayAllocWithSize(oi.source, arrayType, hsize, len, scale);
		}
		var nlen = genRef1(olen);
		if (!oi.checkFact(Facts.O_NO_LENGTH_CHECK) && !context.compiler.DisableLengthChecks) {
			// add a check (length < 0)
			var check = curBlock.opIntLt(nlen, newRegion.zeroConst());
			apply(oi.source, V3Op.newConditionalThrow(V3Exception.LengthCheck), [check]);
		}
		var size: SsaInstr = newRegion.intConst(hsize);
		if (scale > 0) {
			// scale the length by the element scale
			var elemsize = nlen;
			if (scale > 1) elemsize = curBlock.opIntMul(nlen, newRegion.intConst(scale));
			if (scale != mach.data.align(scale)) {
				// alignment is necessary
				size = curBlock.opIntAdd(newRegion.intConst(hsize + mach.data.alignAdd), elemsize);
				size = curBlock.opIntAnd(newRegion.intConst(mach.data.alignMask), size);
			} else {
				size = curBlock.opIntAdd(size, elemsize);
			}
		}
		// allocate the array, store tag, and store length
		var narr = apply(oi.source, MachOp.newAlloc(mach.machType(arrayType)), [size]);
		storeObjectTag(narr, arrayType);
		ptrStore(Int.TYPE, narr, mach.getArrayLengthOffset(arrayType), nlen);
		return narr;
	}
	def genArrayAllocWithSize(source: Source, arrayType: Type, hsize: int, len: int, scale: int) -> SsaInstr {
		var totalSize = mach.data.align(hsize + len * scale);
		// allocate the array with the known size
		var narr = apply(source, MachOp.newAlloc(mach.machType(arrayType)), [newRegion.intConst(totalSize)]);
		storeObjectTag(narr, arrayType); // store tag
		ptrStore(Int.TYPE, narr, mach.getArrayLengthOffset(arrayType), newRegion.intConst(len)); // store length
		return narr;
	}
	def genArrayInit(oi: SsaApplyOp) -> SsaInstr {
		var arrayType = oi.op.typeArgs(0);
		var offset = mach.getArrayElemOffset(arrayType), scale = mach.getArrayElemScale(arrayType);
		var narr = genArrayAllocWithSize(oi.source, arrayType, offset, oi.inputs.length, scale);
		var elemType = V3Array.elementType(arrayType);
		for (i = 0; i < oi.inputs.length; i++) {
			// generate unchecked pointer stores to initialize the array
			var val = genRef1(oi.inputs(i));
			if (!SsaValue.?(val) || SsaValue.!(val).val != null) {
				// store only non-default values
				ptrStore(elemType, narr, offset, val);
			}
			offset = offset + scale;
		}
		return narr;
	}
	def genArrayGetElem(oi: SsaApplyOp) -> SsaInstr {
		genBoundsCheck(oi, false);
		var narr = genRef1(oi.inputs(0)), arrayType = oi.op.typeArgs(0);
		var hsize = mach.getArrayElemOffset(arrayType), scale = mach.getArrayElemScale(arrayType);
		var index = genRef1(oi.inputs(1));
		var offset = genArrayElemOffset(hsize, scale, index);
		// XXX: fold null check into pointer access if no bounds check
		return ptrLoad(mach.machType(oi.getType()), ptrAdd(narr, offset), 0);
	}
	def genArraySetElem(oi: SsaApplyOp) -> SsaInstr {
		genBoundsCheck(oi, false);
		var narr = genRef1(oi.inputs(0)), arrayType = oi.op.typeArgs(0);
		var hsize = mach.getArrayElemOffset(arrayType), scale = mach.getArrayElemScale(arrayType);
		var index = genRef1(oi.inputs(1));
		var offset = genArrayElemOffset(hsize, scale, index);
		var val = genRef1(oi.inputs(2));
		// XXX: fold null check into pointer access if no bounds check
		return ptrStore(mach.machType(oi.op.paramTypes(2)), ptrAdd(narr, offset), 0, val);
	}
	def genArrayElemOffset(headerSize: int, scale: int, index: SsaInstr) -> SsaInstr {
		if (SsaValue.?(index)) {
			// fold the offset calculation
			return newRegion.intConst(headerSize + scale * Int.unbox(SsaValue.!(index).val));
		} else {
			var offset = index;
			if (scale > 1) offset = curBlock.opIntMul(index, newRegion.intConst(scale));
			if (headerSize != 0) offset = curBlock.opIntAdd(offset, newRegion.intConst(headerSize));
			return offset;
		}
	}
	def genArrayGetLength(oi: SsaApplyOp) -> SsaInstr {
		var oarr = oi.inputs(0), narr = genRef1(oarr);
		return refLoad(Int.TYPE, oi, oarr, narr, mach.getArrayLengthOffset(oi.op.typeArgs(0)));
	}
	def genClassAlloc(oi: SsaApplyOp) -> SsaInstr {
		var classType = oi.getType();
		var size = mach.getObjectSize(classType, null);
		// allocate the object
		var nobj = apply(oi.source, MachOp.newAlloc(mach.machType(classType)), [newRegion.intConst(size)]);
		storeObjectTag(nobj, classType);
		var newRef = V3Op.extractIrSpec(oi.op);
		if (newRef.member != null) {
			// nontrivial constructor
			var constr = mach.getCodeAddress(newRef);
			var newType = mach.funcType(newRef);
			call(oi.source, true, newType, newRegion.valConst(newType, constr), Arrays.prepend(nobj, genRefs(oi.inputs)));
		}
		return nobj;
	}
	def genClassGetField(oi: SsaApplyOp) -> SsaInstr {
		var fieldRef = V3Op.extractIrSpec(oi.op);
		var oobj = oi.inputs(0), nobj = genRef1(oobj);
		var ftype = mach.machType(fieldRef.getFieldType());
		if (V3.isVariant(fieldRef.container)) {
			return genIfNull(oi, ftype, nobj, null, ptrLoad(ftype, _, mach.classFieldOffset(fieldRef)));
		}
		return refLoad(ftype, oi, oobj, nobj, mach.classFieldOffset(fieldRef));
	}
	def genClassSetField(oi: SsaApplyOp) -> SsaInstr {
		var fieldRef = V3Op.extractIrSpec(oi.op), nobj = genRef1(oi.inputs(0));
		var val = genRef1(oi.inputs(1)), ft = mach.machType(fieldRef.getFieldType());
		var offset = mach.classFieldOffset(fieldRef);
		if (V3Op.needsNullCheck(oi, nobj)) return ptrStoreT(oi.source, ft, nobj, offset, val);
		else return ptrStore(ft, nobj, offset, val);
	}
	def genClassGetMethod(oi: SsaApplyOp) -> SsaInstr {
		var methodRef = V3Op.extractIrSpec(oi.op), machType = mach.funcType(methodRef);
		return newRegion.valConst(machType, mach.getCodeAddress(methodRef));
	}
	def genClassGetVirtual(oi: SsaApplyOp) -> SsaInstr {
		var methodRef = V3Op.extractIrSpec(oi.op);
		var oobj = oi.inputs(0), nobj = genRef1(oobj);
		if (V3.isVariant(methodRef.container)) {
			var defm = mach.getCodeAddress(context.prog.ir.resolveVariantDefaultMethodImpl(methodRef));
			return genIfNull(oi, mach.funcType(methodRef), nobj, defm, genMtableLookup(oi, oobj, _, methodRef));
		}
		return genMtableLookup(oi, oobj, nobj, methodRef);
	}
	def genMtableLookup(oi: SsaApplyOp, oobj: SsaDfEdge, nobj: SsaInstr, methodRef: IrSpec) -> SsaInstr {
		// use method-table based dispatch
		var tid = refLoad(mach.tagType, oi, oobj, nobj, 0);
		var mtbl = newRegion.valConst(mach.data.ptrType, mach.methodTable(methodRef));
		return ptrLoad(mach.funcType(methodRef), ptrAdd(mtbl, tid), 0);
	}
	def genComponentGetField(oi: SsaApplyOp) -> SsaInstr {
		var fieldRef = V3Op.extractIrSpec(oi.op);
		return ptrLoad(mach.machType(fieldRef.getFieldType()), componentFieldPtr(fieldRef), 0);
	}
	def genComponentSetField(oi: SsaApplyOp) -> SsaInstr {
		var fieldRef = V3Op.extractIrSpec(oi.op);
		var val = genRef1(oi.inputs(1));
		return ptrStore(mach.machType(fieldRef.getFieldType()), componentFieldPtr(fieldRef), 0, val);
	}
	def genTupleCreate(oi: SsaApplyOp) -> SsaInstr {
		var ni = genRefs(oi.inputs);
		return apply(null, V3Op.newTupleCreate(mach.machType(oi.getType())), ni);
	}
	def genTupleGetElem(oi: SsaApplyOp) -> SsaInstr {
		var e = genRef1(oi.inputs(0)), index = oi.op.attr<int>();
		return apply(null, V3Op.newTupleGetElem(e.getType(), index), [e]);
	}
	def genNullCheck(oi: SsaApplyOp) -> SsaInstr {
		var oobj = oi.inputs(0), nobj = genRef1(oobj);
		if (context.compiler.DisableNullChecks) return nobj;
		if (SsaValue.?(nobj)) {
			var oval = SsaValue.!(nobj).val;
			if (oval == null) curBlock.addThrow(oi.source, V3Exception.NullCheck);
			return nobj; // no check necessary
		}
		if (V3Op.needsNullCheck(oi, oobj.dest)) {
			if (explicitNullChecks) return explicitNullCheck(oi.source, mach.machType(oobj.dest.getType()), nobj);
			else ptrLoadT(oi.source, Void.TYPE, nobj, 0);
		}
		return nobj;
	}
	def genBoundsCheck(oi: SsaApplyOp, nullCheck: bool) -> SsaInstr {
		var oarr = oi.inputs(0), narr = genRef1(oarr);
		if (context.compiler.DisableBoundsChecks || oi.checkFact(Facts.O_NO_BOUNDS_CHECK)) {
			return if(nullCheck, genNullCheck(oi), newRegion.nullConst(Void.TYPE));
		}
		// load length
		// XXX: CSE the array length if possible
		var len = refLoad(Int.TYPE, oi, oarr, narr, mach.getArrayLengthOffset(oi.op.typeArgs(0)));
		var index = genRef1(oi.inputs(1));
		var throwOp = V3Op.newConditionalThrow(V3Exception.BoundsCheck);
		// throw BoundsCheckException if ugteq(index, length)
		apply(oi.source, throwOp, [apply(null, MachOp.opIntUgteq, [index, len])]);
		return index;
	}
	def genCallMethod(oi: SsaApplyOp) -> SsaInstr {
		var methodRef = V3Op.extractIrSpec(oi.op);
		if (V3.isClass(methodRef.container)) genNullCheck(oi);
		var addr = mach.getCodeAddress(methodRef);
		var ftype = mach.funcType(methodRef);
		var receiver = !V3.isComponent(methodRef.container);
		return call(oi.source, receiver, ftype, newRegion.valConst(ftype, addr), genRefs(oi.inputs));
	}
	def genCallClassVirtual(oi: SsaApplyOp) -> SsaInstr {
		var methodRef = V3Op.extractIrSpec(oi.op);
		var addr = genClassGetVirtual(oi);
		var ftype = mach.funcType(methodRef);
		return call(oi.source, true, ftype, addr, genRefs(oi.inputs));
	}
	def genVariantGetTag(oi: SsaApplyOp, nobj: SsaInstr) -> SsaInstr {
		var val = ptrLoad(mach.tagType, nobj, 0);
		var min = mach.classIdRange(oi.op.typeArgs(0)).0;
		if (min != 0) {
			var sub = V3.asInt(mach.tagType).lookupInfix(V3Infix.Sub);
			val = apply(oi.source, sub, [val, newRegion.intConst(min)]);
		}
		var shift = IntOp.log(mach.code.addressSize);
		if (shift > 0) {
			var shr = V3.asInt(mach.tagType).lookupInfix(V3Infix.Shr);
			val = apply(null, shr, [val, newRegion.intConst(shift)]);
		}
		var conv = V3Op.newIntConvert(mach.tagType, oi.op.resultType);
		return apply(null, conv, [val]);
	}
	def genConditionalThrow(oi: SsaApplyOp) -> SsaInstr {
		return apply(oi.source, oi.op, genRefs(oi.inputs));
	}
	def genType1(t: Type) -> Type {
		return mach.machType(t);
	}
	def genVal1(v: Val, t: Type) -> Val {
		return mach.machVal(v, t);
	}

	def explicitNullCheck(source: Source, t: Type, nobj: SsaInstr) -> SsaInstr {
		if (context.compiler.DisableNullChecks) return nobj;
		var check = apply(null, V3Op.newEqual(t), [nobj, newRegion.nullConst(t)]);
		apply(source, V3Op.newConditionalThrow(V3Exception.NullCheck), [check]);
		return nobj;
	}
	def storeObjectTag(nobj: SsaInstr, t: Type) {
		var tag = newRegion.valConst(mach.tagType, mach.objectTag(t));
		ptrStore(mach.tagType, nobj, 0, tag);
	}
	def genIfNull(oi: SsaApplyOp, resultType: Type, nobj: SsaInstr, nullVal: Val, gen: SsaInstr -> SsaInstr) -> SsaInstr {
		if (nobj.checkFact(Facts.V_ZERO)) {
			return newRegion.valConst(resultType, nullVal);
		} else if (V3Op.needsNullCheck(oi, nobj)) {
			var tblock = newBlock(), fblock = newBlock(), merge = newBlock();
			curBlock.addIfNull(nobj, tblock.block, fblock.block);
			curBlock = fblock;
			// if (nobj != null) generate the nonnull case
			var nonNull = gen(nobj);
			fblock.addGoto(merge.block);
			tblock.addGoto(merge.block);
			curBlock = merge;
			if (blockEndMap != null) blockEndMap.set(context.block, curBlock.block);
			// if (nobj == null) use the null value
			return curBlock.addPhi(resultType, [newRegion.valConst(resultType, nullVal), nonNull]);
		} else {
			return gen(nobj);
		}
	}

	def call(source: Source, receiver: bool, ftype: Type, func: SsaInstr, args: Array<SsaInstr>) -> SsaInstr {
		if (func != null) args = Arrays.prepend(func, args);
		return curBlock.addApply(source, MachOp.newCallAddress(receiver, ftype, ftype.nested.head), args);
	}
	def apply(source: Source, op: Operator, args: Array<SsaInstr>) -> SsaInstr {
		return curBlock.addApply(source, op, args);
	}
	def ptrAdd(p: SsaInstr, offset: SsaInstr) -> SsaInstr {
		var pt = p.getType(), pd = pt; // TODO: derived type
		if (SsaValue.?(p) && SsaValue.?(offset)) {
			// XXX: fold address calculation in optimizer, not here
			var addr = Addr.!(SsaValue.!(p).val);
			var offset = Int.unbox(SsaValue.!(offset).val);
			if (addr != null) {
				var addr = newRegion.valConst(pd, addr.add(offset));
				addr.setFact(Facts.V_NON_ZERO);
				return addr;
			}
		}
		return apply(null, MachOp.newPtrAdd(pt, pd), [p, offset]); 
	}
	def ptrLoad(vt: Type, p: SsaInstr, offset: int) -> SsaInstr {
		if (offset != 0) p = ptrAdd(p, newRegion.intConst(offset));
		var i = apply(null, MachOp.newPtrLoad(p.getType(), vt), [p]);
		i.setFact(Facts.O_NO_NULL_CHECK); // this load won't trap
		return i;
	}
	def ptrStore(vt: Type, p: SsaInstr, offset: int, v: SsaInstr) -> SsaInstr {
		if (offset != 0) p = ptrAdd(p, newRegion.intConst(offset));
		var i = apply(null, MachOp.newPtrStore(p.getType(), vt), [p, v]);
		i.setFact(Facts.O_NO_NULL_CHECK); // this store won't trap
		return i;
	}
	def ptrLoadT(source: Source, t: Type, p: SsaInstr, offset: int) -> SsaInstr {
		if (offset != 0) p = ptrAdd(p, newRegion.intConst(offset));
		return apply(source, MachOp.newPtrLoad(p.getType(), t), [p]); // this load may trap
	}
	def ptrStoreT(source: Source, t: Type, p: SsaInstr, offset: int, v: SsaInstr) -> SsaInstr {
		if (offset != 0) p = ptrAdd(p, newRegion.intConst(offset));
		return apply(source, MachOp.newPtrStore(p.getType(), t), [p, v]); // this store may trap
	}
	def refLoad(vt: Type, oi: SsaApplyOp, oobj: SsaDfEdge, nobj: SsaInstr, offset: int) -> SsaInstr {
		if (V3Op.needsNullCheck(oi, oobj.dest)) {
			if (explicitNullChecks) explicitNullCheck(oi.source, mach.machType(oobj.dest.getType()), nobj);
			else return ptrLoadT(oi.source, vt, nobj, offset);
		}
		return ptrLoad(vt, nobj, offset);
	}
	def componentFieldPtr(f: IrSpec) -> SsaInstr {
		return newRegion.valConst(mach.data.ptrType, mach.componentFieldPtr(f));
	}
	def newBlock() -> V3SsaBuilder {
		return V3SsaBuilder.new(context, newRegion, SsaBlock.new());
	}
}
