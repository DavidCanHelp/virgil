// Copyright 2021 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def SPACE = AddressSpace.new("mem", false, 64, 8,
	Alignment.new(4096), Alignment.new(8));
def X86_64_LINUX_TARGET = Aeneas.registerTarget(
		LinuxTarget.new("x86-64-linux", SPACE, IntNormalizers.I64LE,
			X86_64LinuxBackend.new(_, _, _, _, false), ElfConst.EM_X86_64));
def X86_64_LINUX_TEST_TARGET = Aeneas.registerTarget(
		LinuxTarget.new("x86-64-linux-test", SPACE, IntNormalizers.I64LE,
			X86_64LinuxBackend.new(_, _, _, _, true), ElfConst.EM_X86_64));

def Regs: X86_64Regs;

// Provides the x86-64-specific logic for generating a Linux binary.
class X86_64LinuxBackend extends X86_64Backend {
	new(compiler: Compiler, prog: Program, mach: MachProgram, w: MachDataWriter,
		test: bool) super(compiler, prog, mach, w, test) { }
	def asm_exit_r(r: X86_64Gpr) {
		asm.movq_r_r(Regs.RDI, r);
		asm.movq_r_i(Regs.RAX, 60);
		asm.syscall();
	}
	def asm_exit_code(code: int) {
		asm.movq_r_i(Regs.RAX, 60);
		asm.movq_r_i(Regs.RDI, code);
		asm.syscall();
	}
	def genTestOutput(frame: MachFrame) { // TODO(x86-64-linux)
		// output the return register as a 4-byte integer to stdout
		asm.pushq_r(loc_gpr(frame, frame.conv.callerRet(0)));
		asm.movd_r_i(Regs.RAX, 1);		// sys_write() = 1
		asm.movd_r_i(Regs.RDI, 1);		// stdout
		asm.movq_r_r(Regs.RSI, Regs.RSP);	// integer is on stack
		asm.movd_r_i(Regs.RDX, 4);		// size to write
		asm.syscall();
	}
	def genSignalHandlerStub() { // TODO(x86-64-linux)
	}
	def genFatalStub(ex: string, addr: Addr) { // TODO(x86-64-linux)
		// 1. encode a call to write() on stderr
		var msgAddr = Addr.new(mach.codeRegion, null, 0);
		asm.movq_r_i(Regs.RAX, 1);			// sys_write() = 1
		asm.movq_r_i(Regs.RDI, 2);			// stderr
		asm.leaq(Regs.RSI, X86_64AddrRef.new(null, null, 1, msgAddr));	// buffer
		asm.movq_r_i(Regs.RDX, ex.length + 1);		// write size
		asm.syscall();
		// 2. encode a call to exit()
		asm_exit_code(-1);
		// 3. output message at end of code
		msgAddr.absolute = w.endAddr();
		w.putb('!');
		w.puta(ex);
		w.atEnd();
	}
	def genSigHandlerInstall(signo: int, handler: Addr) { // TODO(x86-64-linux)
	}
}
