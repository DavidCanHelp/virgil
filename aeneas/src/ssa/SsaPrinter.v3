// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Utility to print SSA code.
class SsaPrinter {
	def buf = TerminalBuffer.new();

	def printGraph(phase: string, render: StringBuffer -> StringBuffer, graph: SsaGraph, sig: Signature) {
		if (phase != null) render(buf.puts(phase).puts(" SSA for: "));
		if (sig != null) {
			buf.sp();
			printTypeArray(sig.paramTypes);
			buf.puts(" -> ");
			printTypeArray(sig.returnTypes);
			buf.ln();
		}
		if (graph == null) {
			buf.puts("\t\t<null>\n");
		} else {
			printParams(graph.params);
			printBlocks(graph.bfBlocks());
		}
		flush();
	}
	def printTypeArray(a: Array<Type>) {
		buf.putc('[');
		for (i < a.length) {
			if (i > 0) buf.csp();
			buf.put1(a[i].render);
		}
		buf.putc(']');
	}
	def printParams(params: Array<SsaParam>) {
		indent(1);
		buf.puts("param ");
		if (params == null) {
			buf.red();
			buf.puts("<null>");
			buf.end();
		} else {
			for (p in params) {
				cyan("$%1", p.index);
				cyan("@%1", p.uid);
				printColonType(p.getType());
				if (printFacts(p.facts)) buf.sp();
			}
		}
		ln();
	}
	def printStats(phase: string, render: StringBuffer -> StringBuffer, graph: SsaGraph) {
		if (phase != null) render(buf.puts(phase).puts(" SSA stats for: "));
		var blocks = graph.bfBlocks();
		buf.format1(" -- %1 blocks", blocks.length);
		for (b in blocks) {
			buf.format1(" %1", b.count());
		}
		ln();
		flush();
	}
	def printBlocks(blocks: Array<SsaBlock>) {
		for (b in blocks) printBlock(b);
		for (b in blocks) b.clearMark();
	}
	def printBlockHeader(block: SsaBlock) {
		indent(1);
		buf.puts("block ");
		putid(block.uid);
		for (e in block.preds) printCfEdge(e);
		ln();
	}
	def printBlock(block: SsaBlock) {
		printBlockHeader(block);
		for (i = block.next; i != null && i != block; i = i.next) {
			if (SsaEnd.?(i)) printEnd(block, SsaEnd.!(i));
			else if (SsaInstr.?(i)) printInstrLn(SsaInstr.!(i));
		}
	}
	def printCfEdge(e: SsaCfEdge) {
		buf.sp();
		if (e == null) {
			buf.red().puts("<null>").end();
		} else {
			putid(e.src.uid);
			buf.puts("->");
			printDest(e);
		}
	}
	def printInstr(i: SsaInstr) {
		if (i == null) return putnull();
		cyan("@%1", i.uid);
		buf.putc(':');

		match (i) {
			x: SsaPhi => buf.puts("phi    ");
			x: SsaReturn => buf.puts("ret    ");
			x: SsaIf => buf.puts("if     ");
			x: SsaGoto => buf.puts("goto   ");
			x: SsaSwitch => buf.puts("switch ");
			x: SsaParam => {
				buf.puts("param  ");
				buf.green().puti(x.index).end();
				printColonType(i.getType());
			}
			x: SsaApplyOp => {
				V3Op.renderInto(x.op, buf);
				printColonType(i.getType());
			}
			x: SsaConst => {
				buf.puts("value  ");
				buf.green();
				V3.renderResult(x.val, i.getType(), buf);
				buf.end();
				printColonType(i.getType());
			}
			x: SsaThrow => {
				buf.puts("throw  ");
				buf.green().puts(x.exception).sp().end();
			}
		}
		var args = i.inputs;
		if (args.length > 0) buf.putc('(');
		for (j < args.length) {
			if (j > 0) buf.csp();
			printRef(args[j]);
		}
		if (args.length > 0) buf.putc(')');
		buf.sp();
		printFacts(i.facts);
	}
	def printInstrLn(i: SsaInstr) {
		indent(2);
		printInstr(i);
		ln();
	}
	def printEnd(block: SsaBlock, end: SsaEnd) {
		printInstrLn(end);
		var succ = end.succs;
		match (end) {
			x: SsaIf => {
				indent(3);
				buf.puts(" true  => ");
				if (succ.length > 0) {
					printDest(succ[0]);
					ln();
				} else {
					buf.red().puts("error").end();
					return ln();
				}
				indent(3);
				buf.puts(" false => ");
				if (succ.length > 1) {
					printDest(succ[1]);
				} else {
					buf.red().puts("error").end();
				}
				ln();
			}
			x: SsaGoto => {
				indent(3);
				buf.puts("=> ");
				printDest(succ[0]);
				ln();
			}
			x: SsaSwitch => {
				var x = SsaSwitch.!(end);
				for (i < succ.length) {
					indent(3);
					var v = i + x.minValue;
					if (v <= x.maxValue) buf.puti(v);
					else buf.puts("_");
					buf.puts(" => ");
					printDest(succ[i]);
					ln();
				}
			}
		}
	}
	def printColonType(t: Type) {
		buf.puts(": ");
		printType(t);
	}
	def printType(t: Type) {
		if (t == null) {
			buf.red();
			buf.puts("<null>");
		} else {
			buf.yellow();
			buf.put1(t.render);
		}
		buf.end().sp();
	}
	def printRef(i: SsaDfEdge) {
		if (i == null || i.dest == null) {
			putnull();
		} else if (SsaConst.?(i.dest)) {
			buf.green().putc('#');
			var const = SsaConst.!(i.dest);
			V3.renderResult(const.val, null, buf);
			buf.putc(':');
			const.vtype.render(buf);
			buf.end();
			cyan("@%1", const.uid);
		} else if (SsaParam.?(i.dest)) {
			var p = SsaParam.!(i.dest);
			cyan("$%1", p.index);
			cyan("@%1", p.uid);
		} else {
			cyan("@%1", i.dest.uid);
		}
	}
	def indent(i: int) {
		while (i-- > 0) buf.tab();
	}
	def printFacts(facts: Fact.set) -> bool {
		if (facts != Facts.NONE) {
			buf.putc('[').puts(Terminal.CTRL_YELLOW);
			Facts.renderFacts(buf, facts);
			buf.puts(Terminal.CTRL_DEFAULT).putc(']');
			return true;
		}
		return false;
	}
	def printDest(edge: SsaCfEdge) {
		if (edge.dest == null) putnull();
		else putid(edge.dest.uid);
	}
	def putid(id: int) {
		buf.blue().putc('#').puti(id).end();
	}
	def cyan(fmt: string, a: int) {
		buf.cyan().format1(fmt, a).end();
	}
	def putnull() {
		buf.red().puts("null").end();
	}
	def ln() {
		buf.ln();
		if (buf.length > 1000) flush();
	}
	def flush() -> this {
		if (buf.length > 0) {
			Terminal.putb(buf);
			buf.reset();
		}
	}
	def printChain(start: SsaLink) -> this {
		var prev: SsaLink;
		var l = start, first = true;
		if (l.prev != null) {
			var p = l.prev;
			if (p == start) buf.puts("* <- ");
			else buf.green().format1("#%1", p.uid).end().puts(" <- ");
		}
		buf.puts("*");
		while (true) {
			if (l == null) break;
			if (prev != null) {
				if (l.prev == prev) buf.puts(" <-> ");
				else buf.puts(" -> ");
			}
			buf.green();
			if (SsaBlock.?(l)) buf.putc('B');
			if (SsaEnd.?(l)) buf.putc('E');
			buf.format1("#%1", l.uid);
			buf.end();
			prev = l;
			l = l.next;
			if (l == start) { buf.puts(" ->*"); break; }
		}
		buf.ln();
	}
}
