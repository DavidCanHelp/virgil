// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Utility to print SSA code.
// XXX: use StringBuffer internally instead of directly writing to Terminal
class SsaPrinter {
	def prog: Program;
	def buf = StringBuffer.new();
	new(prog) { }
	def printRegion(region: SsaRegion) {
		printBlocks(region.bfBlocks());
		flush();
	}
	def printBlocks(blocks: Array<SsaBlock>) {
		for (b in blocks) printBlock(b);
		for (b in blocks) b.clearMark();
	}
	def printBlockHeader(block: SsaBlock) {
		indent(1);
		buf.puts("block ");
		putid(block.uid);
		if (block.phis != null) {
			block.phis.phiEdges.apply(printCfEdge);
		}
		ln();
	}
	def printBlock(block: SsaBlock) {
		printBlockHeader(block);
		if (block.phis != null) block.phis.phiInstrs.apply(printInstrLn);
		block.instrs.apply(printInstrLn);
		if (block.end != null) printEnd(block.end);
	}
	def printCfEdge(e: SsaCfEdge) {
		buf.putc(' ');
		putid(e.src.uid);
		buf.puts("->");
		printDest(e);
	}
	def printInstr(i: SsaInstr) {
		if (i == null) return putnull();
		cyan("@%1", i.uid);
		var name = "<unknown>";
		var extra: string;
		
		if (SsaParam.?(i)) {
			name = "param ";
			extra = StringBuffer.new().puti(SsaParam.!(i).index).toString();
		} else if (SsaApplyOp.?(i)) {
			name = "apply ";
			extra = V3Op.renderOp(SsaApplyOp.!(i).op);
		} else if (SsaValue.?(i)) {
			name = "value ";
			extra = prog.render(SsaValue.!(i).val);
		} else if (SsaPhi.?(i)) {
			name = "phi   ";
		} else if (SsaThrow.?(i)) {
			name = "throw ";
			extra = SsaThrow.!(i).exception;
		} else if (SsaReturn.?(i)) {
			name = "ret   ";
		} else if (SsaSwitch.?(i)) {
			name = "sw    ";
		} else if (SsaGoto.?(i)) {
			name = "goto  ";
		}
		buf.putc(':');
		buf.puts(name);
		if (extra != null) green("[%1] ", extra);
		var args = i.inputs;
		for (j = 0; j < args.length; j++) {
			if (j > 0) buf.puts(", ");
			printRef(args(j));
		}
		buf.putc(' ');
		printFacts(i.facts);
	}
	def printInstrLn(i: SsaInstr) {
		indent(2);
		printInstr(i);
		ln();
	}
	def printEnd(end: SsaEnd) {
		printInstrLn(end);
		var succ = end.block.succ;
		if (SsaSwitch.?(end)) {
			var sw = SsaSwitch.!(end);
			for (i = 0; i < sw.vals.length; i++) {
				indent(3);
				prog.renderResult(sw.vals(i), null, buf);
				buf.puts(" => ");
				printDest(succ(i));
				ln();
			}
			indent(3);
			buf.puts("=> ");
			printDest(succ(sw.vals.length));
			ln();
		} else if (SsaGoto.?(end)) {
			indent(3);
			buf.puts("=> ");
			printDest(succ(0));
			ln();
		}
	}
	def printRef(i: SsaDfEdge) {
		if (i == null || i.dest == null) {
			putnull();
		} else if (SsaValue.?(i.dest)) {
			buf.puts(Terminal.CTRL_GREEN);
			buf.putc('#');
			prog.renderResult(SsaValue.!(i.dest).val, null, buf);
			buf.puts(Terminal.CTRL_DEFAULT);
		} else if (SsaParam.?(i.dest)) {
			cyan("$%1", SsaParam.!(i.dest).index);
		} else {
			cyan("@%1", i.dest.uid);
		}
	}
	def indent(i: int) {
		while (i-- >= 0) buf.putc('\t');
	}
	def printFacts(facts: int) {
		if (facts != 0) {
			buf.putc('[').puts(Terminal.CTRL_YELLOW);
			Facts.renderFacts(buf, facts);
			buf.puts(Terminal.CTRL_DEFAULT).putc(']');
		}
	}
	def printDest(edge: SsaCfEdge) {
		if (edge.dest == null) putnull();
		else putid(edge.dest.uid);
	}
	def putid(id: int) {
		buf.puts(Terminal.CTRL_BLUE);
		buf.putc('#');
		buf.puti(id);
		buf.puts(Terminal.CTRL_DEFAULT);
	}
	def cyan(fmt: string, a: int) {
		buf.puts(Terminal.CTRL_CYAN);
		buf.format1(fmt, a);
		buf.puts(Terminal.CTRL_DEFAULT);
	}
	def green<T>(fmt: string, a: T) {
		buf.puts(Terminal.CTRL_GREEN);
		buf.format1(fmt, a);
		buf.puts(Terminal.CTRL_DEFAULT);
	}
	def putnull() {
		buf.puts(Terminal.CTRL_RED);
		buf.puts("null");
		buf.puts(Terminal.CTRL_DEFAULT);
	}
	def ln() {
		buf.putc('\n');
		if (buf.length > 1000) {
			// for performance, buffer the output up to 1000 characters
			Terminal.putb(buf);
			buf.reset();
		}
	}
	def flush() {
		if (buf.length > 0) Terminal.putb(buf);
	}
}
