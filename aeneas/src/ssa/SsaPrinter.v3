// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Utility to print SSA code.
class SsaPrinter(prog: Program) {
	def buf = TerminalBuffer.new();

	def printGraph(phase: string, render: StringBuffer -> StringBuffer, graph: SsaGraph) {
		if (phase != null) render(buf.puts(phase).puts(" SSA for: ")).ln();
		if (graph == null) buf.puts("\t\t<null>\n");
		else printBlocks(graph.bfBlocks());
		flush();
	}
	def printStats(phase: string, render: StringBuffer -> StringBuffer, graph: SsaGraph) {
		if (phase != null) render(buf.puts(phase).puts(" SSA stats for: "));
		var blocks = graph.bfBlocks();
		buf.format1(" -- %1 blocks", blocks.length);
		for (b in blocks) {
			buf.format1(" %1", b.count());
		}
		ln();
		flush();
	}
	def printBlocks(blocks: Array<SsaBlock>) {
		for (b in blocks) printBlock(b);
		for (b in blocks) b.clearMark();
	}
	def printBlockHeader(block: SsaBlock) {
		indent(1);
		buf.puts("block ");
		putid(block.uid);
		if (block.phis != null) {
			block.phis.phiEdges.apply(printCfEdge);
		}
		ln();
	}
	def printBlock(block: SsaBlock) {
		printBlockHeader(block);
		if (block.phis != null) block.phis.phiInstrs.apply(printInstrLn);
		for (i = block.next; i != block; i = i.next) {
			if (SsaEnd.?(i)) printEnd(SsaEnd.!(i));
			else if (SsaInstr.?(i)) printInstrLn(SsaInstr.!(i));
		}
	}
	def printCfEdge(e: SsaCfEdge) {
		buf.sp();
		putid(e.src.uid);
		buf.puts("->");
		printDest(e);
	}
	def printInstr(i: SsaInstr) {
		if (i == null) return putnull();
		cyan("@%1", i.uid);
		buf.putc(':');
		
		// XXX: clean this up when typecase is available
		if (SsaParam.?(i)) {
			buf.puts("param  ");
			buf.green().puti(SsaParam.!(i).index).end();
			buf.puts(": ");
			buf.yellow();
			buf.put1(i.getType().render).end().sp();
		} else if (SsaApplyOp.?(i)) {
			V3Op.renderInto(SsaApplyOp.!(i).op, buf);
			buf.puts(": ");
			var t = i.getType();
			if (t == null) {
				buf.red();
				buf.puts("<null>");
			} else {
				buf.yellow();
				buf.put1(i.getType().render);
			}
			buf.end().sp();
		} else if (SsaValue.?(i)) {
			buf.puts("value  ");
			buf.green();
			prog.renderResult(SsaValue.!(i).val, i.getType(), buf);
			buf.end();
			buf.puts(": ");
			buf.yellow();
			buf.put1(i.getType().render).end().sp();
		} else if (SsaPhi.?(i)) {
			buf.puts("phi    ");
		} else if (SsaReturn.?(i)) {
			buf.puts("ret    ");
		} else if (SsaIf.?(i)) {
			buf.puts("if     ");
		} else if (SsaGoto.?(i)) {
			buf.puts("goto   ");
		} else if (SsaSwitch.?(i)) {
			buf.puts("switch ");
		} else if (SsaThrow.?(i)) {
			buf.puts("throw  ");
			buf.green().puts(SsaThrow.!(i).exception).sp().end();
		}
		var args = i.inputs;
		for (j < args.length) {
			if (j > 0) buf.puts(", ");
			printRef(args(j));
		}
		buf.sp();
		printFacts(i.facts);
	}
	def printInstrLn(i: SsaInstr) {
		indent(2);
		printInstr(i);
		ln();
	}
	def printEnd(end: SsaEnd) {
		printInstrLn(end);
		var succ = end.block.succ;
		if (SsaIf.?(end)) {
			indent(3);
			buf.puts(" true  => ");
			printDest(succ(0));
			ln();
			indent(3);
			buf.puts(" false => ");
			printDest(succ(1));
			ln();
		} else if (SsaGoto.?(end)) {
			indent(3);
			buf.puts("=> ");
			printDest(succ(0));
			ln();
		} else if (SsaSwitch.?(end)) {
			var sw = SsaSwitch.!(end);
			for (i < succ.length) {
				indent(3);
				var v = i + sw.minValue;
				if (v <= sw.maxValue) buf.puti(v);
				else buf.puts("_");
				buf.puts(" => ");
				printDest(succ(i));
				ln();
			}
		}
	}
	def printRef(i: SsaDfEdge) {
		if (i == null || i.dest == null) {
			putnull();
		} else if (SsaValue.?(i.dest)) {
			buf.green().putc('#');
			prog.renderResult(SsaValue.!(i.dest).val, null, buf);
			buf.end();
		} else if (SsaParam.?(i.dest)) {
			cyan("$%1", SsaParam.!(i.dest).index);
		} else {
			cyan("@%1", i.dest.uid);
		}
	}
	def indent(i: int) {
		while (i-- >= 0) buf.putc('\t');
	}
	def printFacts(facts: int) {
		if (facts != 0) {
			buf.putc('[').puts(Terminal.CTRL_YELLOW);
			Facts.renderFacts(buf, facts);
			buf.puts(Terminal.CTRL_DEFAULT).putc(']');
		}
	}
	def printDest(edge: SsaCfEdge) {
		if (edge.dest == null) putnull();
		else putid(edge.dest.uid);
	}
	def putid(id: int) {
		buf.blue().putc('#').puti(id).end();
	}
	def cyan(fmt: string, a: int) {
		buf.cyan().format1(fmt, a).end();
	}
	def putnull() {
		buf.red().puts("null").end();
	}
	def ln() {
		buf.ln();
		if (buf.length > 1000) {
			// for performance, buffer the output up to 1000 characters
			Terminal.putb(buf);
			buf.reset();
		}
	}
	def flush() {
		if (buf.length > 0) Terminal.putb(buf);
	}
}
