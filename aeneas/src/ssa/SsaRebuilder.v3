// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Shared functionality for building new SSA code from old SSA code, e.g.
// during normalization or lowering.
class SsaRebuilder(context: SsaContext) {
	def instrMap: SsaMap = SsaMap.new();
	var newGraph: SsaGraph;
	var multiMap: PartialMap<SsaInstr, Array<SsaInstr>>;
	var blockStartMap: PartialMap<SsaBlock, SsaBlock>;
	var blockEndMap: PartialMap<SsaBlock, SsaBlock>;
	var curBlock: SsaBuilder;
	var edgeMapMap: PartialMap<SsaBlock, Array<int>>;

	// Stack for processing phis and blocks. Order doesn't really matter as long as a block's
	// dominators are processed before the block. Using a queue yields breadth-first order.
	var phiQueue: Vector<SsaPhi>;
	var blockQueue: Vector<(SsaBlock, SsaBlock)>;

	def genGraph() -> SsaGraph {
		if (newGraph != null) return newGraph;
		var rt = context.graph.returnType;
		var tn = normalize(rt);
		if (tn != null) rt = tn.newType;
		newGraph = SsaGraph.new(genParams(), rt);
		var oldStart = context.graph.startBlock;
		if (oldStart.succ.length == 0) {
			// common case of a single block that ends in return or throw
			// no need for blockmaps, queueing, or phi handling
			genBlock(oldStart, newGraph.startBlock);
		} else {
			// a method with multiple blocks
			genMultiBlock(oldStart, newGraph.startBlock);
		}
		instrMap.clear();
		return newGraph;
	}
	def genMultiBlock(oldStart: SsaBlock, newStart: SsaBlock) {
		blockStartMap = Ssa.newBlockMap();
		blockStartMap.set(oldStart, newStart);
		blockEndMap = Ssa.newBlockMap();
		blockEndMap.set(oldStart, newStart);
		phiQueue = Vector.new();
		blockQueue = Vector.new();
		blockQueue.add(oldStart, newStart);
		for (i < blockQueue.length) genBlock(blockQueue.array(i));
		for (i < phiQueue.length) finishPhi(phiQueue.array(i));
	}
	def genParams() -> Array<SsaParam> {
		// translate parameters and allocate new graph
		var params = Vector<SsaParam>.new().grow(context.graph.params.length);
		for (p in context.graph.params) genParam(p, params);
		return params.extract();
	}
	private def genBlock(ob: SsaBlock, nb: SsaBlock) {
		context.block = ob;
		this.curBlock = SsaBuilder.new(context, newGraph, nb);
		for (i = ob.next; i != ob && !curBlock.end; i = i.next) {
			// translate each instruction in the block
			if (SsaApplyOp.?(i)) genApplyOp(SsaApplyOp.!(i));
			else if (SsaReturn.?(i)) genReturn(SsaReturn.!(i));
			else if (SsaIf.?(i)) genIf(ob, SsaIf.!(i));
			else if (SsaGoto.?(i)) genGoto(SsaGoto.!(i));
			else if (SsaSwitch.?(i)) genSwitch(ob, SsaSwitch.!(i));
			else if (SsaThrow.?(i)) genThrow(SsaThrow.!(i));
			else return context.fail("unexpected block end");
		}
	}
	private def finishPhi(oi: SsaPhi) {
		// XXX: if only one predecessor, replace phi with its (one) input
		var ob = oi.block, nb = mapBlockStart(ob);
		var edgeMap = getEdgeMap(oi, ob, nb);
		var ni = SsaPhi.!(instrMap.get(oi));
		if (ni != null) {
			// phi was mapped one-to-one; map the new inputs
			var newInputs = Array<SsaInstr>.new(nb.phis.phiEdges.length);
			var facts = 0;
			for (j < newInputs.length) {
				var nii = genRef1(oi.inputs(edgeMap(j)));
				newInputs(j) = nii;
				if (j == 0) facts = nii.facts;
				else facts = facts & nii.facts;
			}
			ni.setInputs(newInputs);
			ni.setFact(facts);
			nb.phis.phiInstrs.add(ni);
			return;
		}
		// phi mapped to multiple phis
		var mi = multiMap.get(oi);
		for (w < mi.length) {
			var wi = SsaPhi.!(mi(w));
			var newInputs = Array<SsaInstr>.new(nb.phis.phiEdges.length);
			for (j < newInputs.length) {
				// XXX: interchange these loops for better performance
				var oii = oi.inputs(edgeMap(j));
				newInputs(j) = genRefs([oii])(w);
			}
			wi.setInputs(newInputs);
			nb.phis.phiInstrs.add(wi);
		}
	}
	private def getEdgeMap(oi: SsaPhi, ob: SsaBlock, nb: SsaBlock) -> Array<int> {
		if (nb.phis != null) return edgeMapMap.get(nb);
		// map new input edges [e0, e1, e2] to old phi input indexes [i0, i2, iX]
		nb.phis = SsaPhis.new(nb);
		var newPreds = incomingPreds(nb);
		var oldEdges = ob.phis.phiEdges;
		var map = Array<int>.new(newPreds.length);
		for (i < oldEdges.length) {
			var newPred = mapBlockEnd(oldEdges.get(i).src);
			for (j < newPreds.length) {
				if (newPred == newPreds.get(j)) map(j) = i;
			}
		}
		if (edgeMapMap == null) edgeMapMap = Ssa.newBlockMap();
		edgeMapMap.set(nb, map);
		return map;
	}
	private def incomingPreds(nb: SsaBlock) -> Vector<(SsaBlock)> {
		var newEdges = nb.phis.phiEdges;
		var newPreds = Vector<SsaBlock>.new();
		var inEdge = nb.predList;

		while (inEdge != null) {
			var next = SsaCfEdge.!(inEdge.next);
			if (inEdge.src.succ.length > 1) {
				// split critical edge between src & dest
				newEdges.add(splitEdge(inEdge));
				newPreds.add(inEdge.src);
			} else {
				// no edges to split
				newEdges.add(inEdge);
				newPreds.add(inEdge.src);
			}
			inEdge = next;
		}
		return newPreds;
	}
	private def splitEdge(inEdge: SsaCfEdge) -> SsaCfEdge {
		// split the edge between src and dest by routing through a new block
		var newBlock = SsaBlock.new();
		SsaBuilder.new(context, newGraph, newBlock).addGoto(inEdge.dest);
		inEdge.update(newBlock);
		return newBlock.succ(0);
	}
	def genParam(oldParam: SsaParam, params: Vector<SsaParam>) {
		var tn = normalize(oldParam.vtype);
		if (tn == null) {
			var np = SsaParam.new(params.length, oldParam.vtype);
			params.add(np);
			return map1(oldParam, np);
		}
		if (params.length == 0 || tn.size == 1) {
			var np = SsaParam.new(params.length, tn.newType);
			params.add(np);
			if (tn.size == 1) map1(oldParam, np);
			else map0(oldParam);
			return;
		}
		// one-zero or one-many mapping
		var newParams = Array<SsaInstr>.new(tn.size);
		for (j < newParams.length) {
			var np = SsaParam.new(params.length, tn.sub(j));
			newParams(j) = np;
			params.add(np);
		}
		mapN(oldParam, newParams);
	}
	def genApplyOp(oi: SsaApplyOp);

	def genIf(ob: SsaBlock, oi: SsaIf) {
		var key = genRef1(oi.inputs(0)), succ = ob.succ;
		if (SsaValue.?(key)) {
			// fold the branch
			var succ = mapEdge(succ(if(key.unbox<bool>(), 0, 1)));
			return curBlock.addGoto(succ);
		}
		curBlock.addIf(key, mapEdge(succ(0)), mapEdge(succ(1)));
	}
	def genSwitch(ob: SsaBlock, oi: SsaSwitch) {
		if (curBlock.end) return;
		var key = genRef1(oi.inputs(0));
		if (SsaValue.?(key)) {
			// fold the switch
			var succ = mapEdge(oi.findSucc(SsaValue.!(key).val));
			return curBlock.addGoto(succ);
		}
		curBlock.end = true;
		curBlock.block.append(SsaSwitch.new(oi.keyType, oi.minValue, oi.maxValue, key));
		var succ = Array<SsaCfEdge>.new(ob.succ.length);
		for (i < succ.length) {
			succ(i) = SsaCfEdge.new(curBlock.block, mapEdge(ob.succ(i)));
		}
		curBlock.block.succ = succ;
	}
	def genReturn(oi: SsaReturn) {
		curBlock.addReturn(genRefs(oi.inputs));
	}
	def genThrow(oi: SsaThrow) {
		curBlock.addThrow(oi.source, oi.exception);
	}
	def genGoto(oi: SsaGoto) {
		var ot = oi.target(), nt = mapBlockStart(ot);
		curBlock.addGoto(nt);
	}

	def mapBlockStart(ob: SsaBlock) -> SsaBlock {
		var nb = blockStartMap.get(ob);
		if (nb == null) {
			nb = SsaBlock.new();
			blockStartMap.set(ob, nb);
			blockEndMap.set(ob, nb);
			blockQueue.add(ob, nb);
		}
		return nb;
	}
	def mapBlockEnd(ob: SsaBlock) -> SsaBlock {
		return blockEndMap.get(ob);
	}
	def map0(oi: SsaInstr) {
		mapN(oi, Ssa.NO_INSTRS);
	}
	def map1(oi: SsaInstr, ni: SsaInstr) {
		if (SsaPhi.?(oi) && SsaPhi.?(ni)) phiQueue.add(SsaPhi.!(oi));
		instrMap.set(oi, ni);
		ni.facts = ni.facts | oi.facts;
	}
	def mapN(oi: SsaInstr, na: Array<SsaInstr>) {
		if (na.length == 1) return map1(oi, na(0));
		if (SsaPhi.?(oi)) phiQueue.add(SsaPhi.!(oi));
		if (multiMap == null) multiMap = Ssa.newMap();
		multiMap.set(oi, na);
	}
	def genRefs(a: Array<SsaDfEdge>) -> Array<SsaInstr> {
		var vec = Vector<SsaInstr>.new().grow(a.length);
		for (i < a.length) {
			var e = a(i), oi = e.dest, ni = instrMap.get(oi);
			if (ni != null) {
				// instruction was already translated
				vec.add(ni);
				continue;
			}
			if (multiMap != null) {
				// could be multiple instructions
				var nm = multiMap.get(oi);
				if (nm != null) {
					vec.addN(nm);
					continue;
				}
			}
			if (SsaValue.?(oi)) {
				// translate and add a use of a value.
				var val = genVal(e, i, SsaValue.!(oi), vec);
				if (val != null) vec.add(val);
			} else if (SsaPhi.?(oi)) {
				// translate and add a use of a phi.
				var phi = genPhi(SsaPhi.!(oi));
				if (phi != null) vec.add(phi);
				else vec.addN(multiMap.get(oi));
			} else {
				unexpectedInstr(oi);
			}
		}
		return vec.extract();
	}
	def genRef1(e: SsaDfEdge) -> SsaInstr {
		var oi = e.dest, ni = instrMap.get(oi);
		if (ni != null) return ni;
		if (SsaValue.?(oi)) return genVal1(e, -1, SsaValue.!(oi));
		if (SsaPhi.?(oi)) return genPhi1(SsaPhi.!(oi));
		return unexpectedInstr(oi);
	}
	def genVal1(e: SsaDfEdge, index: int, oi: SsaValue) -> SsaInstr {
		var val = genVal(e, index, oi, null);
		if (val == null) context.fail1("expected 1-1 val mapping for: #%1", oi.uid);
		return val;
	}
	def genPhi1(oi: SsaPhi) -> SsaInstr {
		var phi = genPhi(oi);
		if (phi == null) context.fail1("expected 1-1 phi mapping for: #%1", oi.uid);
		return phi;
	}
	def genVal(e: SsaDfEdge, index: int, oi: SsaValue, vec: Vector<SsaInstr>) -> SsaValue {
		var tn = normalize(oi.vtype), val: SsaValue;

		if (tn == null)        map1(oi, val = newGraph.valConst(oi.vtype, genSimpleVal(oi.val)));
		else if (tn.size == 1) map1(oi, val = newGraph.valConst(tn.newType, genSimpleVal(oi.val)));
		else if (tn.size == 0) map0(oi);
		else                   genValN(e, index, oi, tn, vec);

		return val;
	}
	def genSimpleVal(v: Val) -> Val {
		return v;
	}
	def genValN(e: SsaDfEdge, index: int, oi: SsaValue, tn: TypeNorm, vec: Vector<SsaInstr>);
	def mapEdge(edge: SsaCfEdge) -> SsaBlock {
		return mapBlockStart(edge.dest);
	}
	def opBoolAnd(x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return curBlock.opBoolAnd(V3Op.opBoolAnd, x, y);
	}
	def opBoolOr(x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return curBlock.opBoolOr(V3Op.opBoolOr, x, y);
	}
	def unexpectedInstr(oi: SsaInstr) -> SsaInstr {
		Terminal.put("Unexpected SSA instruction: ");
		var printer = SsaPrinter.new(context.prog);
		printer.printInstr(oi);
		Terminal.putb(printer.buf);
		context.fail("unexpected instruction");
		return oi;  // TODO: return a new error instruction?
	}
	def genPhi(oldPhi: SsaPhi) -> SsaPhi {
		var tn = normalize(oldPhi.vtype);
		var newBlock = mapBlockStart(oldPhi.block);
		if (tn == null) {    // no normalization
			var phi = SsaPhi.new(oldPhi.vtype, newBlock, Ssa.NO_INSTRS);
			map1(oldPhi, phi);
			return phi;
		}
		if (tn.size == 0) {  // degenerate zero-width phi
			map0(oldPhi);
			return null;
		}
		if (tn.size == 1) {  // normal case
			var phi = SsaPhi.new(tn.newType, newBlock, Ssa.NO_INSTRS);
			map1(oldPhi, phi);
			return phi;
		}
		// complex case.
		var phis = Array<SsaInstr>.new(tn.size);
		for (i < phis.length) {
			phis(i) = SsaPhi.new(tn.sub(i), newBlock, Ssa.NO_INSTRS);
		}
		mapN(oldPhi, phis);
		return null;
	}
	def typeFromEdge(e: SsaDfEdge, index: int) -> Type {
		var s = e.src;
		if (SsaPhi.?(s)) return SsaPhi.!(s).vtype;
		if (SsaApplyOp.?(s)) {
			var op = SsaApplyOp.!(s).op;
			if (index < 0) {
				index = 0;
				while (index < op.paramTypes.length) {
					if (s.inputs(index) == e) break;
					index++;
				}
			}
			return op.paramTypes(index);
		}
		if (SsaReturn.?(s)) {
			// TODO: graphs should have multiple return values.
			var t = context.graph.returnType;
			if (V3.isTuple(t)) t = Lists.get(t.nested, index);
			return t;
		}
		unexpectedInstr(s);
		return Void.TYPE;
	}
	def normalize(t: Type) -> TypeNorm;
	def mapNorm(oi: SsaInstr, ni: SsaInstr, tn: TypeNorm) {
		if (tn == null || tn.size == 1) return map1(oi, ni);
		if (tn.size == 0) return map0(oi);
		curBlock.at(null);
		var values = Array<SsaInstr>.new(tn.size);
		for (i < tn.size) values(i) = curBlock.opTupleGetElem(tn.newType, i, ni);
		mapN(oi, values);
	}
	def genEqualN(oi: SsaApplyOp, tn: TypeNorm) -> SsaInstr {
		var expr: SsaInstr, newArgs = genRefs(oi.inputs);
		var join = if(oi.op.opcode == Opcode.Equal, opBoolAnd, opBoolOr);
		for (i < tn.size) {
			var cmp: SsaInstr, a = newArgs(i), b = newArgs(i + tn.size);
			if (oi.op.opcode == Opcode.Equal) cmp = curBlock.opEqual(tn.sub(i), a, b);
			else cmp = curBlock.opNotEqual(tn.sub(i), a, b);
			if (expr == null) expr = cmp;
			else expr = join(expr, cmp);
		}
		map1(oi, expr);
		return expr;
	}
}
class TypeNorm(oldType: Type, newType: Type, sub: Array<Type>) {
	var raFacts: int;
	def size = if(sub == null, 1, sub.length);
	def addTo(vec: Vector<Type>) {
		if (sub == null) vec.add(newType);
		else if (sub.length > 0) vec.addN(sub);
	}
}
