// Copyright 2012 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A reusable context containing the compiler, the program, optimization settings, current
// method, and current block during the processing of SSA code. Also useful for generating
// internal error messages that contain context (e.g. when an SSA invariant is violated)
// which helps in debugging the compiler.
class SsaContext {
	def compiler: Compiler;
	def prog = compiler.prog;
	var opts = compiler.ssaOpts;
	var spec: IrSpec;
	var method: IrMethod;
	var region: SsaRegion;
	var block: SsaBlock;

	new(compiler) {}

	def enterSpec(s: IrSpec) -> SsaContext {
		spec = s;
		return enterMethod(s.asMethod());
	}
	def enterMethod(m: IrMethod) -> SsaContext {
		method = m;
		region = if(m != null, m.ssa);
		block = if(region != null, region.startBlock);
		return this;
	}
	def fail(msg: string) {
		// TODO: render SSA of failing method/block by default?
		var buf = StringBuffer.new();
		if (block != null) buf.format1("near block #%1 ", block.uid);
		if (method != null) method.renderLong(buf.puts("in ")).puts(": ");
		buf.puts(msg);
		compiler.ERROR.fail(buf.toString());
	}
}