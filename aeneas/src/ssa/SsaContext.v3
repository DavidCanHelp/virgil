// Copyright 2012 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A reusable context containing the compiler, the program, optimization settings, current
// method, and current block during the processing of SSA code. Also useful for generating
// internal error messages that contain context (e.g. when an SSA invariant is violated)
// which helps in debugging the compiler.
class SsaContext(compiler: Compiler, prog: Program) {
	var spec: IrSpec;
	var method: IrMethod;
	var graph: SsaGraph;
	var block: SsaBlock;

	def enterSpec(s: IrSpec) -> this {
		spec = s;
		enterMethod(s.asMethod());
	}
	def enterMethod(m: IrMethod) -> this {
		method = m;
		graph = if(m != null, m.ssa);
		block = if(graph != null, graph.startBlock);
	}
	def nonnull<T>(a: T) -> T {
		var x: T;
		if (a == x) fail("expected nonnull");
		return a;
	}
	def fail(msg: string) {
		// XXX: render SSA of failing method/block by default?
		prog.ERROR.fail(where().puts(msg).toString());
	}
	def fail1<A>(fmt: string, a: A) {
		// XXX: render SSA of failing method/block by default?
		prog.ERROR.fail(where().format1(fmt, a).toString());
	}
	def where() -> StringBuffer {
		var buf = StringBuffer.new();
		if (block != null) buf.format1("near block #%1 ", block.uid);
		if (method != null) method.renderLong(buf.puts("in ")).puts(": ");
		return buf;
	}
	def printSsa(phase: string) {
		var p = compiler.printMatcher;
		if (p != null && p.matches(method.source)) {
			var render = if(spec != null, spec.render, method.renderLong);
			SsaPrinter.new().printGraph(phase, render, method.ssa);
		}
		var s = compiler.statsMatcher;
		if (s != null && s.matches(method.source)) {
			var render = if(spec != null, spec.render, method.renderLong);
			SsaPrinter.new().printStats(phase, render, method.ssa);
		}
		verify();
	}
	def verify() {
		if (Debug.PARANOID) SsaGraphVerifier.new(prog, graph).verify();
	}
}
