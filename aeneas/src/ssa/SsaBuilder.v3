// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A helper class that can be used in construction of a new basic block.
class SsaBlockBuilder {
	var block: SsaBlock;
	var nullChecks: PartialMap<SsaInstr, SsaInstr>;
	var end: bool;

	new(block) { }

	def addApply(source: Source, op: Operator, args: Array<SsaInstr>) -> SsaInstr {
		if (end) return null;
		var i = SsaApplyOp.new(source, op, args);
		block.instrs.add(i);
		return i;
	}
	def addThrow(source: Source, ex: string) -> SsaInstr {
		if (end) return block.end;
		end = true;
		return block.end = SsaThrow.new(source, block, ex);
	}
	def addInstr(i: SsaInstr) -> SsaInstr {
		if (!end) block.instrs.add(i);
		return i;
	}
	def addSwitch(v: SsaInstr, vtype: Type, vals: Array<Val>, t: Array<SsaBlock>, d: SsaBlock) {
		if (end) return;
		end = true;
		var succ = Array<SsaCfEdge>.new(1 + t.length);
		for (i = 0; i < t.length; i++) {
			succ(i) = SsaCfEdge.new(block, t(i));
		}
		succ(t.length) = SsaCfEdge.new(block, d);
		block.succ = succ;
		block.end = SsaSwitch.new(block, vtype, v, vals);
	}
	def addGoto(target: SsaBlock) {
		if (end) return;
		end = true;
		block.succ = [SsaCfEdge.new(block, target)];
		block.end = SsaGoto.new(block);
	}
	def addReturn(vals: Array<SsaInstr>) {
		if (end) return;
		end = true;
		block.end = SsaReturn.new(block, vals);
	}
	def addNullCheck(source: Source, app: SsaInstr, i: SsaInstr) -> SsaInstr {
		if (end) return i;
		if (i.checkFact(SsaFacts.V_NON_ZERO)) return i;
		if (app != null && app.checkFact(SsaFacts.O_NO_NULL_CHECK)) return i;
		var check: SsaInstr;
		if (nullChecks == null) nullChecks = SsaUtil.newMap();
		else if ((check = nullChecks.get(i)) != null) return check;
		check = SsaApplyOp.new(source, V3Op.newNullCheck(i.getType()), [i]);
		check.facts = SsaFacts.V_NON_ZERO | i.facts;
		nullChecks.set(i, check);
		return addInstr(check);
	}
	def addPhi(t: Type, vals: Array<SsaInstr>) -> SsaInstr {
		if (block.phis == null) {
			block.phis = SsaPhis.new(block);
			for (l = block.predList; l != null; l = SsaCfEdge.!(l.next)) {
				block.phis.phiEdges.add(l);
			}
		}
		return block.phis.newPhi(t, vals);
	}
}
