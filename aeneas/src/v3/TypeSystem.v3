// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Defines operations on types for type checking.
component TypeSystem {
	// static decisions about type casts and queries
	def TRUE = TypeCast.new(CastOp.TRUE, Bool.TRUE);
	def FALSE = TypeCast.new(CastOp.FALSE, Bool.FALSE);
	def TRUE_X_NULL = TypeCast.new(CastOp.TRUE_X_NULL, null);
	def FALSE_X_NULL = TypeCast.new(CastOp.FALSE_X_NULL, null);
	def UNKNOWN_QUERY = TypeCast.new(CastOp.UNKNOWN_QUERY, null);
	def UNKNOWN_CAST = TypeCast.new(CastOp.UNKNOWN_CAST, null);
	def CLASS_CAST = TypeCast.new(CastOp.CLASS_CAST, null);
	def CLASS_QUERY = TypeCast.new(CastOp.CLASS_QUERY, null);
	def INT_CONVERSION = TypeCast.new(CastOp.INT_CONVERSION, null);
	def TUPLE_CAST = TypeCast.new(CastOp.TUPLE_CAST, null);
	def TUPLE_QUERY = TypeCast.new(CastOp.TUPLE_QUERY, null);

	def maybeEqual(t1: Type, t2: Type) -> bool {
		if (t1 == t2) return true;
		if (TypeParam.?(t1) || TypeParam.?(t2)) return true;
		if (TypeVar.?(t1) || TypeVar.?(t2)) return true;
		if (t1.typeCon == t2.typeCon) {
			return checkList(t1.nested, t2.nested, maybeEqual);
		}
		return false;
	}
	def isSubtype(t1: Type, t2: Type) -> bool {
		if (t1 == t2) return true;
		match (t2.typeCon.kind) {
			V3Kind.CLASS: {
				// check subtyping of classes by searching up the inheritance tree.
				if (t1 == Null.TYPE) return true;
				if (V3.isClass(t1)) {
					for (ct1 = t1; ct1 != null; ct1 = V3.getSuperType(ct1)) {
						if (ct1 == t2) return true;
						if (ct1.typeCon == t2.typeCon) return false;
					}
				}
				return false;
			}
			V3Kind.ARRAY: {
				// the only subtype of an array type is NULL.
				return t1 == Null.TYPE;
			} 
			V3Kind.TUPLE: {
				// tuples are co-variantly typed.
				if (V3.isTuple(t1)) return checkList(t1.nested, t2.nested, isSubtype);
				return false;
			}
			V3Kind.FUNCREF,
			V3Kind.DELEGATE: {
				if (t1 == Null.TYPE) return true;
				// functions are co-variant in return type and contra-variant in arguments.
				if (V3.isFunction(t1)) {
					var tl1 = t1.nested;
					var tl2 = t2.nested;
					return isSubtype(tl1.tail.head, tl2.tail.head)
						&& isSubtype(tl2.head, tl1.head);
				}
				return false;
			}
		}
		return false;
	}
	def isImplicitlyConvertible(src: Type, dest: Type) -> bool {
		if (dest == src || isSubtype(src, dest)) return true;
		if (V3.isInt(src) && V3.isInt(dest)) {
			return isIntImplicitlyConvertible(V3.asInt(src), V3.asInt(dest));
		}
		if (V3.isTuple(src) && V3.isTuple(dest)) {
			return checkList(src.nested, dest.nested, isImplicitlyConvertible);
		}
		return false;
	}
	def isIntImplicitlyConvertible(st: Int_TypeCon, dt: Int_TypeCon) -> bool {
		if (st.width > dt.width) return false;
		if (st.width == dt.width) return st.signed == dt.signed;
		return dt.signed || st.signed == dt.signed;
	}
	def checkList<T>(a: List<T>, b: List<T>, func: (T, T) -> bool) -> bool {
		while (a != null) {
			if (b == null) return false;
			if (!func(a.head, b.head)) return false;
			a = a.tail;
			b = b.tail;
		}
		return b == null;
	}
	def upper(t1: Type, t2: Type) -> Type {
		if (t1 == t2) {
			return t1;
		} else if (t1 == Null.TYPE && isReference(t2)) {
			return t2;
		} else if (t2 == Null.TYPE && isReference(t1)) {
			return t1;
		} else if (V3.isClass(t1)) {
			if (V3.isClass(t2)) {
				var crt = commonSuperClass(t1, t2);
				if (crt.0 != null && crt.0 == crt.1) return crt.0;
			}
			return null;
		} else if (V3.isFunction(t1)) {
			if (V3.isFunction(t2)) {
				return mergeLists(t1.nested, t2.nested, Function.DELEGATE, upper);
			}
			return null;
		} else if (V3.isTuple(t1) && V3.isTuple(t2)) {
			return mergeLists(t1.nested, t2.nested, Tuple.TYPECON, upper);
		}
		return null;
	}
	def widen(t1: Type, t2: Type) -> Type {
		if (t1 == t2) return t1;
		if (V3.isInt(t1)) {
			if (V3.isInt(t2)) {
				var i1 = V3.asInt(t1), i2 = V3.asInt(t2);
				if (isIntImplicitlyConvertible(i1, i2)) return t2;
				if (isIntImplicitlyConvertible(i2, i1)) return t1;
			}
			return null;
		}
		if (V3.isTuple(t1) && V3.isTuple(t2)) {
			return mergeLists(t1.nested, t2.nested, Tuple.TYPECON, widen);
		}
		return upper(t1, t2);
	}
	def mergeLists(tl1: List<Type>, tl2: List<Type>, tc: TypeCon, mergefun: (Type, Type) -> Type) -> Type {
		var rl: List<Type>;
		while (tl1 != null) {
			if (tl2 == null) return null;
			var rt = mergefun(tl1.head, tl2.head);
			if (rt == null) return null;
			rl = List.new(rt, rl);
			tl1 = tl1.tail;
			tl2 = tl2.tail;
		}
		if (tl2 == null) return tc.create(Lists.reverse(rl));
		return null;
	}
	def commonSuperClass(ct1: Type, ct2: Type) -> (Type, Type) {
		// common case: the same class.
		if (ct1.typeCon == ct2.typeCon) return (ct1, ct2);
		var ic1 = inheritanceChain(ct1);
		var ic2 = inheritanceChain(ct2);
		var r1: Type, r2: Type;
		// walk down both inheritance chains from the root until it diverges.
		while (ic1 != null && ic2 != null) {
			if (ic1.head.typeCon == ic2.head.typeCon) {
				r1 = ic1.head;
				r2 = ic2.head;
			}
			ic1 = ic1.tail;
			ic2 = ic2.tail;
		}
		return (r1, r2);
	}
	def inheritanceChain(ct: Type) -> List<Type> {
		// PERF: caching of inheritance chains might be important for performance
		var list: List<Type>;
		for (t = ct; t != null; t = V3.getSuperType(t)) {
			list = List.new(t, list);
		}
		return list;
	}
	def isReference(t1: Type) -> bool {
		var kind = t1.typeCon.kind;
		return kind == V3Kind.CLASS || kind == V3Kind.ARRAY || kind == V3Kind.DELEGATE;
	}
	def unifyWiden(t1: Type, t2: Type) {
		unify(t1, t2, true);
	}
	def unifyUpper(t1: Type, t2: Type) {
		unify(t1, t2, false);
	}
	def unify(t1: Type, t2: Type, useWiden: bool) {
		if (TypeVar.?(t1)) {
			// solve for this type variable
			var tvar = TypeVar.!(t1);
			if (tvar.vtype == null) {
				tvar.setType(t2);
			} else {
				var sol = if (useWiden, widen(tvar.vtype, t2), upper(tvar.vtype, t2));
				if (sol != null) tvar.setType(sol);
			}
		} else if (t1.open()) {
			// XXX: some adjustments for better inference of class types
			// may help here. For example, marking the existing solution of
			// a type variable as "do not modify" since it is already refers
			// to a nested type of an invariant type constructor

			// solve for any type variables nested in this type
			if (V3.isTuple(t1) && V3.isTuple(t2)) {
				// if both are tuple types, continue with current unification function
				if (useWiden) Lists.reduceV(t1.nested, t2.nested, unifyWiden);
				else Lists.reduceV(t1.nested, t2.nested, unifyUpper);
			} else if (t1.typeCon == t2.typeCon) {
				// if typecons match exactly, use the upper() unification function
				Lists.reduceV(t1.nested, t2.nested, unifyUpper);
			} else if (V3.isClass(t1) && V3.isClass(t2)) {
				var common = commonSuperClass(t1, t2);
				var ct1 = common.0, ct2 = common.1;
				if (ct1 != null && ct2 != null) {
					Lists.reduceV(ct1.nested, ct2.nested, unifyUpper);
				}
			}
		}
	}
	def newTypeQuery(fromType: Type, toType: Type) -> TypeCast {
		if (fromType == Null.TYPE) return FALSE;
		if (TypeParam.?(toType)) return UNKNOWN_QUERY;
		if (TypeParam.?(fromType)) return UNKNOWN_QUERY;
		if (TypeVar.?(toType)) return UNKNOWN_QUERY;
		if (TypeVar.?(fromType)) return UNKNOWN_QUERY;
		if (V3.isClass(toType)) {
			// check if they have a common superclass
			if (fromType == toType) return TRUE_X_NULL;
			var cs = TypeSystem.commonSuperClass(fromType, toType);
			var ftc = cs.0, ttc = cs.1;
			if (ftc == null) return FALSE;
			// one must be a superclass of the other in order to succeed
			if (ftc.typeCon == toType.typeCon) return maybeQuery(ftc, toType, TRUE_X_NULL, FALSE);
			if (fromType.typeCon == ttc.typeCon) return maybeQuery(fromType, ttc, CLASS_QUERY, FALSE);
			return FALSE;
		}
		if (V3.isTuple(fromType) && V3.isTuple(toType)) {
			return newTupleQuery(fromType, toType);
		}
		if (V3.isFunction(fromType) && V3.isFunction(toType)) {
			return checkFunctionSubtype(fromType, toType, UNKNOWN_QUERY);
		}
		if (V3.isArray(toType)) return maybeQuery(fromType, toType, TRUE_X_NULL, FALSE);
		if (fromType == toType) return TRUE;
		return maybeQuery(fromType, toType, TRUE, FALSE);
	}
	def checkFunctionSubtype(fromType: Type, toType: Type, unknown: TypeCast) -> TypeCast {
		// check param and return types
		if (fromType.open() || toType.open()) return unknown;
		var fl = fromType.nested, tl = toType.nested;
		if (!TypeSystem.isSubtype(fl.tail.head, tl.tail.head)) return FALSE;
		if (!TypeSystem.isSubtype(tl.head, fl.head)) return FALSE;
		return TRUE;
	}
	def newTypeCast(fromType: Type, toType: Type) -> TypeCast {
		if (fromType == toType) return TRUE;
		if (TypeParam.?(toType)) return UNKNOWN_CAST;
		if (TypeParam.?(fromType)) return UNKNOWN_CAST;
		if (TypeVar.?(toType)) return UNKNOWN_CAST;
		if (TypeVar.?(fromType)) return UNKNOWN_CAST;
		if (V3.isClass(toType)) {
			// check if they have a common superclass
			if (fromType == Null.TYPE) return TRUE;
			var cs = TypeSystem.commonSuperClass(fromType, toType);
			var ftc = cs.0, ttc = cs.1;
			if (ftc == null) return if(V3.isClass(fromType), FALSE_X_NULL, FALSE);
			// one must be a superclass of the other in order to succeed
			if (ftc.typeCon == toType.typeCon) { // upcast
				return maybeCast(ftc, toType, TRUE, FALSE_X_NULL);
			}
			if (fromType.typeCon == ttc.typeCon) { // downcast
				return maybeCast(fromType, ttc, CLASS_CAST, FALSE_X_NULL);
			}
			return FALSE;
		}
		if (V3.isTuple(fromType) && V3.isTuple(toType)) {
			return newTupleCast(fromType, toType);
		}
		if (V3.isFunction(fromType) && V3.isFunction(toType)) {
			return checkFunctionSubtype(fromType, toType, UNKNOWN_CAST);
		}
		if (V3.isArray(toType)) {
			if (fromType == Null.TYPE) return TRUE;
			return maybeCast(fromType, toType, TRUE, FALSE);
		}
		if (V3.isVariant(fromType)) {
			return maybeCast(fromType, toType, TRUE, FALSE);
		}
		if (V3.isInt(fromType) && V3.isInt(toType)) return INT_CONVERSION;
		return FALSE;
	}
	def maybeQuery(t1: Type, t2: Type, eq: TypeCast, neq: TypeCast) -> TypeCast {
		if (t1 == t2) return eq;
		if (TypeSystem.maybeEqual(t1, t2)) return UNKNOWN_QUERY;
		return neq;
	}
	def maybeCast(t1: Type, t2: Type, eq: TypeCast, neq: TypeCast) -> TypeCast {
		if (t1 == t2) return eq;
		if (TypeSystem.maybeEqual(t1, t2)) return UNKNOWN_CAST;
		return neq;
	}
	def newTupleQuery(ff: Type, tt: Type) -> TypeCast {
		// recursively check all nested types
		var fl = ff.nested, tl = tt.nested;
		while (fl != null) {
			if (tl == null) return FALSE; // tuple size doesnt match
			var inner = TypeSystem.newTypeQuery(fl.head, tl.head);
			if (inner.isFalse()) return FALSE;
			fl = fl.tail;
			tl = tl.tail;
		}
		if (tl != null) return FALSE; // tuple size doesnt match
		return TUPLE_QUERY;
	}
	def newTupleCast(ff: Type, tt: Type) -> TypeCast {
		// recursively check all nested types
		var fl = ff.nested, tl = tt.nested;
		while (fl != null) {
			if (tl == null) return FALSE; // tuple size doesnt match
			if (TypeSystem.newTypeCast(fl.head, tl.head).isFalse()) return FALSE;
			fl = fl.tail;
			tl = tl.tail;
		}
		if (tl != null) return FALSE; // tuple size doesnt match
		return TUPLE_CAST;
	}
	def evalTypeQuery(ff: Type, tt: Type, val: Val) -> TypeCast {
		return newTypeQuery(ff, tt).eval(ff, tt, val);
	}
	def evalTypeCast(ff: Type, tt: Type, val: Val) -> TypeCast {
		return newTypeCast(ff, tt).eval(ff, tt, val);
	}
}
// enumeration of the different kinds of type casts
component CastOp {
	def UNKNOWN_CAST   = '\x00';
	def UNKNOWN_QUERY  = '\x01';
	def TRUE           = '\x02';
	def TRUE_X_NULL    = '\x03';
	def FALSE_X_NULL   = '\x04';
	def FALSE          = '\x05';
	def VALUE          = '\x06';
	def SUBSUME        = '\x07';
	def INT_CONVERSION = '\x08';
	def CLASS_CAST     = '\x09';
	def CLASS_QUERY    = '\x0a';
	def TUPLE_CAST     = '\x0b';
	def TUPLE_QUERY    = '\x0c';
}

// The representation of a cast operation, or a the result of a cast operation.
class TypeCast(approx: byte, result: Val) {
	def eval(ff: Type, tt: Type, val: Val) -> TypeCast {
		match (approx) {
			CastOp.TRUE,
			CastOp.FALSE,
			CastOp.VALUE: return this;
			CastOp.UNKNOWN_CAST: return TypeSystem.evalTypeCast(ff, tt, val);
			CastOp.UNKNOWN_QUERY: return TypeSystem.evalTypeQuery(ff, tt, val);
			CastOp.SUBSUME: return TypeSystem.TRUE;
			CastOp.TRUE_X_NULL: return if(val == null, TypeSystem.FALSE, TypeSystem.TRUE);
			CastOp.FALSE_X_NULL: return if(val != null, TypeSystem.FALSE, TypeSystem.TRUE);
			CastOp.TUPLE_QUERY: return evalTupleQuery(ff, tt, val);
			CastOp.TUPLE_CAST: return evalTupleCast(ff, tt, val);
			CastOp.CLASS_CAST: {
				if (val == null) return TypeSystem.TRUE;
				if (tt.open()) return TypeSystem.FALSE;
				if (!Record.?(val) || !TypeSystem.isSubtype(Record.!(val).rtype, tt))
					return TypeSystem.FALSE;
				return TypeSystem.TRUE;
			}
			CastOp.CLASS_QUERY: {
				if (val == null) return TypeSystem.FALSE;
				if (tt.open()) return TypeSystem.FALSE;
				if (!Record.?(val) || !TypeSystem.isSubtype(Record.!(val).rtype, tt))
					return TypeSystem.FALSE;
				return TypeSystem.TRUE;
			}
			CastOp.INT_CONVERSION: {
				return IntOp.evalConversion(ff, tt, val);
			}
		}
		return TypeSystem.FALSE; // this is an error, should never happen
	}
	def evalTupleQuery(ff: Type, tt: Type, val: Val) -> TypeCast {
		// recursively check all nested types
		var values: Array<Val>;
		if (val == null) values = Array<Val>.new(Lists.length(ff.nested));
		else if (!Tuple_Value.?(val)) return TypeSystem.FALSE;
		else values = Tuple_Value.!(val).values;

		var a = ff.nested, b = tt.nested, i = 0;
		while (a != null) {
			if (b == null) return TypeSystem.FALSE;
			var r = TypeSystem.evalTypeQuery(a.head, b.head, values(i));
			if (r.approx == CastOp.FALSE) return TypeSystem.FALSE;
			a = a.tail; b = b.tail; i++;
		}
		return if(b == null, TypeSystem.TRUE, TypeSystem.FALSE);
	}
	def evalTupleCast(ff: Type, tt: Type, val: Val) -> TypeCast {
		// recursively check nested types of tuples
		var values: Array<Val>;
		if (val == null) values = Array<Val>.new(Lists.length(ff.nested));
		else if (!Tuple_Value.?(val)) return TypeSystem.FALSE;
		else values = Tuple_Value.!(val).values;

		var a = ff.nested, b = tt.nested, i = 0, rv = Array<Val>.new(values.length);
		while (a != null) {
			if (b == null) return TypeSystem.FALSE;
			var v = values(i);
			var r = TypeSystem.evalTypeCast(a.head, b.head, v);
			if (r.approx == CastOp.FALSE) return TypeSystem.FALSE;
			else if (r.approx == CastOp.VALUE) rv(i) = r.result;
			else rv(i) = v;
			a = a.tail; b = b.tail; i++;
		}
		if (b == null) return TypeCast.new(CastOp.VALUE, Tuple_Value.new(rv));
		return TypeSystem.FALSE;
	}
	def isFalse() -> bool { return approx == CastOp.FALSE; }
	def isTrue() -> bool { return approx == CastOp.TRUE; }
	def name() -> string {
		match (approx) {
			CastOp.TRUE: return "TRUE";
			CastOp.FALSE: return "FALSE";
			CastOp.VALUE: return "VALUE";
			CastOp.UNKNOWN_CAST:  return "UNKNOWN_CAST";
			CastOp.UNKNOWN_QUERY: return "UNKNOWN_QUERY";
			CastOp.SUBSUME:  return "SUBSUME";
			CastOp.TRUE_X_NULL:  return "TRUE_X_NULL";
			CastOp.FALSE_X_NULL: return "FALSE_X_NULL";
			CastOp.TUPLE_QUERY:  return "TUPLE_QUERY";
			CastOp.TUPLE_CAST: return "TUPLE_CAST";
			CastOp.CLASS_CAST: return "CLASS_CAST";
			CastOp.CLASS_QUERY: return "CLASS_QUERY";
			CastOp.INT_CONVERSION: return "INT_CONVERSION";
		}
		return "???";
	}
}
