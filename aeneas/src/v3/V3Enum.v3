// Copyright 2015 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def enumSetName(classDecl: VstClass) -> string {
	return Strings.toBuffer(classDecl.fullName).putc('.').puts("set").toString();
}
// Type constructor for sets of enums.
class V3EnumSet_TypeCon extends Member_TypeCon {
	def classDecl: VstClass;
	def intType = Int.getType(false, classDecl.cases.length);
	var binopTypes: Array<Type>;
	def eqOp = intType.lookupInfix0(V3Infix.Equal);
	def andOp = intType.lookupInfix0(V3Infix.And);
	def orOp = intType.lookupInfix0(V3Infix.Or);
	def xorOp = intType.lookupInfix0(V3Infix.Xor);

	new(classDecl, typeCache: TypeCache) 
		super(enumSetName(classDecl), V3Kind.ENUM_SET, 0, typeCache) {
	}
	// Lookup "Type.member" or "receiver.member"
	def lookupMember(t: Type, name: string, receiver: bool) -> LookupResult {
		var c = classDecl.memberMap.get(name);
		if (VstCaseMember.?(c)) {
			// simple membership check
			var op = V3Op.newVstSugar(VstOperator.EnumSetHas(VstCaseMember.!(c)), TypeUtil.NO_TYPES, [t], Bool.TYPE);
			return if(receiver, LookupResult.Apply(op, null, Facts.O_PURE), LookupResult.Inst(op, null));
		}
		if (!receiver && Strings.equal(name, "all")) {
			// the full set of all cases
			return LookupResult.Value(create0(), intType.max);
		}
		return LookupResult.None;
	}
	// Lookup "x <infix> y"
	def lookupInfix(infix: byte, rtype: Type) -> Operator {
		if (binopTypes == null) binopTypes = [create0(), create0()];
		var op: VstOperator, result = create0();
		match(infix) {
			V3Infix.And:  { op = VstOperator.EnumSetIntersect; }
			V3Infix.Or:   { op = VstOperator.EnumSetUnion; }
			V3Infix.Sub:  { op = VstOperator.EnumSetSubtract; }
			V3Infix.Lt:   { op = VstOperator.EnumSetStrictSubset; result = Bool.TYPE; }
			V3Infix.Gt:   { op = VstOperator.EnumSetStrictSuperset; result = Bool.TYPE; }
			V3Infix.Lteq: { op = VstOperator.EnumSetSubset; result = Bool.TYPE; }
			V3Infix.Gteq: { op = VstOperator.EnumSetSuperset; result = Bool.TYPE; }
			_: return null;
		}
		return V3Op.newVstSugar(op, TypeUtil.NO_TYPES, binopTypes, result);
	}

	def genEnumToSet(x: SsaInstr, block: SsaBuilder) -> SsaInstr {
		var shl = intType.lookupInfix0(V3Infix.QShl);
		var one = block.graph.valConst(intType, intType.box(1));
		var i = block.addApply(null, shl, [one, x]);
		i.setFact(Facts.O_NO_SHIFT_CHECK);
		return i;
	}
}
