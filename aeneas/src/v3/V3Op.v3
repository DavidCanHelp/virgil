// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Constants for the syntax of infix operators, used during parsing and
// semantic analysis
component V3Infix {
	def Equal    = '\x01';
	def NotEqual = '\x02';
	def Add      = '\x03';
	def Sub      = '\x04';
	def Mul      = '\x05';
	def Div      = '\x06';
	def Mod      = '\x07';
	def And      = '\x08';
	def Or       = '\x09';
	def AndAnd   = '\x0a';
	def OrOr     = '\x0b';
	def Xor      = '\x0c';
	def Shl      = '\x0d';
	def Sar      = '\x0e';
	def Shr      = '\x0f';
	def Lt       = '\x10';
	def Gt       = '\x11';
	def Lteq     = '\x12';
	def Gteq     = '\x13';
	def NumOps   = Gteq + 1;

	def names: Array<string> = Array.new(NumOps);
	def prec: Array<byte> = Array.new(NumOps);

	def op(num: int, name: string, p: int) {
		names(num) = name;
		prec(num) = byte.!(p);
	}

	new() {
		op(Equal,	"==",  17);
		op(NotEqual,	"!=",  16);
		op(Add,		"+",   23);
		op(Sub,		"-",   23);
		op(Mul,		"*",   24);
		op(Div,		"/",   24);
		op(Mod,		"%",   24);
		op(And,		"&",   15);
		op(Or,		"|",   13);
		op(AndAnd,	"&&",  12);
		op(OrOr,	"||",  11);
		op(Xor,		"^",   14);
		op(Shl,		"#<<", 22);
		op(Sar,		">>",  22);
		op(Shr,		"#>>", 22);
		op(Lt,		"<",   20);
		op(Gt,		">",   21);
		op(Lteq,	"<=",  18);
		op(Gteq,	">=",  19);
	}
	def parse(name: string) -> byte {
		// XXX: avoid the need to create an entire ParserState here
		var f = ParsedFile.new("<input>", name);
		var p = ParserState.new(f, ErrorGen.new(1), Parser.skipNone, null);
		var r = Parser.parseInfix(p);
		return if(r != null, r.infix);
	}
}
// Collects syntactic properties of an operator
class V3OpSyntax {
	def opcode: Opcode;
	def name: string;
	def builder: (V3SsaBuilder, Operator, SsaInstr, SsaInstr) -> SsaInstr;
	new(opcode, name, builder) { }
}
// This component centralizes all the operators in the Virgil III language.
// XXX: Use TypeArgs instead of Array<Type> to represent type arguments
component V3Op {
	def NONE: Array<Type> = [];
	def renderOp(op: Operator) -> string {
		return renderInto(op, StringBuffer.new()).toString();
	}
	def renderInto(op: Operator, buf: StringBuffer) -> StringBuffer {
		buf.puts(op.opcode.name);
		if (TerminalBuffer.?(buf)) TerminalBuffer.!(buf).green();
		if (OperatorOf<void>.?(op)) {
			renderTypeArgs(op.typeArgs, buf);
			return buf.end();
		}
		buf.putc('[');
		if (OperatorOf<V3OpSyntax>.?(op)) {
			buf.puts(op.attr<V3OpSyntax>().name);
		} else if (OperatorOf<TypeCast>.?(op)) {
			buf.puts(op.attr<TypeCast>().name());
		} else if (OperatorOf<IrMember>.?(op)) {
			var m = op.attr<IrMember>();
			if (m != null) m.render(buf);
		} else if (OperatorOf<int>.?(op)) {
			buf.puti(op.attr<int>());
		} else if (OperatorOf<string>.?(op)) {
			buf.puts(op.attr<string>());
		} else if (OperatorOf<Int_TypeCon>.?(op)) {
			buf.puts(op.attr<Int_TypeCon>().name);
		} else if (OperatorOf<Operator>.?(op)) {
			renderInto(op.attr<Operator>(), buf);
		} else {
			buf.puts("???");
		}
		renderTypeArgs(op.typeArgs, buf.putc(']'));
		return buf.end();
	}
	def needsNullCheck(app: SsaInstr, obj: SsaInstr) -> bool {
		if (obj.checkFact(Facts.V_NON_ZERO)) return false;
		return !app.checkFact(Facts.O_NO_NULL_CHECK);
	}
	def newOp0(opcode: Opcode, typeArgs: Array<Type>, paramTypes: Array<Type>, resultType: Type, func: (void, Arguments) -> Result) -> Operator {
		return OperatorOf<void>.new(opcode, typeArgs, paramTypes, resultType, (), func);
	}
	def newOp1<T>(opcode: Opcode, typeArgs: Array<Type>, paramTypes: Array<Type>, resultType: Type, func: (T, Arguments) -> Result, param: T) -> Operator {
		return OperatorOf<T>.new(opcode, typeArgs, paramTypes, resultType, param, func);
	}
	def op2(opcode: Opcode, name: string, left: Type, right: Type, result: Type, f: (V3OpSyntax, Arguments) -> Result, builder: (V3SsaBuilder, Operator, SsaInstr, SsaInstr) -> SsaInstr) -> OperatorOf<V3OpSyntax> {
		return OperatorOf.new(opcode, NONE, [left, right], result, V3OpSyntax.new(opcode, name, builder), f);
	}
	def renderTypeArgs(typeArgs: Array<Type>, buf: StringBuffer) -> StringBuffer {
		if (typeArgs.length > 0) {
			buf.putc('<');
			for (i < typeArgs.length) {
				if (i > 0) buf.puts(", ");
				var t = typeArgs(i);
				if (t == null) buf.puts("null");
				else t.render(buf);
			}			
			buf.putc('>');
		}
		return buf;
	}
	def extractIrSpec(exp: Operator) -> IrSpec {
		var ta = exp.typeArgs;
		return IrSpec.new(ta(0), ta, exp.attr<IrMember>());
	}
	def evalIrSpec(member: IrMember, args: Arguments) -> IrSpec {
		var ta = args.getTypes();
		return IrSpec.new(ta(0), ta, member);
	}
	def b(args: Arguments, index: int) -> bool { return Bool.unbox(args.getArg(index)); }

//----------------------------------------------------------------------------
	def syntaxEqual = V3OpSyntax.new(Opcode.Equal, "==", V3SsaBuilder.opEqualOf);
	def newEqual(equalType: Type) -> OperatorOf<V3OpSyntax> {
		return OperatorOf.new(Opcode.Equal, [equalType], [equalType, equalType], Bool.TYPE, syntaxEqual, evalEqual);
	}
	def evalEqual(s: V3OpSyntax, args: Arguments) -> Result {
		return Bool.box(Values.equal(args.getArg(0), args.getArg(1)));
	}
//----------------------------------------------------------------------------
	def syntaxNotEqual = V3OpSyntax.new(Opcode.Equal, "!=", V3SsaBuilder.opNotEqualOf);
	def newNotEqual(equalType: Type) -> OperatorOf<V3OpSyntax> {
		return OperatorOf.new(Opcode.NotEqual, [equalType], [equalType, equalType], Bool.TYPE, syntaxNotEqual, evalNotEqual);
	}
	def evalNotEqual(s: V3OpSyntax, args: Arguments) -> Result {
		return Bool.box(!Values.equal(args.getArg(0), args.getArg(1)));
	}
//----------------------------------------------------------------------------
	def newIntConvert(ft: Type, tt: Type) -> Operator {
		return OperatorOf.new(Opcode.IntConvert, [ft, tt], [ft], tt, (), evalIntConvert);
	}
	def evalIntConvert(v: void, a: Arguments) -> Result {
		var val = a.getArg(0), r = IntOp.evalConversion(a.getType(0), a.getType(1), val);
		if (r.approx == CastOp.TRUE) return val;
		if (r.approx == CastOp.VALUE) return r.result;
		else return a.throw(V3Exception.TypeCheck, null);
	}
//----------------------------------------------------------------------------
	def opBoolAnd = op2(Opcode.BoolAnd, "&&", Bool.TYPE, Bool.TYPE, Bool.TYPE, evalBoolAnd, V3SsaBuilder.opBoolAnd);
	def evalBoolAnd(s: V3OpSyntax, a: Arguments) -> Result { return Bool.box(b(a, 0) && b(a, 1)); }
//----------------------------------------------------------------------------
	def opBoolOr = op2(Opcode.BoolOr, "||", Bool.TYPE, Bool.TYPE, Bool.TYPE, evalBoolOr, V3SsaBuilder.opBoolOr);
	def evalBoolOr(s: V3OpSyntax, a: Arguments) -> Result { return Bool.box(b(a, 0) || b(a, 1)); }
//----------------------------------------------------------------------------
	def opBoolNot: Operator	= newOp0(Opcode.BoolNot, NONE, [Bool.TYPE], Bool.TYPE, evalBoolNot);
	def evalBoolNot(v: void, a: Arguments) -> Result { return Bool.box(!b(a, 0)); }
//----------------------------------------------------------------------------
	def newIntWide(op: Operator, normal: Array<Type>, result: Type) -> Operator {
		return OperatorOf.new(Opcode.IntWide, TypeUtil.NO_TYPES, normal, result, op, evalIntWide);
	}
	def evalIntWide(op: Operator, args: Arguments) -> Result {
		return args.throw(V3Exception.InternalError, "unimplemented");
	}
//----------------------------------------------------------------------------
	def newTypeCast(ff: Type, tt: Type) -> OperatorOf<TypeCast> {
		var cast = TypeSystem.newTypeCast(ff, tt);
		return OperatorOf.new(Opcode.TypeCast, [ff, tt], [ff], tt, cast, evalTypeCast);
	}
	def evalTypeCast(cast: TypeCast, args: Arguments) -> Result {
		var val = args.getArg(0), r = cast.eval(args.getType(0), args.getType(1), val);
		if (r.approx == CastOp.TRUE) return val;
		if (r.approx == CastOp.VALUE) return r.result;
		else return args.throw(V3Exception.TypeCheck, null);
	}
//----------------------------------------------------------------------------
	def newTypeQuery(ff: Type, tt: Type) -> OperatorOf<TypeCast> {
		var cast = TypeSystem.newTypeQuery(ff, tt);
		return OperatorOf.new(Opcode.TypeQuery, [ff, tt], [ff], Bool.TYPE, cast, evalTypeQuery);
	}
	def evalTypeQuery(cast: TypeCast, args: Arguments) -> Result {
		var val = args.getArg(0), r = cast.eval(args.getType(0), args.getType(1), val);
		return if(r.approx == CastOp.TRUE, Bool.TRUE, Bool.FALSE);
	}
//----------------------------------------------------------------------------
	def newTypeSubsume(typeFrom: Type, typeTo: Type) -> Operator {
		return newOp0(Opcode.TypeSubsume, [typeFrom, typeTo], [typeFrom], typeTo, evalTypeSubsume);
	}
	def evalTypeSubsume(v: void, args: Arguments) -> Result {
		return args.getArg(0);
	}
//----------------------------------------------------------------------------
	def newArrayAlloc(arrayType: Type) -> Operator {
		return newOp0(Opcode.ArrayAlloc, [arrayType], [Int.TYPE], arrayType, evalArrayAlloc);
	}
	def evalArrayAlloc(v: void, args: Arguments) -> Result {
		var length = Int.unbox(args.getArg(0));
		if (length < 0) return args.throw(V3Exception.LengthCheck, null);
		var arrayType = args.getType(0);
		if (arrayType.open()) return args.throw(V3Exception.InternalError, Strings.format1("type %1 should not be open", arrayType.render));
		return args.getProgram().newRecord(arrayType, length);
	}
//----------------------------------------------------------------------------
	def newArrayInit(arrayType: Type, length: int) -> Operator {
		var etype = V3Array.elementType(arrayType);
		var types = Array<Type>.new(length);
		for (i < types.length) types(i) = etype;
		return newOp1(Opcode.ArrayInit, [arrayType], types, arrayType, evalArrayInit, length);
	}
	def evalArrayInit(length: int, args: Arguments) -> Result {
		var array = args.getProgram().newRecord(args.getType(0), length);
		// XXX: Arrays.mapIndex could be useful?
		for (i < length) array.values(i) = args.getArg(i);
		return array;
	}
//----------------------------------------------------------------------------
	def newArrayGetElem(arrayType: Type) -> Operator {
		return newOp0(Opcode.ArrayGetElem, [arrayType], [arrayType, Int.TYPE], V3Array.elementType(arrayType), evalArrayGetElem);
	}
	def evalArrayGetElem(v: void, args: Arguments) -> Result {
		var array = Record.!(args.getArg(0));
		var index = Int.unbox(args.getArg(1));
		if (array == null) return args.throw(V3Exception.NullCheck, null);
		if (index < 0 || index >= array.values.length) return args.throw(V3Exception.BoundsCheck, null);
		return array.values(index);
	}
//----------------------------------------------------------------------------
	def newArraySetElem(arrayType: Type) -> Operator {
		var etype = V3Array.elementType(arrayType);
		return newOp0(Opcode.ArraySetElem, [arrayType], [arrayType, Int.TYPE, etype], Void.TYPE, evalArraySetElem);
	}
	def evalArraySetElem(v: void, args: Arguments) -> Result {
		var array = Record.!(args.getArg(0));
		var index = Int.unbox(args.getArg(1));
		if (array == null) return args.throw(V3Exception.NullCheck, null);
		if (index < 0 || index >= array.values.length) return args.throw(V3Exception.BoundsCheck, null);
		var val = args.getArg(2);
		array.values(index) = val;
		return val;
	}
//----------------------------------------------------------------------------
	def newArrayGetLength(arrayType: Type) -> Operator {
		return newOp0(Opcode.ArrayGetLength, [arrayType], [arrayType], Int.TYPE, evalArrayGetLength);
	}
	def evalArrayGetLength(v: void, args: Arguments) -> Result {
		var array = Record.!(args.getArg(0));
		if (array == null) return args.throw(V3Exception.NullCheck, null);
		return Int.box(array.values.length);
	}
//----------------------------------------------------------------------------
	def newClassAlloc(newRef: IrSpec) -> Operator {
		var ftype = newRef.getBoundType(), paramTypes = Function.getParamTypeArray(ftype);
		return newOp1(Opcode.ClassAlloc, [newRef.container], paramTypes, newRef.container, evalClassAlloc, newRef.member);
	}
	def evalClassAlloc(newRef: IrMember, args: Arguments) -> Result {
		var spec = evalIrSpec(newRef, args), prog = args.getProgram();
		var record = prog.newRecord(spec.container, prog.ir.numFields(spec.container));
		return args.fwCall(spec, record, 0, args.numArgs());
	}
	def newEmptyClassAlloc(classType: Type) -> Operator {
		return newOp1(Opcode.ClassAlloc, [classType], TypeUtil.NO_TYPES, classType, evalEmptyClassAlloc, null);
	}
	def evalEmptyClassAlloc(newRef: IrMember, args: Arguments) -> Result {
		var classType = args.getType(0), prog = args.getProgram();
		var record = prog.newRecord(classType, prog.ir.numFields(classType));
		return record;
	}
//----------------------------------------------------------------------------
	def newClassGetField(fieldRef: IrSpec) -> Operator {
		return newOp1(Opcode.ClassGetField, [fieldRef.container], [fieldRef.container],
			fieldRef.getFieldType(), evalClassGetField, fieldRef.member);
	}
	def evalClassGetField(fieldRef: IrMember, args: Arguments) -> Result {
		var object = Record.!(args.getArg(0));
		if (object == null) {
			if (V3.isVariant(fieldRef.container)) return null;
			else return args.throw(V3Exception.NullCheck, null);
		}
		return object.values(fieldRef.index);
	}
//----------------------------------------------------------------------------
	def newClassInitField(fieldRef: IrSpec) -> Operator {
		var fieldType = fieldRef.getFieldType();
		return newOp1(Opcode.ClassInitField, [fieldRef.container], [fieldRef.container, fieldType], Void.TYPE, evalClassSetField, fieldRef.member);
	}
//----------------------------------------------------------------------------
	def newClassSetField(fieldRef: IrSpec) -> Operator {
		var fieldType = fieldRef.getFieldType();
		return newOp1(Opcode.ClassSetField, [fieldRef.container], [fieldRef.container, fieldType], Void.TYPE, evalClassSetField, fieldRef.member);
	}
	def evalClassSetField(fieldRef: IrMember, args: Arguments) -> Result {
		var object = Record.!(args.getArg(0));
		if (object == null) return args.throw(V3Exception.NullCheck, null);
		var val = args.getArg(1);
		object.values(fieldRef.index) = val;
		return Values.BOTTOM;
	}
//----------------------------------------------------------------------------
	def newClassGetMethod(methodRef: IrSpec) -> Operator {
		var typeArgs = methodRef.typeArgs;
		return newOp1(Opcode.ClassGetMethod, typeArgs, [methodRef.container], 
			methodRef.getBoundType(), evalClassGetMethod, methodRef.member);
	}
	def evalClassGetMethod(meth: IrMember, args: Arguments) -> Result {
		var obj = Record.!(args.getArg(0));
		var spec = evalIrSpec(meth, args);
		if (obj == null) {
			if (!V3.isVariant(meth.container)) return args.throw(V3Exception.NullCheck, null);
		}
		return Closure.new(obj, spec);
	}
//----------------------------------------------------------------------------
	def newClassGetVirtual(methodRef: IrSpec) -> Operator {
		var typeArgs = methodRef.typeArgs;
		return newOp1(Opcode.ClassGetVirtual, typeArgs, [methodRef.container], 
			methodRef.getBoundType(), evalClassGetVirtual, methodRef.member);
	}
	def evalClassGetVirtual(member: IrMember, args: Arguments) -> Result {
		var obj = Record.!(args.getArg(0)), spec = evalIrSpec(member, args);
		if (obj == null) {
			if (V3.isVariant(member.container)) spec = args.getProgram().ir.resolveVariantDefaultMethodImpl(spec);
			else return args.throw(V3Exception.NullCheck, null);
		} else {
			spec = args.getProgram().ir.resolveMethodImpl(obj.rtype, spec);
		}
		return Closure.new(obj, spec);
	}
//----------------------------------------------------------------------------
	def newClassGetVirtFunc(methodRef: IrSpec) -> Operator {
		var typeArgs = methodRef.typeArgs;
		return newOp1(Opcode.ClassGetVirtFunc, typeArgs, [methodRef.container], 
			methodRef.getFuncType(), evalClassGetVirtFunc, methodRef.member);
	}
	def evalClassGetVirtFunc(member: IrMember, args: Arguments) -> Result {
		var obj = Record.!(args.getArg(0)), spec = evalIrSpec(member, args);
		if (obj == null) {
			if (V3.isVariant(member.container)) spec = args.getProgram().ir.resolveVariantDefaultMethodImpl(spec);
			else return args.throw(V3Exception.NullCheck, null);
		} else {
			spec = args.getProgram().ir.resolveMethodImpl(obj.rtype, spec);
		}
		return FuncVal.new(spec);
	}
//----------------------------------------------------------------------------
	def newComponentInit(ctype: Type, member: IrMethod) -> Operator {
		return newOp1(Opcode.ComponentInit, [ctype], TypeUtil.NO_TYPES, ctype, evalComponentInit, member);
	}
	def evalComponentInit(newRef: IrMember, args: Arguments) -> Result {
		var ctype = args.getType(0), decl = V3.asComponent(ctype).componentDecl;
		var prog = args.getProgram(), record = prog.getComponentRecord(decl);
		if (record == null) {
			record = prog.newRecord(ctype, prog.ir.numFields(ctype));
			prog.setComponentRecord(decl, record);
			var spec = IrSpec.new(ctype, [ctype], newRef);
			return args.fwCall(spec, record, 0, 0);
		}	
		return record;
	}
//----------------------------------------------------------------------------
	def newComponentGetField(fieldRef: IrSpec) -> Operator {
		return newOp1(Opcode.ComponentGetField, [fieldRef.container], [fieldRef.container], 
			fieldRef.getFieldType(), evalComponentGetField, fieldRef.member);
	}
	def evalComponentGetField(memberRef: IrMember, args: Arguments) -> Result {
		return getComponentRecord(args).values(memberRef.index);
	}
	def getComponentRecord(args: Arguments) -> Record {
		return args.getProgram().getComponentRecord(V3.asComponent(args.getType(0)).componentDecl);
	}
//----------------------------------------------------------------------------
	def newComponentSetField(fieldRef: IrSpec) -> Operator {
		var fieldType = fieldRef.getFieldType();
		return newOp1(Opcode.ComponentSetField, [fieldRef.container], [fieldRef.container, fieldType], 
			Void.TYPE, evalComponentSetField, fieldRef.member);
	}
	def evalComponentSetField(memberRef: IrMember, args: Arguments) -> Result {
		getComponentRecord(args).values(memberRef.index) = args.getArg(1);
		return Values.BOTTOM;
	}
//----------------------------------------------------------------------------
	def newComponentGetMethod(methodRef: IrSpec) -> Operator {
		var typeArgs = methodRef.typeArgs;
		return newOp1(Opcode.ComponentGetMethod, typeArgs, [methodRef.container], 
			methodRef.getBoundType(), evalComponentGetMethod, methodRef.member);
	}
	def evalComponentGetMethod(member: IrMember, args: Arguments) -> Result {
		var memberRef = evalIrSpec(member, args);
		return Closure.new(args.getArg(0), memberRef);
	}
//----------------------------------------------------------------------------
	def newTupleCreate(tupleType: Type) -> Operator {
		var paramTypes = Lists.toArray(tupleType.nested);
		return newOp1(Opcode.TupleCreate, [tupleType], paramTypes, tupleType, evalTupleCreate, paramTypes.length);
	}
	def evalTupleCreate(length: int, args: Arguments) -> Result {
		var vals = Array<Val>.new(length);
		for (i < vals.length) vals(i) = args.getArg(i);
		return Tuple_Value.new(vals);
	}
//----------------------------------------------------------------------------
	def newTupleGetElem(tupleType: Type, index: int) -> Operator {
		return newOp1(Opcode.TupleGetElem, [tupleType], [tupleType], Lists.get(tupleType.nested, index), evalTupleGetElem, index);
	}
	def evalTupleGetElem(index: int, args: Arguments) -> Result {
		var tuple = args.getArg(0);
		if (tuple == Values.BOTTOM) return Values.BOTTOM;
		return (Tuple_Value.!(tuple)).values(index);
	}
//----------------------------------------------------------------------------
	def newNullCheck(rtype: Type) -> Operator {
		return newOp0(Opcode.NullCheck, [rtype], [rtype], rtype, evalNullCheck);
	}
	def evalNullCheck(v: void, args: Arguments) -> Result {
		var obj = args.getArg(0);
		if (obj == null) return args.throw(V3Exception.NullCheck, null);
		return obj;
	}
//----------------------------------------------------------------------------
	def newBoundsCheck(rtype: Type) -> Operator {
		return newOp0(Opcode.BoundsCheck, [rtype], [rtype, Int.TYPE], Void.TYPE, evalBoundsCheck);
	}
	def evalBoundsCheck(v: void, args: Arguments) -> Result {
		var array = Record.!(args.getArg(0));
		var index = Int.unbox(args.getArg(1));
		if (array == null) return args.throw(V3Exception.NullCheck, null);
		if (index < 0 || index >= array.values.length) return args.throw(V3Exception.BoundsCheck, null);
		return null;
	}
//----------------------------------------------------------------------------
	def newCallClassVirtual(methodRef: IrSpec) -> Operator {
		var typeArgs = methodRef.typeArgs, ftype = methodRef.getUnboundType();
		var paramTypes = Function.getParamTypeArray(ftype);
		return newOp1(Opcode.CallClassVirtual, typeArgs, paramTypes, Function.getReturnType(ftype), evalCallClassVirtual, methodRef.member);
	}
	def evalCallClassVirtual(member: IrMember, args: Arguments) -> Result {
		var r = evalClassGetVirtual(member, args);
		if (Closure.?(r)) {
			var del = Closure.!(r);
			return args.fwCall(del.memberRef, del.val, 1, args.numArgs());
		}
		return r;
	}
//----------------------------------------------------------------------------
	def newCallMethod(methodRef: IrSpec) -> Operator {
		var typeArgs = methodRef.typeArgs;
		var ftype = if (V3.isComponent(methodRef.container), methodRef.getBoundType(), methodRef.getUnboundType());
		var paramTypes = Function.getParamTypeArray(ftype);
		return newOp1(Opcode.CallMethod, typeArgs, paramTypes, Function.getReturnType(ftype), evalCallMethod, methodRef.member);
	}
	def evalCallMethod(member: IrMember, args: Arguments) -> Result {
		var spec = evalIrSpec(member, args);
		if (member.container.typeCon.kind == V3Kind.CLASS) {
			var obj = Record.!(args.getArg(0));
			if (obj == null) return args.throw(V3Exception.NullCheck, null);
		}
		if (V3.isComponent(member.container)) {
			return args.fwCall(spec, null, 0, args.numArgs());
		}
		return args.fwCall(spec, args.getArg(0), 1, args.numArgs());
	}
//----------------------------------------------------------------------------
	def newCallClosure(ftype: Type) -> Operator {
		var fTypes = Function.getParamTypeArray(ftype);
		var paramTypes = Arrays.prepend(ftype, fTypes);
		return newOp0(Opcode.CallClosure, [ftype], paramTypes, Function.getReturnType(ftype), evalCallClosure);
	}
	def evalCallClosure(v: void, args: Arguments) -> Result {
		return args.throw(V3Exception.InternalError, "CallClosure should not be directly evaluated");
	}
//----------------------------------------------------------------------------
	def newCallFunction(ftype: Type) -> Operator {
		ftype = Function.funcRefType(Function.prependParamType(AnyObject.TYPE, ftype));
		if (ftype.typeCon.kind != V3Kind.FUNCREF) return V3.fail("only function types allowed");
		var paramTypes = Arrays.prepend(ftype, Function.getParamTypeArray(ftype));
		return newOp0(Opcode.CallFunction, [ftype], paramTypes, Function.getReturnType(ftype), evalCallFunction);
	}
	def evalCallFunction(v: void, args: Arguments) -> Result {
		return args.throw(V3Exception.InternalError, "CallFunction should not be directly evaluated");
	}
//----------------------------------------------------------------------------
	def newCreateClosure(methodRef: IrSpec, closure: Type) -> Operator {
		var typeArgs = methodRef.typeArgs;
		return newOp1(Opcode.CreateClosure, typeArgs, [closure], methodRef.getBoundType(), evalCreateClosure, methodRef.member);
	}
	def evalCreateClosure(member: IrMember, args: Arguments) -> Result {
		return Closure.new(args.getArg(0), evalIrSpec(member, args));
	}
//----------------------------------------------------------------------------
	def newSystemOp(name: string, paramTypes: Array<Type>, returnType: Type, func: (string, Arguments) -> Result) -> Operator {
		return newOp1(Opcode.SystemOp, NONE, paramTypes, returnType, func, name);
	}
//----------------------------------------------------------------------------
	def newVariantGetTag(vtype: Type) -> Operator {
		var vt = [vtype];
		return newOp1(Opcode.VariantGetTag, vt, vt, V3.asClass(vtype).classDecl.tagType, evalVariantGetTag, ());
	}
	def evalVariantGetTag(v: void, args: Arguments) -> Result {
		var val = args.getArg(0);
		return if(Record.?(val), Int.box(V3.getVariantTag(Record.!(val).rtype)));
	}
//----------------------------------------------------------------------------
	def newConditionalThrow(exception: string) -> Operator {
		return newOp1(Opcode.ConditionalThrow, NONE, [Bool.TYPE], Void.TYPE, evalConditionalThrow, exception);
	}
	def evalConditionalThrow(exception: string, args: Arguments) -> Result {
		if (Bool.unbox(args.getArg(0))) return args.throw(exception, null);
		return Values.BOTTOM;
	}
	// return op' where (x <op> y) == !(x <op'> y), if it exists
	def inverse(op: Operator) -> Operator {
		// XXX: use IntOp.inverse
		match(op.opcode) {
			Equal:    return V3Op.newNotEqual(op.typeArgs(0)); // !(x == y) == (x != y)
			NotEqual: return V3Op.newEqual(op.typeArgs(0));    // !(x != y) == (x == y)
			IntLt:    return IntOp.get(op, V3Infix.Gteq);  // !(x < y) == (x >= y)
			IntGt:    return IntOp.get(op, V3Infix.Lteq);  // !(x > y) == (x <= y)
			IntLteq:  return IntOp.get(op, V3Infix.Gt);    // !(x <= y) == (x > y)
			IntGteq:  return IntOp.get(op, V3Infix.Lt);    // !(x >= y) == (x < y)
			_: return null;
		}
	}
	// return op' where (x <op> y) == (y <op'> x), if it exists
	def commute(op: Operator) -> Operator {
		// XXX: use IntOp.commute
		match(op.opcode) {
			IntAdd,
			IntMul,
			IntAnd,
			IntOr,
			IntXor,
			Equal,
			NotEqual: return op; // (x <op> y) == (y <op> x)
			IntLt:    return IntOp.get(op, V3Infix.Gt);    // (x < y) == (y > x)
			IntGt:    return IntOp.get(op, V3Infix.Lt);    // (x > y) == (y < x)
			IntLteq:  return IntOp.get(op, V3Infix.Gteq);  // (x <= y) == (y >= x)
			IntGteq:  return IntOp.get(op, V3Infix.Lteq);  // (x >= y) == (y <= x)
			_: return null;
		}
	}
}
