// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

component V3Kind {
	def VOID	= 0;
	def BOOL	= 1;
	def INT		= 2;
	def ARRAY	= 3;
	def COMPONENT	= 4;
	def CLASS	= 5;
	def DELEGATE	= 6;
	def TUPLE	= 7;
	def TYPE_PARAM	= 8;
	def TYPE_VAR	= 9;
	def FUNCREF	= 10;
	def SET		= 11;
	def VARIANT	= 12;

	new() {
		// prevent inconsistencies with Type infrastructure
		if (TYPE_VAR != TypeUtil.TYPE_VAR_KIND) return V3.fail("kind number does not match");
		if (TYPE_PARAM != TypeUtil.TYPE_PARAM_KIND) return V3.fail("kind number does not match");
	}
}
// Utilities related to the Virgil III source language, including common types, 
// type queries, name mangling, etc.
component V3 {
	def toplevelTypeEnv = TypeEnv.new(null, null);
	def stringType      = V3Array.newType(Byte.TYPE);
	def stringArrayType = V3Array.newType(stringType);
	def voidArrayType   = V3Array.newType(Void.TYPE);

	new() {
		// add the basic types and the array constructor
		Int.addTypes(toplevelTypeEnv);
		toplevelTypeEnv.add(Bool.TYPE.typeCon);
		toplevelTypeEnv.add(Void.TYPE.typeCon);
		toplevelTypeEnv.add(V3Array.TYPECON);

		// build a special type con for strings
		toplevelTypeEnv.add(TypeUtil.aliasTypeCon("string", stringType));
	}
	def isPolymorphicClass(ctype: Type) -> bool {
		return ctype != null && V3Class_TypeCon.?(ctype.typeCon) && ctype.typeCon.arity > 0;
	}
	def isClass(t: Type) -> bool { return t != null && V3Class_TypeCon.?(t.typeCon); }
	def isInt(t: Type) -> bool { return t != null && t.typeCon.kind == V3Kind.INT; }
	def isComponent(t: Type) -> bool { return t != null && V3Component_TypeCon.?(t.typeCon); }
	def isFunction(t: Type) -> bool { return t != null && Function_TypeCon.?(t.typeCon); }
	def isArray(t: Type) -> bool { return t != null && t.typeCon == V3Array.TYPECON; }
	def isVariant(t: Type) -> bool { return t != null && t.typeCon.kind == V3Kind.VARIANT; }
	def isTuple(t: Type) -> bool { return t != null && t.typeCon == Tuple.TYPECON; }
	def isString(t: Type) -> bool { return t == stringType; }
	def isPrimitiveArray(t: Type) -> bool {
		if (t.typeCon.kind == V3Kind.ARRAY) {
			match (t.nested.head.typeCon.kind) {
				V3Kind.INT, V3Kind.BOOL, V3Kind.VOID: return true;
			}
		}
		return false;
	}
	def asClass(t: Type) -> V3Class_TypeCon {
		return V3Class_TypeCon.!(t.typeCon);
	}
	def asComponent(t: Type) -> V3Component_TypeCon {
		return V3Component_TypeCon.!(t.typeCon);
	}
	def asVariant(t: Type) -> V3Variant_TypeCon {
		return V3Variant_TypeCon.!(t.typeCon);
	}
	def asInt(t: Type) -> Int_TypeCon {
		return Int_TypeCon.!(t.typeCon);
	}
	def isSigned(t: Type) -> bool {
		return Int_TypeCon.?(t.typeCon) && Int_TypeCon.!(t.typeCon).signed;
	}
	def getSyntheticVariantType(t: Type, vcase: VstVariantCase) -> Type {
		return vcase.synthetic.typeCon.create(t.nested);
	}
	def isDefaultVariantType(t: Type) -> bool {
		return getVariantTag(t) == 0;
	}
	def makeDefaultVariantRecord(prog: Program, t: Type) -> Record {
		var first = asVariant(t).variantDecl.firstCase;
		if (first != null) t = getSyntheticVariantType(t, first);
		return prog.newRecord(t, prog.ir.makeIrClass(t).fields.length);
	}
	def getVariantTag(t: Type) -> int {
		var vcase = asVariant(t).variantDecl.container;
		return if (vcase == null, -1, vcase.tag);
	}
	def getSuperType(t: Type) -> Type {
		// inlined and optimized super type lookup
		if (t == null) return null;
		if (V3Class_TypeCon.?(t.typeCon)) {
			var ct = V3Class_TypeCon.!(t.typeCon);
			var declSuper = ct.classDecl.superclass;
			if (declSuper == null) return null;
			if (t.nested == null) return declSuper.getType();
			return ct.lookupCachedSuperType(t);
		}
		if (V3Variant_TypeCon.?(t.typeCon)) {
			var vt = V3Variant_TypeCon.!(t.typeCon);
			if (vt.variantDecl.container == null) return null;
			return vt.variantDecl.parent.typeCon.create(t.nested);
		}
		return null;
	}
	def getTypeArgs(t: Type) -> TypeArgs {
		// inlined and optimized type arguments lookup
		if (t == null) return null;
		if (t.nested == null) return null;
		if (V3Class_TypeCon.?(t.typeCon)) {
			var ct = V3Class_TypeCon.!(t.typeCon);
			return ct.lookupCachedTypeArgs(t);
		}
		if (V3Variant_TypeCon.?(t.typeCon)) {
			var vt = V3Variant_TypeCon.!(t.typeCon);
			return TypeArgs.new(vt.variantDecl.typeEnv, Lists.toArray(t.nested)); // TODO: cache type args
		}
		return null;
	}
	def mangleIrMember(m: IrMember) -> string {
		var buffer = m.render(StringBuffer.new());
		return Mangle.mangleB(buffer, StringBuffer.new()).toString();
	}
	def newRecordMap<T>() -> HashMap<Record, T> {
		return HashMap.new(Record.id, Record.==);
	}
	def newProgram() -> Program {
		var prog = Program.new();
		var lang = V3Language.new(prog);
		prog.addLanguage(lang);
		return prog;
	}
	def renderResult(result: Result, buffer: StringBuffer) -> StringBuffer {
		return newProgram().renderResult(result, null, buffer);
	}
	def unboxIntegral(v: Val) -> int {
		if (v == null) return 0;
		if (Box<int>.?(v)) return Box<int>.!(v).val;
		if (Box<bool>.?(v)) return if(Bool.unbox(v), 1, 0);
		return fail("expected integral value");
	}
	def fail<T>(msg: string) -> T {
		var v: T;
		System.error(V3Exception.InternalError, msg);
		return v;
	}
	def fail1<T, A>(msg: string, p: A) -> T {
		return fail(Strings.format1(msg, p));
	}
	def newComponent(name: string, where: string, typeCache: TypeCache) -> Type {
		var decl = VstComponent.new(Token.new(where, name, 0, 0), null);
		var typeCon = V3Component_TypeCon.new(decl, typeCache);
		decl.memberMap = Strings.newMap();
		return typeCon.create0();
	}
}
// Rendering of values specific to Virgil III
class V3Language(prog: Program) extends Language {
	def renderVal(val: Val, vtype: Type, buffer: StringBuffer) -> bool {
		if (val == Values.BOTTOM) return renderBottom(vtype, buffer);
		else if (vtype == Byte.TYPE) renderByte(Byte.unbox(val), buffer);
		else if (Box<int>.?(val)) buffer.puti(Int.unbox(val));
		else if (Box<bool>.?(val)) buffer.appendBool(Bool.unbox(val));
		else if (Record.?(val)) {
			var record = Record.!(val);
			buffer.format2("#%1:%2", record.id, record.rtype.render);
		} else if (Tuple_Value.?(val)) {
			var vals = (Tuple_Value.!(val)).values;
			var list: List<Type> = null;
			if (vtype != null) list = vtype.nested;
			buffer.putc('(');
			for (i < vals.length) {
				if (i > 0) buffer.puts(", ");
				if (list != null) {
					prog.renderResult(vals(i), list.head, buffer);
					list = list.tail;
				} else {
					prog.renderResult(vals(i), null, buffer);
				}
			}
			buffer.putc(')');
		} else if (Delegate.?(val)) {
			var del = Delegate.!(val);
			prog.renderResult(del.val, null, buffer);
			buffer.putc(':');
			del.memberRef.render(buffer);
		} else if (FuncVal.?(val)) {
			var func = FuncVal.!(val);
			buffer.putc('&');
			func.memberRef.render(buffer);
		} else {
			return false;
		}
		return true;
	}
	def renderByte(ch: byte, buffer: StringBuffer) -> bool {
		buffer.putc('\'').appendEscapedChar(ch).putc('\'');
		return true;
	}
	def renderBottom(vtype: Type, buffer: StringBuffer) -> bool {
		if (vtype == null) {
			buffer.puts("<null>");
			return true;
		}
		var str: string;
		match (vtype.typeCon.kind) {
			V3Kind.VOID: str = "void";
			V3Kind.BOOL: str = "false";
			V3Kind.INT: str = "0";
			V3Kind.CLASS, V3Kind.ARRAY, V3Kind.DELEGATE: str = "null";
			V3Kind.COMPONENT: str = V3.asComponent(vtype).componentDecl.name();
			V3Kind.TUPLE: {
				buffer.putc('(');
				for (list = vtype.nested; list != null; list = list.tail) {
					if (list != vtype.nested) buffer.puts(", ");
					prog.renderResult(Values.BOTTOM, list.head, buffer);
				}
				buffer.putc(')');
				return true;
			}
			V3Kind.VARIANT: {
				vtype.render(buffer).puts(".null"); // TODO: render variant
				return true;
			}
		} else {
			return false;
		}
		buffer.puts(str);
		return true;
	}
}
// Constants for exception names
component V3Exception {
	def NullCheck     = "NullCheckException";
	def TypeCheck     = "TypeCheckException";
	def BoundsCheck   = "BoundsCheckException";
	def LengthCheck   = "LengthCheckException";
	def Unimplemented = "UnimplementedException";
	def DivideByZero  = "DivideByZeroException";
	def ShiftCheck    = "ShiftCheckException";
	def InternalError = "InternalError";
	def HeapOverflow  = "HeapOverflow";
	def StackOverflow = "StackOverflow";
}
