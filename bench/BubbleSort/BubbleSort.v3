// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A simple bubble sort program to test array performance and optimizations 
component BubbleSort {
	var array: Array<int>;

	def main(args: Array<Array<byte>>) -> int {
		var max = Util.intArg(args, 500);
		init(max);
		bubblesort();
		print();
		return 0;
	}
	def init(max: int) {
		array = Array<int>.new(max);
		var f = max - 13;
		var h = ((max - 117) * (max - 13)) / max;
		for (i = 0; i < array.length; i++) {
			array(i) = (f * i) % h - (h / 2);
		}
		print();
	}
	def bubblesort() {
		for (i = 0; i < array.length - 1; i++) {
			for (j = 0; j < array.length - 1; j++) {
				if (array(j) > array(j + 1)) {
					var tmp = array(j);
					array(j) = array(j + 1);
					array(j + 1) = tmp;
				}
			}
		}
	}
	def print() {
		for (e in array) {
			System.puti(e);
			System.ln();
		}
	}
}
