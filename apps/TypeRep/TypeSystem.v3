// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Relations between types.
component TypeSystem {
	// Returns {true} if {src} is a subtype of {dst}, meaning that every value
	// of type {src} can subsume without representation change to type {dst}.
	def isSubtype(src: Type, dst: Type) -> bool {
		if (src == dst) return true;

		// aliases might be subtypes of any type
		if (AliasType.?(src)) return isSubtype(AliasType.!(src).getAlias(), dst);

		match (dst) {
			y: ClassType => {
				// check subtyping of classes by searching up the inheritance tree.
				match (src) {
					x: NullType => return true;
					x: ClassType => {
						for (c = x; c != null; c = c.getSuperClassType()) {
							if (c == y) return true;
						}
						return false;
					}
					_ => return false;
				}
			}
			y: VariantType => {
				// check subtyping of variants by searching up the inheritance tree.
				match (src) {
					x: VariantType => {
						for (v = x; v != null; v = v.getSuperVariantType()) {
							if (v == y) return true;
						}
						return false;
					}
					_ => return false;
				}
			}
			y: ArrayType => {
				// the only subtype of an array type is NULL.
				return NullType.?(src);
			}
			y: TupleType => {
				// tuples are co-variantly typed.
				match (src) {
					x: TupleType => return isSubtypeArray(x.elemTypes, y.elemTypes);
					_ => return false;
				}
			}
			y: ClosureType => {
				// closures are co-variant in return and contra-variant in params
				match (src) {
					x: ClosureType => return isSigSubtype(x.sig, y.sig);
					_ => return false;
				}
			}
			y: FuncType => {
				// functions are co-variant in return and contra-variant in params
				match (src) {
					x: FuncType => return isSigSubtype(x.sig, y.sig);
					_ => return false;
				}
			}
		}
		return false;
	}
	private def isSigSubtype(s1: Signature, s2: Signature) -> bool {
		return isSubtypeArray(s1.results, s2.results) && isSubtypeArray(s2.params, s1.params);
	}
	private def isSubtypeArray = Arrays.allTrue(_, _, isSubtype);

	// Returns {true} if {src} is promotable to {dst}, meaning that every value
	// of type {src} can either subsume without representation change or be promoted
	// to type {dst} without any loss of information.
	def isPromotable(src: Type, dst: Type) -> bool {
		if (dst == src || isSubtype(src, dst)) return true;
		match (src) {
			x: IntType => match (dst) {
				y: IntType => {
					if (x.width > y.width) return false;
					if (x.width == y.width) return x.signed == y.signed;
					return y.signed || x.signed == y.signed;
				}
				y: FloatType => {
					if (x.signed) return x.width <= y.fraction_width;
					else return x.width <= 1 + y.fraction_width;
				}
				_ => return false;
			}
			x: FloatType => match (dst) {
				y: FloatType => return x.total_width <= y.total_width;
				_ => return false;
			}
			x: TupleType => match (dst) {
				y: TupleType => return Arrays.allTrue(x.elemTypes, y.elemTypes, isPromotable);
				_ => return false;
			}
			x: EnumType => match (dst) {
				y: EnumSetType => return x.decl == y.decl;
				_ => return false;
			}
			_ => return false;
		}
	}

	// Returns {true} if every value of type {src} can be assigned to {dst}, either
	// through subsumption or promotion.
	def isAssignable(src: Type, dst: Type) -> bool {
		return src == dst || isSubtype(src, dst) || isPromotable(src, dst);
	}

	// Compute the common superclass of {x} and {y}, if it exists, walking up the
	// inheritance hierarchy in lock-step fashion until the same declaration is met.
	def commonSuperClass(x: ClassType, y: ClassType) -> (ClassType, ClassType) {
		while (x.depth > y.depth) x = x.getSuperClassType();
		while (y.depth > x.depth) y = y.getSuperClassType();
		return if(x.decl == y.decl, (x, y), (null, null));
	}

	// Returns {true} if there is some assignment of types to type parameters such
	// that {x} and {y} are exactly equivalent types.
	def maybeEqual(x: Type, y: Type) -> bool {
		return false; // TODO
	}
	private def maybeQuery(x: Type, y: Type, t: TypeQuery, f: TypeQuery) -> TypeQuery {
		if (x == y) return t;
		if (maybeEqual(x, y)) return TypeQuery.Polymorphic;
		return f;
	}

	// Return {true} if {t} is a reference type.
	def isRefType(t: Type) -> bool {
		match (t) {
			x: ClassType => return true;
			x: ArrayType => return true;
			x: ClosureType => return true;
			_ => return false;
		}
	}

	// Unify two types {a} and {b} (adjusting the solution to any type variables that may
	// appear in {a}) and return a type that both {a} and {b} are assignable to, if such
	// a type exists. Type variables in {b} are ignored.
	def widen = unify(_, _, UnifyMode.PROMOTE);

	// Unify two types {a} and {b} (adjusting the solution to any type variables that may
	// appear in {a}) and return a type that both {a} and {b} are subtypes of, if such
	// a type exists. Type variables in {b} are ignored.
	def upper = unify(_, _, UnifyMode.UP);
	// Guts of recursive unification.
	private def unify(a: Type, b: Type, mode: UnifyMode) -> Type {
		if (a == b) return a;

		match (a) {
			x: IntType => if (mode == UnifyMode.PROMOTE) {
				if (isPromotable(x, b)) return b;
				if (isPromotable(b, x)) return x;
			}
			x: FloatType => if (mode == UnifyMode.PROMOTE) {
				if (isPromotable(x, b)) return b;
				if (isPromotable(b, x)) return x;
			}
			x: ArrayType => match (b) {
				y: ArrayType => {
					var e = unify(x.elemType, y.elemType, UnifyMode.EQUAL);
					return if(e != null, ArrayTypeCache.get(e));
				}
				y: NullType => return x;
			}
			x: ClassType => match (b) {
				y: ClassType => match (mode) {
					UP, PROMOTE => {
						return unifySuperClasses(x, y);
					}
					EQUAL => {
						var u = unifySuperClasses(x, y);
						return if(u.decl == x.decl && u.decl == y.decl, u);
					}
					DOWN => {
						var u = unifySuperClasses(x, y);
						if (u == null) return null;
						return if(x.depth > y.depth, x, y);
					}
				}
				y: NullType => return if(mode == UnifyMode.DOWN, null, x);
				_ => return null;
			}
			x: VariantType => {
				// TODO: nearly identical to classes, except the type arg substitution
			}
			x: ClosureType => match (b) {
				y: ClosureType => {
					var pt = unifyArray(x.sig.params, y.sig.params, reverseUnifyMode(mode));
					var rt = unifyArray(x.sig.results, y.sig.results, mode);
					return if(pt != null && rt != null, ClosureTypeCache.get(Signature.new(pt, rt)));
				}
			}
			x: FuncType => match (b) {
				y: FuncType => {
					var pt = unifyArray(x.sig.params, y.sig.params, reverseUnifyMode(mode));
					var rt = unifyArray(x.sig.results, y.sig.results, mode);
					return if(pt != null && rt != null, FuncTypeCache.get(Signature.new(pt, rt)));
				}
			}
			x: TupleType => match(b) {
				y: TupleType => {
					var elemTypes = unifyArray(x.elemTypes, y.elemTypes, mode);
					if (elemTypes == null) return null;
					return TupleTypeCache.get(elemTypes);
				}
				_ => return null;
			}
			x: TypeVarType => {
				var lub = if(x.solution == null, b, unify(x.solution, b, mode));
				if (lub != null) {
					x.solution = lub;
					return x;
				}
				return null;
			}
			x: NullType => {
				if(isRefType(b)) return if(mode == UnifyMode.DOWN, null, b);
			}
			// TODO: type aliases
		}
		return null;
	}
	private def unifyArray(at: Array<Type>, bt: Array<Type>, mode: UnifyMode) -> Array<Type> {
		if (at.length != bt.length) return null;
		var rt = Array<Type>.new(at.length);
		for (i < rt.length) {
			var u = unify(at[i], bt[i], mode);
			if (u == null) return null;
			rt[i] = u;
		}
		return rt;
	}
	private def unifySuperClasses(x: ClassType, y: ClassType) -> ClassType {
		var t = commonSuperClass(x, y), xx = t.0, yy = t.1;
		if (xx == null) return null;
		var typeArgs = unifyArray(xx.typeArgs, yy.typeArgs, UnifyMode.EQUAL);
		if (typeArgs == null) return null;
		xx = xx.decl.getCachedType(typeArgs);
		return if(xx == yy, xx);
	}
	private def reverseUnifyMode(mode: UnifyMode) -> UnifyMode {
		match (mode) {
			UP => return UnifyMode.DOWN;
			DOWN => return UnifyMode.UP;
			_ => return mode;
		}
	}

	// Compute the query operation, if it exists, for the expression "dst.?(src)".
	def newTypeQuery(src: Type, dst: Type) -> TypeQuery {
		if (NullType.?(src)) return TypeQuery.False;
		if (src == dst) return TypeQuery.True;
		if (TypeParamType.?(src)) return TypeQuery.Polymorphic;
		if (TypeVarType.?(src)) return TypeQuery.Polymorphic;

		match (dst) {
			y: IntType => ; // TODO
			y: FloatType => ; // TODO
			y: ArrayType => ; // TODO
			y: ComponentType => ;
			y: ClassType => match (src) {
				x: ClassType => {
					var t = commonSuperClass(x, y), xx = t.0, yy = t.1;
					if (xx == null) return TypeQuery.False;
					var query = if(x.depth < y.depth, TypeQuery.ClassQuery, TypeQuery.TrueExceptForNull);
					return maybeQuery(xx, yy, query, TypeQuery.False);
				}
				_ => return TypeQuery.False;
			}
			y: ClosureType => ; // TODO
			y: FuncType => ; // TODO
			y: TupleType => match (src) {
				x: TupleType => ; // TODO
				_ => return TypeQuery.False;
			}
			y: TypeParamType => return TypeQuery.Polymorphic;
			y: TypeVarType => return TypeQuery.Polymorphic;
			y: SetType => ; // TODO
			y: VariantType => ; // TODO
			y: EnumType => ; // TODO
			y: EnumSetType => ; // TODO
		}
		return TypeQuery.Polymorphic;
	}
	// Compute the cast operation, if it exists, for the expression "dst.!(src)".
	def newTypeCast(src: Type, dst: Type) -> TypeCast; // TODO
}

enum UnifyMode {
	UP, PROMOTE, EQUAL, DOWN
}

// The different types of type query operations.
type TypeQuery {
	case False;
	case Polymorphic;
	case True;
	case TrueExceptForNull;
	case FloatQueryI;
	case FloatQueryD;
	case IntQueryF;
	case IntQueryI;
	case ClassQuery;
	case VariantQuery;
	case TupleQuery(sub: Array<TypeQuery>);
}

// The different kinds of type cast operations.
type TypeCast {
	case Throw;
	case Polymorphic;
	case Value;
	case Identity;
	case ThrowIfNonNull;
	case FloatCastI;
	case FloatPromoteI;
	case FloatPromoteF;
	case IntCastI;
	case IntCastF;
	case IntPromoteI;
	case IntViewI;  // legacy
	case Subsume;
	case ClassCast;
	case VariantCast;
	case TupleCast(sub: Array<TypeCast>);
}
