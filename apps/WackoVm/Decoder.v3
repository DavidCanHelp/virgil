def MAX_U32V_LENGTH = 5;
def MAX_U64V_LENGTH = 10;
// hexadecimal characters for each 0-15 integer value
def hexMap = "0123456789ABCDEF";

class Decoder(data: Array<byte>) {
	var pos: int;
	var ok = true;
	var error_msg: string;
	var error_pos: int;
	var error_pt: int;
	var limit = data.length;

	def consume_u8(name: string) -> u8 {
		if (!check(pos, 1, name)) return 0;
		var result: u8 = data[pos++];
		if (Debug.verbose) trace(pos - 1, name);
		return result;
	}
	def consume_u16(name: string) -> u16 {
		if (!check(pos, 2, name)) return 0;
		var b0: u32 = data[pos];
		var b1: u32 = data[pos + 1];
		pos += 2;
		var result = u16.!((b1 << 8) | b0);
		if (Debug.verbose) trace(pos - 2, name);
		return result;
	}
	def consume_u32(name: string) -> u32 {
		if (!check(pos, 4, name)) return 0;
		var b0: u32 = data[pos];
		var b1: u32 = data[pos + 1];
		var b2: u32 = data[pos + 2];
		var b3: u32 = data[pos + 3];
		pos += 4;
		var result = (b3 << 24) | (b2 << 16) | (b1 << 8) | b0;
		if (Debug.verbose) trace(pos - 4, name);
		return result;
	}
	def consume_u32v(name: string) -> (int, u32) {
		var t = read_u32v(pos);
		pos += t.0;
		if (Debug.verbose) trace(pos - t.0, name);
		return t;
	}
	def consume_string(name: string) -> string {
		var pt = pos;
		var t = consume_u32v(null), len = int.!(t.1);
		if (!ok) return null;
		if (len < 0 || len > data.length || len + pos > data.length) {
			error(pt, "string too long");
			return null;
		}
		var result = Arrays.range(data, pos, pos + len);
		pos += len;
		if (Debug.verbose) trace_string(pt, name, result);
		return result;
	}

	def read_u8(offset: int) -> u8 {
		if (!check(offset, 1, null)) return 0;
		return data[offset];
	}
	def read_u16(offset: int) -> u16 {
		if (!check(offset, 2, null)) return 0;
		var b0: u32 = data[offset];
		var b1: u32 = data[offset + 1];
		return u16.!((b1 << 8) | b0);
	}
	def read_u32(offset: int) -> u32 {
		if (!check(offset, 4, null)) return 0;
		var b0: u32 = data[offset];
		var b1: u32 = data[offset + 1];
		var b2: u32 = data[offset + 2];
		var b3: u32 = data[offset + 3];
		return (b3 << 24) | (b2 << 16) | (b1 << 8) | b0;
	}
	def read_u64(offset: int) -> u64 {
		if (!check(offset, 8, null)) return 0;
		var b0: u32 = data[offset];
		var b1: u32 = data[offset + 1];
		var b2: u32 = data[offset + 2];
		var b3: u32 = data[offset + 3];
		var wl = (b3 << 24) | (b2 << 16) | (b1 << 8) | b0;
		var b4: u32 = data[offset + 4];
		var b5: u32 = data[offset + 5];
		var b6: u32 = data[offset + 6];
		var b7: u32 = data[offset + 7];
		var wh = (b7 << 24) | (b6 << 16) | (b5 << 8) | b4;
		return u64.!(wh) | wl;
	}
	def read_u32v(offset: int) -> (int, u32) {
		var max = offset + MAX_U32V_LENGTH;
		if (max > data.length) max = data.length;
		var result: u32, b: byte;
		for (i = offset; i < max; i++) {
			b = data[i];
			result |= (b & 0x7Fu) << u5.!(7 * (i - offset));
			if (b < 0x80) return (1 + (i - offset), result);
		}
		// rare case of hitting max.
		var len = max - offset;
		match (len) {
			0: error(offset, "u32v expected, fell off end");
			1, 2, 3, 4: if (b >= 0x80) error(offset, "u32v unterminated, fell off end");
			5: if ((b & 0xF0) != 0) error(offset, "stray upper bits in u32v");
		}
		return (len, result);
	}
	def read_u64v(offset: int) -> (int, u64) {
		var max = offset + MAX_U64V_LENGTH;
		if (max > data.length) max = data.length;
		var result: u64, b: byte;
		for (i = offset; i < max; i++) {
			b = data[i];
			result |= (b & 0x7Fu) << u6.!(7 * (i - offset));
			if (b < 0x80) return (1 + (i - offset), result);
		}
		// rare case of hitting max.
		var len = max - offset;
		match (len) {
			0: error(offset, "u32v expected, fell off end");
			1, 2, 3, 4, 5, 6, 7, 8, 9: if (b >= 0x80) error(offset, "u32v unterminated, fell off end");
			10: if ((b & 0xF0) != 0) error(offset, "stray upper bits in u32v");
		}
		return (len, result);
	}

	private def check(offset: int, len: int, name: string) -> bool {
		if (offset + len >= data.length) {
			trace_off_end(offset, name);
			return ok = false;
		}
		return true;
	}

	def error(offset: int, msg: string) -> Decoder {
		ok = false;
		limit = 0;
		if (error_msg == null) {
			error_pos = offset;
			error_msg = msg;
		}
		return this;
	}

	def error1<T>(offset: int, msg: string, v: T) -> Decoder {
		return error(offset, String.format1(msg, v));
	}

	private def trace_off_end(start: int, name: string) {
		trace_prefix(start, data.length, name);
		def b = Debug.buf;
		b.puts("<unexpected end>\n");
		Debug.flush();		
	}

	private def trace(start: int, name: string) {
		if (name == null) return;
		def b = Debug.buf;
		trace_prefix(start, pos, name);
		b.ln();
		Debug.flush();
	}

	private def trace_string(start: int, name: string, result: string) {
		trace_prefix(start, pos - result.length, name);
		def b = Debug.buf;
		b.puts(" (");
		b.puti(result.length);
		b.puts(" bytes) \"");
		var max = result.length;
		if (max > 20) max = 20;
		for (i < max) {
			var ch = result(i);
			if (ch < 32 || ch > 127) ch = '.';
			b.putc(ch);
		}
		b.puts("\"");
		b.ln();
		Debug.flush();
	}

	private def trace_prefix(start: int, limit: int, name: string) {
		def b = Debug.buf;
		b.puts(" +");
		b.puti(start);
		b.pad(' ', 10);
		b.puts(name);
		b.pad(' ', 35);
		for (i = start; i < limit; i++) {
			b.putc(' ');
			var d = data[i];
			b.putc(hexMap[d >> 4]);
			b.putc(hexMap[d & 0xF]);
		}
	}
}
enum Section(code: byte, id: string) {
	Unknown(0, null),
	Type(1, null),     // Function signature declarations
	Import(2, null),   // Import declarations
	Function(3, null), // Function declarations
	Table(4, null),    // Indirect function table and other tables
	Memory(5, null),   // Memory attributes
	Global(6, null),   // Global declarations
	Export(7, null),   // Exports
	Start(8, null),    // Start function declaration
	Element(9, null),  // Elements section
	Code(10, null),    // Function bodies (code)
	Data(11, null),    // Data segments
	Names(12, "name")  // Names
}
def RETURN_VOID: Array<ValueType> = [];
def MAX_GROW_SIZE = 100000;
class ModuleDecoder extends Decoder {
	new(data: Array<byte>) super(data) { }
	def module = WasmModule.new();
	def WASM_MAGIC = 0x6d736100u;
	def WASM_VERSION = 0x0Du;

	def read() -> WasmModule {
		// ===== Parse the header =================================
		var magic = consume_u32("wasm magic");
		if (magic != WASM_MAGIC) {
			error(0, "invalid magic word");
			return finish();
		}

		var version = consume_u32("wasm version");
		if (version != WASM_VERSION) {
			error(4, "invalid version");
			return finish();
		}

		// ===== Parse the sections ===============================
		while (pos < data.length) {
			if (!ok) break;
			var section = consume_u8("section code");
			var length = int.!(consume_u32v("section length").1);
			match (section) {
				Section.Type.code: ;
				Section.Import.code: ;
				Section.Function.code: ;
				Section.Table.code: ;
				Section.Memory.code: ;
				Section.Global.code: ;
				Section.Export.code: ;
				Section.Start.code: ;
				Section.Element.code: ;
				Section.Code.code: ;
				Section.Data.code: ;
				_: ;
			}

		}

		return finish();
	}
	def readMemory(length: int);
	def readSignatures(length: int) {
		readRepeated("signatures count", "signature", module.signatures, readSignature);
	}
	def readSignature(index: int) -> FunctionSig {
		var pt = pos;
		var form = consume_u8("type form");
		if (form != TypeConstructors.Function.code) error(pt, "invalid type, expected function");
		var param_count = consume_u32v("param count").1;
		var ptypes = Array<ValueType>.new(int.!(param_count));
		for (j < param_count) {
			var ptype = consume_u8("param type");
			ptypes(int.!(j)) = toValueType(ptype);
		}
		var ret_count = consume_u32v("return count").1;
		var rtypes = Array<ValueType>.new(int.!(ret_count));
		for (j < ret_count) {
			var rtype = consume_u8("return type");
			rtypes(int.!(j)) = toValueType(rtype);
		}
		return FunctionSig.new(rtypes, ptypes);
	}
	def readGlobals(length: int) {
		readRepeated("globals count", "global", module.globals, readGlobal);
	}
	def readGlobal(index: int) -> WasmGlobal;
	def readDataSegments(length: int) {
		readRepeated("data segments count", "data segment", module.data_segments, readDataSegment);
	}
	def readDataSegment(index: int) -> WasmDataSegment;
	def readStartFunction(length: int) {
		module.startFunction = int.!(consume_u32v("start function").1);
	}
	def readImportTable(length: int) {
		readRepeated("imports count", "import", module.imports, readImport);
	}
	def readImport(index: int) -> WasmImport;
	def readExportTable(length: int) {
		readRepeated("exports count", "export", module.exports, readExport);
	}
	def readExport(index: int) -> WasmExport;
	def readFunctionSignatures(length: int) {
		readRepeated("function signatures", "function signature", module.functions, readFunctionSignature);
	}
	def readFunctionSignature(index: int) -> WasmFunction {
		var func = WasmFunction.new(index);
		func.sig_index = consume_sig_index();
		return func;
	}
	def readFunctionBodies(length: int) {
		readRepeated("function bodies", "function", module.functions, readFunctionBody);
	}
	def readFunctionBody(index: int) -> WasmFunction {
		var length = consume_u32v("body length").1;
		var func = module.functions.get(index);
		if (ok) {
			func.body_start = pos;
			pos += int.!(length);
			func.body_end = pos;
		}
		return func;
	}
	def readNames(length: int) {
		readRepeated("names", "name", module.functions, readName);
	}
	def readName(index: int) -> WasmFunction {
		var name = consume_string("function name");
		var func = module.functions.get(index);
		if (ok) func.name = name;
		var local_count = consume_u32v("locals count").1;
		for (i < local_count) {
			consume_string("local name");
		}
		return func;
	}

	def consume_sig_index() -> int {
		var pt = pos;
		var index = consume_u32v("signature index").1;
		if (index >= module.signatures.length) {
			error1(pt, "invalid signature index %1", int.!(index));
		}
		return int.!(index);
	}
	def consume_func_index() -> int {
		var pt = pos;
		var index = consume_u32v("function index").1;
		if (index >= module.functions.length) {
			error1(pt, "invalid function index %1", int.!(index));
		}
		return int.!(index);
	}
	def readRepeated<T>(name_count: string, name: string, vec: Vector<T>, read: int -> T) {
		var count = consume_u32v(name_count).1;
		if (count < MAX_GROW_SIZE) vec.grow(int.!(count));
		for (i < count) {
			var index = int.!(i);
			if (!ok) break;
			if (Debug.verbose) {
				def b = Debug.buf;
				b.puts(name);
				b.puts("[");
				b.puti(index);
				b.puts("]\n");
				Debug.flush();
			}
			var result = read(index);
			vec.grow(index + 1);
			vec.set(index, result);
			if (vec.length <= index) vec.length = index + 1;
		}
	}
	def toValueType(b: byte) -> ValueType {
		match (b) {
			ValueType.I32.code: return ValueType.I32;
			ValueType.I64.code: return ValueType.I64;
			ValueType.F32.code: return ValueType.F32;
			ValueType.F64.code: return ValueType.F64;
		}
		error(pos - 1, "invalid value type");
		return ValueType.I32;
	}
	def finish() -> WasmModule {
		if (ok) return module;
		var msg = String.format2("+%1: %2", error_pos, error_msg);
		System.error("ModuleError", msg);
		return null;
	}
}
