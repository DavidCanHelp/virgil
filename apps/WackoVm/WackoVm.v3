var flagM: bool;
var flagS: bool;
var flagI: bool;
var flagF: bool;
var flagE: bool;

def main(args: Array<string>) -> int {
	for (fileName in args) {
		if (fileName.length > 0 && fileName[0] == '-') {
			for (ch in fileName) {
				if (ch == 'm') flagM = true;
				if (ch == 's') flagS = true;
				if (ch == 'i') flagI = true;
				if (ch == 'f') flagF = true;
				if (ch == 'e') flagE = true;
				if (ch == 'v') Debug.verbose = true;
			}
			continue;
		}

		var data = System.fileLoad(fileName);
		if (data == null) System.error("FileNotFound", fileName);
		System.puts("Loading ");
		System.puts(fileName);
		System.puts("...\n");
		var decoder = ModuleDecoder.new(data);
		var module = decoder.read();
		resolveSignatures(module);
		var p = ModulePrinter.new(module);
		p.flagM = flagM;
		p.flagS = flagS;
		p.flagI = flagI;
		p.flagF = flagF;
		p.flagE = flagE;
		p.print();
	}
	return 0;
}
def resolveSignatures(module: WasmModule) {
	for (i < module.imports.length) {
		var imp = module.imports[i];
		if (imp.sig_index < module.signatures.length) {
			imp.sig = module.signatures[imp.sig_index];
		}
	}
	for (i < module.functions.length) {
		var function = module.functions[i];
		if (function.sig_index < module.signatures.length) {
			function.sig = module.signatures[function.sig_index];
		}
	}
}