// Copyright 2013 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def INTSIZE = 64;
def BYTESIZE = INTSIZE * 4;
def buf = Array<byte>.new(BYTESIZE);

// A random number generating utility that dumps random bytes to the console.
def main(args: Array<string>) {
	if (args.length == 0) runForever();
	for (a in args) {
		var i = Int.parseDecimal(a);
		if (i.passed) Random.seed = Random.seed ^ i.val;
	}
	var i = Int.parseDecimal(args(0));
	if (i.passed) run(i.val);
	else System.error("UserError", "invalid length specified");
}
def run(bytes: int) {
	while (bytes >= BYTESIZE) {
		for (i < INTSIZE) put(i * 4);
		System.puts(buf);
		bytes = bytes - BYTESIZE;
	}
	while (bytes-- > 0) {
		System.putc(byte.!(Random.next()));
	}
}
def runForever() {
	while (true) {
		for (i < INTSIZE) put(i * 4);
		System.puts(buf);
	}
}
def put(index: int) {
	var val = Random.next();
	buf(index + 0) = byte.!(val);
	buf(index + 1) = byte.!(val >>> 8);
	buf(index + 2) = byte.!(val >>> 16);
	buf(index + 3) = byte.!(val >>> 24);
}
